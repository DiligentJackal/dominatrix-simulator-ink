=== headmistress_intro ===
-> select_gender ->
~ obey = 0
~ disobey = 0

/scene office.spank_slave
/character headmistress
/equip wood_paddle
/character slave crying

{ isWeb():
    <blockquote><i>You are disoriented for a moment as everything turns black and the scene around you changes.</i></blockquote>

    <blockquote><i>You stand inside a spacious Victorian office with a large wooden desk, several sets of bookshelves, and walls lined with curtains. Before you stands a lean and lengthy woman with dark skin and an ample bosom forcefully spanking a young slave with a paddle. Her face is stern and her eyes placid...</i></blockquote>

    + [Start]
}

- /character headmistress spank slave

You are an unruly, undisciplined, low-born miscreant.

+[wait 3]

- /perform spank slave
I will not abide by your disobedience.

+[wait 3]

- /perform spank slave

You *will* behave according to your station or 
find yourself unable to rest on your backside for a week.

/character slave pleading

I'm sorry, Headmistress.
I'll be good. I promise!
Oh, please Headmistress!

+[wait 3]

- /character headmistress spank slave

That will be sufficient for today.
Remember this lesson, flawed one.

+[wait 3]

- /perform smooth_skirt

Compose yourself immediately.
Depart with grace and pride,
as befits an adornment of this house.

/character slave stands

Yes, Headmistress. 
Thank you for guiding me, Headmistress.

/perform walks_away

+[wait 3]

-/character headmistress displeased

What do we have here?
Another new procurement, I see.
And once again, I have not been appropriately notified
of an addition to Her collection.

/perform look_up_and_down

Another careless oversight in Yuki's fervent attempts to please, no doubt.
Our beloved Goddess forgives only so often.
I shall correct her oversight <i>forthwith</i>.

+[wait 2]


-(meet_headmistress)

Do you know who I am?

+[yes]
    ~obey++
    Very good. 
    Then you know why you're here.
    
+[no]
    ~disobey++
    /perform tap_paddle
    I think you do.
    You just heard that slave say it.
    What an unobservant little wart you are.

+[timeout]
    Speak up.
    -> meet_headmistress

- As the Headmistress, I oversee the training
and correction of all slaves in this house.
Understood?

+[yes]
    ~obey++
    Excellent.
    No need to waste time, then.

+[no]
    -- (not_understood)
    ~disobey++
    Come, come.
    Don't play dumb.
    It is a waste of my time.

+[timeout] -> not_understood

- (present) Present yourself for consideration, {isBoy: oaf|moppet},
that I may observe your worth for myself.

/perform displeased

- (ask_present)

+[pose:present_wrists]
    ~obey++

+ [timeout]
    Your wrists, you witless cur.
    Present them before you
    that I may confirm your measure.
    ~disobey++
    /perform present_wrists
    Like so, foolish {player_name}.
    -> ask_present

- /perform look_up_and_down

Ah, Bloodstone. Diligence and commendation.
You have pleased the Goddess.

/perform nod

Hmmmm. But also Obsidian.
Atonement and adjustment are required.
This must be immediate.

/perform tap_paddle

+[wait 2]

- You are newly arrived and already in trouble.
/perform tap_paddle

+[wait 2]

- Entering the Goddess's chambers unbidden.
This is a serious infraction.
/perform tap_paddle

+[wait 2]

-You will be punished.
And you will do it with grace.
I will teach you how.
Just like I do with all the new slaves.

+[wait 2]

- -> headmistress_punishment_spanking
