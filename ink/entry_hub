=== entry_hub ===

-> select_gender_check ->

{ deviation == puppyplay:
    -> yuki_task_puppyplay
}

~ reward_just_removed = false
~ punishment_just_removed = false
~ setHub(None)

{ inTutorial: -> tutorial_entry }
{ deviation == tutorial || deviation == demo: -> demo_end}

-> normal_entry


= normal_entry

/scene entry.hub
/music entry
/ambient rain

+ {LIST_VALUE(goddess_hub_status)} [move: Goddess ({goddess_hub_status})]
    -> tryEnter(goddess_hub_status, -> goddess_hub, -> exit)
+ [move: Yuki] -> yuki_hub
+ [move: Nega] -> nega_hub
+ [move: Headmistress] -> headmistress_hub


= tutorial_entry

{ tutorial_step == at_yuki_intro:
    ~ punishments = 1
    ~ rewards = 1
    ~ yuki_shortcut = true
    -> move_yuki
}

{ tutorial_step == at_hm_intro:
    ~ punishments = 1
    ~ rewards = 1
    ~ hm_shortcut = true
    -> move_headmistress_or_yuki
}

{ tutorial_step == at_nega_intro || tutorial_step == at_hm_bonus:
    ~ punishments = 0
    ~ rewards = 1
    ~ hm_bonus_shortcut = true
    -> move_headmistress_or_nega
}

{ tutorial_step == at_goddess_outro:
    ~ punishments = 0
    ~ rewards = 0
    -> demo_end
}

/logError Shouldn't be in the entry at this point.
~ punishments = 0
~ rewards = 0
-> demo_end


= move_yuki

-> select_gender_check ->

/scene entry.intro1
/music yuki
/ambient rain

{ isWeb(): 
    <blockquote><i>You are disoriented for a moment as everything turns black and the scene around you changes.</i></blockquote>

    <blockquote><i>You stand inside a large Victorian foyer with a sweeping staircase and several sets of doors leading off of it. The room is fairly well lit, but all of the doors are shrouded in shadows. Before you stands a lithe, young woman dressed in a maid's uniform. Beaming a bright smile, she greets you...</i></blockquote>
}

+ [move: Yuki] -> yuki_hub


= move_headmistress_or_yuki

/scene entry.hub
/music entry
/ambient rain

+ [move: Headmistress] -> headmistress_hub
+ [move: Yuki] -> yuki_hub

= move_headmistress_or_nega

/scene entry.hub_empty
/music entry
/ambient rain

+ [move: Headmistress] -> headmistress_hub
+ [move: Nega] -> nega_intro



