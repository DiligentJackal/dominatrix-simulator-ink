VAR headmistress_spanking_target_count = 10
VAR headmistress_spanking_implement = "hand"
VAR headmistress_spanking_count = 0
VAR headmistress_spanking_break_count = 0
VAR headmistress_spanking_mistakes = 0

=== headmistress_punishment_spanking ===

~ intro = headmistress_punishment_spanking == 1

TODO: Support shuffling swats and equipment

// {shuffle:
// - ~ headmistress_spanking_target_count = 10
// - ~ headmistress_spanking_target_count = 25
// - ~ headmistress_spanking_target_count = 35
// }

// Just use '10' swats for now.
~ headmistress_spanking_target_count = 10

// {shuffle:
// - ~headmistress_spanking_implement = "paddle"
// - ~headmistress_spanking_implement = "hand"
// - ~headmistress_spanking_implement = "crop"
// }

// Just use the hand for now.
~headmistress_spanking_implement = "hand"

~ headmistress_spanking_break_count = 0
~ headmistress_spanking_count = 0
~ headmistress_spanking_mistakes = 0


/scene LectureHall.Punishment_Spanking
/music headmistress
/character headmistress
// /equip {headmistress_spanking_implement}
// /perform circle_desk

{intro: 
    Punishments are administered with the slave perched upon my desk. #05
- else:
    For your transgressions, #01
    a spanking is in order. #02
}

/character headmistress point_paddle_at_desk
Alight forthwith! #08

- (get_on_desk)
/perform idle

+ [move:get_on_desk]
    Pleasingly executed. #09
+ [timeout]
    -- (get_on_desk_timed_out)
    Our guests are waiting.<br/>Tut. Tut. #12
    -> get_on_desk
-
{not get_on_desk_timed_out:
    ~obey++
    /perform look_up_and_down
    Observe the eagerness with which our chattel attempts to comply. #13
    This behaviour pleases Her. #14
- else:
    ~disobey++
    /perform displeased
    Note the hesitation. A sure sign of poor breeding. #15
    Such offends the Goddess and must be striken from the specimen. #16
}

- /perform point_down
 Now, kneel, slave. #225

- (kneel)
- (kneel_tries)

+ [pose:kneel]
+ { kneel_tries < 2 } [timeout] -> kneel
//+ { kneel_tries >= 2 } [timeout]

- -> Headmistress_GoodJob ->


{intro: -> punish_intro | -> punish_continue}

= punish_intro

- /perform one_finger
The muscles of the ass must remain relaxed between swats. #44
/perform behold
This provides an exceptionally pleasing display when they clench #45
in response to the impact of my admonishments. #46
//perform tap_paddle
/perform serious

- /character headmistress smooth_skirt
But enough of this pretense. #48
/character crowd murmur
/ambient crowd
/character headmistress curious
We aren't here just for you. #49
/character headmistress serious
We have our entire class to enlighten! #50

- 
//Let us show our audience #51
//the fine display you have for us. #52
/character crowd cheers_loud
/action raise_curtains

-> punish_continue

= punish_continue

/character headmistress arm_raise

Welcome esteemed guests! #53

/perform idle
Our subject today has earned a spanking. #54

/perform serious
{headmistress_spanking_implement == "paddle":
    I will use a paddle made of mahogany. #55
    An excellent, hard wood ideal for leaving #56
    bright red welts on the flesh of disobedient slaves. #57
}

{headmistress_spanking_implement == "hand":
    My own hand will do today. #58
    I want to relish the crisp strike of my hand #60
    and the quivering sound of flesh meeting flesh. #61
}

{headmistress_spanking_implement == "crop":
    For today's punishment, I have selected #62
    a slender and rigid crop. #63
    The pattern of stinging pain that will burst forth #64
    from the skin is exotic to behold! #65
}

/character crowd oos 

- /character headmistress behold
Listen to that excitement, {player_name}. #67
Your ass will provide a fine demonstration today. #68

/perform open_arms_shrug
Are they not fortunate to have one such as you to observe? #69

+[yes]
    ~obey++
    /perform serious
    Perform well and your punishment marker will be removed. #70

+[no]
    ~disobey++
    /perform displeased
    It is a shame you do not value the learning of others. #71
    
- /perform behold
As always, purrs, moans, and words of encouragement #73
are permitted during the punishment lesson. #74

/character crowd oos
/character headmistress idle

What say you, {isBoy: toad|strumpet}? #79

{cycle:
    - Will our esteemed guests be enthralled #80
     by {isBoy:your turgid bouncing cock?|the glistening of your cunt?} #81
    - Do you feel their eyes on you? #82
    - Are you throbbing in anticipation? #83
    - Do you want to touch yourself? #84
    - Does pain turn you on? #85
}

+ [yes]
    ~ obey++
    /perform curious
    /character crowd cheers 
    /character headmistress idle
    So eager for your debasement. #86
    Most pleasing. #87

+ [no]
    ~ disobey++
    /perform thoughtful
    /character crowd boos
    /character headmistress idle
    A disappointing response for our guests. #88
    Rectify this immediately, slave. #226

 - Rub yourself until your {isBoy: shaft is hard|slit is wet}. #227
 
+ [wait:10]

- -> Headmistress_masturbation_comments->

/wait 0.01
Pray continue. #228

+ [wait:10]

- -> Headmistress_masturbation_comments->

/wait 0.01
A little more, I think. #229
 
+ [wait:10]

- (accept_punishment) What say you, slave, will your {isBoy: rod|folds} please our guests? #230

+ [yes]
  Excellent. Then let us continue. #231
  
+ [no]
  A disappointment. We shall continue anyways. #232
  
- /perform smooth_skirt

//And finally, we come to the heart of the matter. #91
- Do you accept that you must be punished? #92

- (ask_punish_ready)

+[yes]
    ~ obey++
    /perform arm_raise
    Your acceptance is most becoming. #93
    /character crowd cheers
    /character headmistress idle

+[no]
    ~ disobey++
    /character crowd boos
    /character headmistress suspicious
    Foolish, little slave. #94
    
    It will be over quickly and your compliance will please the Goddess. #95
    /perform inspect
    Shall we continue with your punishment? #96

    ++[yes]
        ~ obey++
        /perform serious
        Magnificent. Let us begin. #97

    ++[no]
        ~ disobey++
        /character crowd angry_crowd
        /character headmistress unhappy
        Make haste from my sight! #98
        You have squandered my time and my attentions. #99
        I will not suffer fools. #100
        You've just earned yet another punishment. #101
        ~ addPunishment()
        ~ headmistress_apologize = true
        Now leave. #102
        /perform point_to_leave
        -> exit

+[timeout] 
    /perform pleased
    You have had enough time to think. #103
    Your punishment must begin. #104
    Do you agree? #105
    -> ask_punish_ready

- /notify title="<color=red>Pro Tip!</color>" text="For a more realistic experience, set down one controller and spank yourself hard when you hear the **slap**. Or better yet, have a very close friend to it for you. ;)<br/><br/>Understood?"

+ [yes]

- {intro: -> spank_instructions}

- /perform serious
Because I am generous, #234
I will offer you a chance to practice first. #106
/perform amused
Would you welcome that, {isBoy: mongrel|moppet}? #107
~ headmistress_difficulty = 1

+[yes]
    -> spank_instructions
+[no]
    /perform nod
    An expert, are we? We will see about that. #108
    -> spank_continue


= spank_instructions

//With each swat, you will show me clearly #109
//just how much you feel it. #110
/perform idle

// hand, crop, paddle

When you hear the slap of my {headmistress_spanking_implement} #111
and feel its sting upon your ass, #112
I want to see that empty head of yours jerk up and back #113
into a proud and upright position. #114

+[wait]

- (hands_and_knees_timeout)
/perform curious
Now. Onto your hands and knees, bauble. #233

+ [pose:hands_and_knees]

- /perform amused

The back must be delicately arched, #21
presenting the most ideal access #22
to the object of discipline: the buttocks. #23

/perform serious
Lift your backside high, {isBoy: mongrel|moppet}. #24

/perform paddle_desk

- Tell me, lump, is that as high as you can lift your hindquarters? #26

+[yes]
    ~obey++
    /perform lean_forward
    I disagree. #27
    Arch just a bit more. #28
    /perform nod
    You will remain in this position without fail. #30

+[no]
    ~disobey++
    /perform displeased
    You serve yourself and our Goddess poorly, heathen. #31
    Raise your ass! #32

+[timeout]
    /perform annoyed
    What are you waiting for? #33
    Your back must strain with the effort of raising your derrier, chattel. #34

- /perform satisfied
-> Headmistress_GoodJob ->

- /character headmistress lean_forward

Time to rehearse. #118

/scene LectureHall.HeadmistressAtBack

+ [wait]

~temp spanks = 0

- (try_spank)
/character headmistress paddle_ass

+ [react-success]
    // This lets us count the number of successful attempts.
    ~spanks++
    
    // Once there have been 3 good spanks, we continue.
    {spanks < 3:
        -> Headmistress_GoodJob ->
        -> try_spank
    - else:
        Just so. #119
        -> present_ass
    }
+ [react-fail]
    // This lets us count the number of failed attempts.
    ~headmistress_spanking_mistakes++

    // We will continue if they fail 3 times.
    { headmistress_spanking_mistakes < 3:
        Not good enough. Try again. #120
        -> try_spank
    - else:
        ~disobey++
        That will do for now. #121
        We will work on that in the future. #122
    }
    
    ~ headmistress_difficulty = 0

- 

{ headmistress_spanking_mistakes > 0:
    ~disobey++
- else:
    ~obey++
}

// Reset spanking mistakes to track them in the next sections.
~ headmistress_spanking_mistakes = 0

- (present_ass)

/perform curious
Stand up and present your ass for our guests. #235 
Let us inspect how well your skin blooms with color. #236

+ [pose:present_ass]

- /perform idle
How exquisite. #237

/lookat crowd

Do you see my students? #238
Look at how the round orbs go flush #239
with excitement at my ministrations! #240

/lookat player

- (rotate_left_timeout)
Turn to the left. #241
-> Headmistress_GoodJob ->

+ [pose:present_ass_left]

- /character crowd oos

- (rotate_right_timeout)
/character headmistress
Now turn to the right. #242
-> Headmistress_GoodJob ->

+ [pose:present_ass_right]

- /character crowd gasps

/character headmistress behold
Ahhh. I am pleased! #243

- (hands_and_knees_timeout2)
/perform curious
Back onto your hands and knees, slave. #244

+ [pose:hands_and_knees] -> spank_continue

= spank_continue

/perform idle
Let us begin in earnest! #245
/character crowd cheers_loud

+ [wait]

- /scene LectureHall.HeadmistressAtBack

// /character headmistress walk_to_back_of_desk

/character crowd murmur

/character headmistress idle

// 10, 25, 35
Prepare yourself for {headmistress_spanking_target_count} swats, {player_name}. #127

- (spank)
/character headmistress paddle_ass

+ [react-success]
    ~ headmistress_spanking_count++
    ~ headmistress_spanking_break_count++
    
    {headmistress_spanking_break_count %3 == 0: 
    -> GoodSpank ->
    }
    
    {headmistress_spanking_break_count >= 6:
    
        // Check mistakes for obey or disobey adjustment
        {headmistress_spanking_mistakes > 0:
            ~ disobey++
        - else:
            ~ obey++
        }
        
        // Reset the mistake counter.
        ~headmistress_spanking_mistakes = 0
        
        -> SpankBreak ->
    }
    {headmistress_spanking_count < headmistress_spanking_target_count: -> spank}
+ [react-fail] -> BadSpank -> spank

- /character headmistress satisfied
Ahhhh. Now, don't you feel better, {player_name}? #128
    +[yes]
    /perform pleased
    ~ obey++
    That is because you desire #129
    to be an obedient little toy. #130
    
    +[no]
    /perform amused
    ~disobey--
    Come now, trinket, you comported yourself quite well. #131
    You should be proud of your performance. #132
    
- /perform circle_player
Your ass has blossomed into an objet d'art. #133

- {obeyed(): -> GoodEnding | -> BadEnding}


= GoodEnding
/perform sassy
In fact, your performance has been so deliciously obedient #134
I wonder if you would like to perform further for our audience? #135

    +[yes]
    /perform confused
    /character crowd claps
    /character headmistress
    Ahhhh. What a crowd pleaser! #136
    /character headmistress idle
    -> Headmistress_player_masturbate ->
    
    +[no]
    /perform serious
    A shame you do not have more in you, #137
    but you have done well. #138
    /perform point_down
    You may descend and return to Mistress Yuki-Onna with my compliments. #139
//need move off desk action
    /character crowd cheers
    /character headmistress
    -> FinalEnding
    
- -> FinalEnding


= BadEnding
/perform sassy
You may have finished enduring your punishment, #140
but your overall performance was quite unsatisfactory. #141
Perhaps you would rather play with yourself for your new fans? #142

    +[yes]
    /perform confused_nega
    Too bad. Then you should have thought of that sooner. #143
    /character crowd boos
    /character headmistress
    -> FinalEnding
    
    +[no] 
    /perform amused
    Too bad. That's exactly what you're going to do. #144
    /character crowd cheers
    /character headmistress
    -> Headmistress_player_masturbate

= FinalEnding

/character headmistress
/perform nod    
Consider one demerit paid in full. #145
/character crowd cheers_loud
/character headmistress
/perform idle

~ removePunishment()


- (stand_straight)
- (stand_straight_tries)

I give you leave to depart. #146

+ [pose:raised_chin]


- + [wait:2]

// If the player has never seen nega-yuki,
// send them off to be tempted by her directly.
- {not nega_hub: -> nega_hub | -> exit }


// Tunnels


= BadSpank
~ headmistress_spanking_mistakes++
/character headmistress displeased
{shuffle:
    - Terrible. We'll have to repeat that one. #147
    - Tsk. Tsk. You can do better. #148
    - Lift that head, cretin. #149
    - Head up or the next one will land harder. #150
    - Are you even trying? #151
    - Your lack of grace is stunning. #152
    - Where is your pride? Try harder. #153
    - Keep those toes pressed flat on the desk. #154
    - Squirming is not allowed. #155
    - I expect a better performance. #156
    - Your audience is disappointed. #157
    - /character crowd boos 
    - /character crowd gasps
    - Do not dodge my spank, slave! #246
    - Uggh. #247
    - You are putting on a poor display. #248
    - You have a long ways to go. #249
    - I can see you will require much instruction. #250
    -
    -
    -
    -
    }
    /character headmistress
->->

= GoodSpank

{shuffle:
    - A fine welt is forming. #158
    - Music to my ears. #159
    - Perhaps I should be counting. #160
    - A red bottom is a pleasing bottom. #161
    - I do so love instructing slaves in proper behavior. #162
    - I never tire of this. #163
    - I suspect that one hurt. #164
    - /character crowd oos
    - /character crowd gasps
    - /character headmistress mmms
    - Brilliant! No doubt my guests are impressed by your display. #165
    - Have you been keeping track? #166
    - Notice the beads of sweat forming on the brow. #167
    - Remember to breathe, {isBoy: runt|poppet}. #168
    - Notice how the {isBoy:shaft bounces|nether lips clench}. #169
    - The light is catching the dewy sheen on the skin most magnificently. #170
    - Observe the {isBoy: tip|slit} begin to glisten. #171
    - Oooo. Look at how the anus puckers. #172
    - No doubt several of my students are stimulating themselves right now. #173
    - Some of you are squirming with remembrance right now. #174
    - The nether regions must be fully in view. #35
    - Knees must be sufficiently apart to allow for clear observation. #36
    - All care must be taken to assure #124
    no lasting harm is inflicted... #125
    unless permanency is desired, of course. #126
    - Feet must remain flat with no obscene curling of the toes. #47
    -
    -
    -
    -
}
/character headmistress
->->


= SpankBreak
~ headmistress_spanking_break_count = 0

- /scene LectureHall.HeadmistressAtFront

- {cycle: 
    - You may rest back on your heels, slave. #251
    - Sit upright for your fans, slave. #252
    - Raise yourself up and sit upon your heels, slave. #253
    }

- (kneel_break)
- (kneel_break_tries)

+ [pose:kneel]
+ { kneel_break_tries < 2 } [timeout] -> kneel_break
//+ { kneel_break_tries >= 2 } [timeout]

- -> Headmistress_GoodJob ->

{ shuffle:
    - -> break01 -> 
    - -> break02 ->
    - -> break03 ->
    //- -> break04 ->
}

+ [continue]

- {cycle:
    - Back on your hands and knees, slave. #254
    - Resume your spanking position, slave. #255
    - Present your ass, {isBoy: runt|strumpet}. #256
}

+ [pose:hands_and_knees]

- /scene LectureHall.HeadmistressAtBack
->->

= break01
/perform serious
Every now and then, the object of correction #175
must be given a moment to compose themself. #176
/character crowd murmur

/character headmistress one_finger
However, this time should not be squandered with mere rest. #177

/perform idle
Reach down and stroke your {isBoy: shaft|folds}. #178
/character crowd gasps
/character headmistress
Punishments must be associated with sensual excitation. #179

Does it feel good to touch yourself, {isBoy: toad|strumpet}? #180
//need moans or some other sexual sound during this scene
+[yes]
    ~ obey++
    /perform nod
    As it should. #181
    You may continue until I instruct you otherwise. #182

+[no]
    ~ disobey++
    What a shame. Do it anyways. #183

- /character headmistress idle
+[wait:10]
- -> Headmistress_masturbation_comments->

+[wait:10]
- -> Headmistress_masturbation_comments->

+[wait:10]
- -> Headmistress_punishment_resumes->

->->

= break02
/perform one_finger
Remember to intersperse strikes with caresses. #184

/perform amused_nega
The inflamed flesh is delightfully sensitive #185
and should be titillated as much as possible. #186

/perform amused
The tantalizing drag of fingers on welted skin #187
regularly sends shivers through the subject. #188

/perform idle
Would you enjoy that, {player_name}? #189

+[yes]
    /perform head_toss
    ~ obey++
    Sorry, shivers are reserved for reward spankings only. #190

+[no]
    /perform amused
    ~ disobey++
    Good. Then you will be pleased to know that #191
    such kind treatment is reserved for reward spankings only! #192

- -> Headmistress_punishment_resumes ->

->->

= break03
Perhaps our little foible here needs a carrot #193
dangled in front of {isBoy: him|her}. #194
/character crowd murmur

/character headmistress satisfied
Great rewards are bestowed upon the most worthy. #197

/perform curious
What do you think, {isBoy: runt|poppet}? #198
Shall I show you the kind of treatment #199
good boys and girls earn? #200

+[yes]
    ~ disobey++
    /character headmistress caress_breasts_large
    Do you imagine they are soft, {player_name}? #201
    
    ++[yes]
        ~ obey++
        /character headmistress caress_breasts_large
        Mmmm. Very soft indeed. #202
    
    ++[no]
        ~ disobey++
        /character headmistress laugh
        Do you imagine they are coarse and uneven, #203
        just like your hindquarters? #204

        +++[yes]
            ~ disobey++
            /character crowd gasps
            /character headmistress displeased
            A brazen response. #205
            Another punishment has been added to your band. #206
            ~ addPunishment()

        //player should either receive another punishment stone or an additional 5 swats

        +++[no]
            ~ disobey++
            /perform nod
            As I surmised. #207
            /perform confused
            You are a baffoon who does not know to think before acting. #209

+[no]
    ~ obey++
    /character headmistress pleased
    My my. A surprisingly chaste response #210
    from one whose {isBoy: shaft|mound} is throbbing so fully. #212
    
    /perform idle
    But a delightful one. #213
    You have not earned a reward, #214
    so why bestow a taste upon you. #215

/character headmistress
- -> Headmistress_punishment_resumes->

->->

 = break04
 /perform sassy

- -> Headmistress_punishment_resumes->

->->

= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform unhappy

Are you too good to take a punishment, {player_name}? #216
Obey instructions. #217
- (check_again)

/perform idle

+ [yes] -> check_yes
+ [no]
+ [timeout] -> check_again

- (check_terse) Well? #218

/perform angry
You merely drag out the invetitable with your delay. #219

/perform annoyed
Are you going to continue to play the reluctant {player_name}? #220

+ [yes] -> check_yes
+ [no]

- /perform bored
Out of my office, heathen. #221
I have no time for your shenanigans. #222
-> out_heathen

- (check_yes) -> Headmistress_GoodJob ->
/perform confused
There is hope for you yet. #223

-> out_heathen

= out_heathen

/perform paddle_desk
-> exit
