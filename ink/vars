// Player Variables
VAR isBoy = true
VAR player_name = "boy"
VAR player_he = "he"
VAR player_him = "him"
VAR rewards = 0
VAR punishments = 0
VAR tutorial_complete = "no"

// Lists
LIST move_status = hidden = 0, locked, unlocked, early_access, under_construction

// Temp Variables
VAR obey = 0
VAR disobey = 0
VAR intro = false
VAR scene_type = "reward"
VAR reward_just_removed = false
VAR punishment_just_removed = false

// Goddess Variables
VAR goddess_favor = 0 
VAR goddess_hub_status = hidden

// Headmistress Variables
VAR headmistress_favor = 0
VAR headmistress_hub_status = hidden
VAR headmistress_difficulty = 1
VAR headmistress_apologize = false

// Nega Variables
VAR nega_favor = 0
VAR nega_hub_status = hidden

// Yuki Variables
VAR yuki_favor = 0
VAR yuki_hub_status = unlocked
VAR yuki_puppy_difficulty = 1



// Fallback for isWeb() external function
=== function isWeb() ===
~ return web

// Fallback for isDebug() external function
=== function isDebug() ===
~ return debug

=== function addPunishment() ===
{punishments < 10:
    ~ punishments++
    /action add_punishment
    ~ return true
}

~ reward_just_removed = false
~ punishment_just_removed = false

~ return false

=== function removePunishment() ===
{punishments > 0:
    ~ punishments--
    /action remove_punishment
    ~ return true
}

~ reward_just_removed = true
~ punishment_just_removed = false

~ return false

=== function addReward() ===
{rewards < 10:
    ~ rewards++
    /action add_reward
    ~ return true
}

~ reward_just_removed = false
~ punishment_just_removed = false

~ return false

=== function removeReward() ===
{rewards > 0:
    ~ rewards--
    /action remove_reward
    ~ return true
}

~ punishment_just_removed = true
~ reward_just_removed = false

~ return false

=== function removeBigReward() ===
{rewards >= 10:
    ~ rewards -= 10
    /action remove_reward_big
    ~ return true
}

~ return false

// Fallback for getNegaDie1/2/3() external function
=== function getNegaDie1() ===
~ return getNegaDie()

=== function getNegaDie2() ===
~ return getNegaDie()

=== function getNegaDie3() ===
~ return getNegaDie()

=== function getNegaDie() ===
~ temp roll = 0

{shuffle:
    - ~roll = -1
    - ~roll = -1
    - ~roll = -1
    - ~roll = 0
    - ~roll = 1
    - ~roll = 1
}

~ return roll

=== function rollToText(roll) ===
{
    - roll == -1: Skull
    - roll == 0: Blank
    - roll == 1: Cup
}

=== function setGender(gender) ===
{
    - gender == "m":
        ~ isBoy = true
        ~ player_name = "boy"
        ~ player_he = "he"
        ~ player_him = "him"
    - gender == "f":
        ~ isBoy = false
        ~ player_name = "girl"
        ~ player_he = "she"
        ~ player_him = "her"
}

~ return

=== select_gender ===
{setGender: ->->}

/scene hallway.genderchoice
/ambient rain
/music entry

~ temp chosen = false

- (options)

+ [character-choice:male]
    ~ setGender("m")
    ~ chosen = true
    /notify text="<i>You now appear masculine.</i>" delay=10 #999
    -> options

+ [character-choice:female]
    ~ setGender("f")
    ~ chosen = true
    /notify text="<i>You now appear feminine.</i>" delay=10 #999
    -> options

+ {chosen} [move]

- ->->

=== jump_menu(-> back) ===

+ [Jump to...]

- + [Goddess Poses]
    /scene Bedroom.Intro
    /move approach
    -> goddess_intro.stand_straight
+ [Yuki Intro] -> entry_hub
+ [Headmistress Intro] -> headmistress_hub
+ [Cancel] -> back

=== summary(place, times) ===

{isWeb():
	<b>{place}</b>
	<i>P:{punishments} / R:{rewards} / You've been here {times} time{times>1:s}.</i>
}

->->

=== exit ===
{tutorial_complete == "yes": 
{shuffle:
- -> goddess_monitoring
- -> entry_hub
- -> entry_hub
- -> entry_hub
- -> entry_hub
- -> entry_hub
}
- else:
-> entry_hub
}

=== demo_end ===
/scene LogoRoom.EndScreen

+ [restart] -> game_intro.choices

=== unlock_all ===
~ yuki_hub_status = unlocked
~ headmistress_hub_status = unlocked
~ nega_hub_status = unlocked
~ goddess_hub_status = unlocked
->->

=== function obeyed() ===
~ return obey - disobey > 0
