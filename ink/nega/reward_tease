  === nega_reward_tease ===
-> select_gender_check ->

TODO: the series of questions are a little repetitive. Would be good to pull at least one of them out and put it into the punishment_tease scene.

~ setScene(reward_scene, Nega, "nega_reward_tease", -> nega_reward_tease)

/scene attic.Ready
/music nega-reward
/ambient howling-wind
/character nega idle_bored

- (tease_intro)

- /perform react_scoffs angry 50
The rules say I have to go easy on you. #06

Lucky {isBoy: prick|{isGirl: cunt|slut}}. #07


~ tries = 0
- (present)
~ tries++

/perform do_snap_fingers angry 55
Stand up straight, {isBoy: maggot|skank}, and present yourself. #102

+ [pose:present_stand]
+ { tries < 2 } [timeout] -> present
+ { tries >= 2 } [timeout]
    ~ addDisobey()
    -> Nega_CheckIfInterested
    -> present
+ [distracted] -> Nega_PayAttention -> present

-/perform react_point_at_player_angry angry 65
Don't you fucking move unless I give permission. #103


- (hold_still)
/perform idle_bored skeptical 60
Got it, {isBoy: prick|cunt}? #104

+ [yes]
    ~ addObey()
    /perform react_nod_little amused 55
    Good. #105

+ [no]
    ~ addDisobey()
    {shuffle:
        - /perform talk_threaten angry 45
        Move and I'll boot your ass out of here so fast, #106

        /perform react_crazy_gesture amused 50
        it will make your head spin. #107

        - -> Nega_Unhappy ->
        /perform idle_unhappy_stomp disgusted 70
        Move and your ass is gone! #108
    }

    -- (understood)
    /perform talk_question skeptical 50
    Understood? #109

    ++ [yes]
        ~ addObey()
        /perform do_fist_pump amused 45
        That's what I fucking thought. #110

    ++ [no]
        ~ addDisobey()
        -> exit

    ++ [timeout] -> Nega_Timeout -> understood
    ++ [distracted] -> Nega_PayAttention -> understood

 + [timeout] -> Nega_Timeout -> hold_still
 + [distracted] -> Nega_PayAttention -> hold_still

- (games)

You're not too bad, you know.

I rather enjoy our little games.

Don't you?

+ [yes]

+ [no]

+ [timeout] -> Nega_Timeout -> games

+ [distracted] -> Nega_PayAttention -> games

- There's just so many naughty things I can do to you!

Present your {hasPenis: cock|pussy} to me.
Let's see how turned on you are.

+ [pose: present_genitals]

+ [timeout]

- (object) 
Oh yah!

You like it when I make you my little sex object,
don't you?

+ [yes]
    Of course you do.
    -> Nega_Objectify ->

+ [no]
    -> Nega_Unhappy ->

+ [timeout] -> Nega_Timeout -> object

+ [distracted] -> Nega_PayAttention -> object


- (thrill)
When you go out of your way to please me, I get a little thrill!

Did you know that?

+ [yes]
    It's so satisfying watching you debase yourself.

+ [no]
    Well, try not to be an idiot and forget.

+ [timeout] -> Nega_Timeout -> thrill

+ [distracted] -> Nega_PayAttention -> thrill

-> Nega_Kneel ->

{shuffle:
    - -> tease_sniff_crotch ->
    - -> tease_worship_ass ->
    - -> tease_kiss_feet ->
}

Oh! That's right!
This is just a tease.
No more time for you, little toy.

Stand up.
I'm done with you. #52
Bye-bye!
- -> exit


= tease_sniff_crotch
/perform idle_tease amused 75
Now don't you move while I get more comfortable. #44

/scene attic.chair
/perform sex_chair_openlegs_wide amused 70

{shuffle:
    - Get over here and give my panties a good whiff. #01
    - Stick that pathetic face of yours into my crotch. #02
    And breathe in deep! #03
    - Bring your ass over here and drink in smell of my cunt! #04
    
}

/wait 
-> Nega_GoodJob.Sex_Chair ->

{shuffle:
    - Breathe in nice and deep. #05
    - Really suck in my smell. #19
    - Wallow in the reek of my pussy. #20
}

-> Nega_GoodJob.Sex_Chair ->


- (belong)
This is where you belong, isn't it {isBoy: piggy|bitch}? #21

+[yes]
    ~ addObey()
    /emotion amused 65
    Of course it is! #22

+[no]
     ~ addDisobey()
    -> Nega_Unhappy.Sex_Chair  ->
    /perform talk_annoyed angry 80
    You'll learn your place soon enough. #45

+ [timeout] -> Nega_Timeout.Sex_Chair -> belong

+ [distracted] -> Nega_PayAttention.Sex_Chair  -> belong

- 
{shuffle:
    - That's all your good for. #46
    - You're not worth anything else. #47
    - You're only use is to amuse me. #48
    - You're an insignificant toy for me to play with. #49
}

No more pussy for you.

-> Nega_Kneel ->

Let me think... what else could I do...

// That's enough! #50

// Now get the fuck away from me. #51


->->


= tease_worship_ass
TODO: create 'worship_ass' performance. Mistress turns around and pushes their ass out toward player. This scene will focus on humiliating the player for being an ass slut who's happy just smelling and nuzzling her ass.
/perform tease_ass_caress

{shuffle:
    - I know you're dying to soak up the smell of my sweaty ass.
    - I bet you can't wait to bury your face in my ass.
    - Time to worship my ass, little bitch.
}

/perform idle_present_ass

Get over here!

/wait

{shuffle:
    - Oh yah! Fill your nose with my filthy smell.
    - Breathe deep and really get in there.
    - I want to feel that nose touch my asshole!
}

/wait 
-> Nega_GoodJob->

/wait
- (good)
Feel good {isBoy: piggy|bitch}? 

+[yes]
    ~ addObey()
    /emotion amused 65
    Of course it does.

+[no]
     ~ addDisobey()
    -> Nega_Unhappy  ->
    /perform talk_annoyed angry 80
    Too bad. 
    You rolled a tease.

+ [timeout] -> Nega_Timeout -> good

+ [distracted] -> Nega_PayAttention  -> good

- 
{shuffle:
    - This is where you belong.
    - Your worth is measured by how well you please me.
    - I'll always remind you what a slut you are.
}

-> Nega_Kneel ->

/perform talk_thoughtful

Now... what else should I do to you...

->->


//consider putting in for Yuki instead
= tease_kiss_feet
TODO: Player kisses her feet for a set amount of time. Dialogue will focus on mocking them for being so eager to kiss her feet.

/perform idle_present_foot

{shuffle:
    - Mmm. My toes need some attention.
    - I bet you're dying to worship my feet.
    - I know you want to be my little foot bitch.
}

Come worship them.

/wait

{shuffle:
    - That's right. Cover them with kisses.
    - Nuzzle your cheek against my foot.
    - Bury your nose in my foot and breathe deep.
}

/wait 
-> Nega_GoodJob->

/wait
- (feet)
You like that, don't you {isBoy: piggy|bitch}? 

+[yes]
    ~ addObey()
    /emotion amused 65
    Yah, you do!

+[no]
     ~ addDisobey()
    -> Nega_Unhappy  ->
    /perform talk_annoyed angry 80
    Too bad. 
    You rolled a tease.

+ [timeout] -> Nega_Timeout -> feet

+ [distracted] -> Nega_PayAttention  -> feet

- 
{shuffle:
    - Your place is at my feet.
    - You're only as good as the ground I walk on.
    - Such a cute little foot slut.
}

-> Nega_Kneel ->

That's enough.
Let me think of something else for you to do...


/perform talk_thoughtful

->->



