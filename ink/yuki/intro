=== yuki_intro ===
-> select_gender ->
~ obey = 0
~ disobey = 0
~ punishments = 1
~ rewards = 1

/scene entry.intro2
/music yuki
/ambient rain
/character yuki idle_yuki
// /equip feather_duster

Oh! There you are! #01

/perform bounces

I’ve been waiting for you. #02

/perform clasp_hands

I'm Mistress Yuki-Onna. #03
/perform idle_yuki
It’s my job to make sure all new arrivals   #04
are taught how to behave  #05
while you’re a guest in Goddess’s home. #06

/perform serious

First things first. #07

- (stand_straight)
- (stand_straight_tries)

/perform idle_yuki
Always stand up straight with your head high  #08
when you present yourself to a Mistress. #09

+ [pose:raised_chin] ->
+ { stand_straight_tries < 2 } [timeout] -> stand_straight
+ { stand_straight_tries >= 2 } [timeout] -> Yuki_Interested -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

- /perform excited

You're such a good {player_name}! #10

- (new_guest)

/perform thoughtful

Do you know why you're here? #11

 + [yes]
    ~ obey++
    /perform confused

    I don't see how you'd know that already. #12
    Someone must have told you. #13
   
    /perform sighs
    
    Well, let's go through the basics anyways,  #14
    to make sure you have the right information.  #15
    
    /wait
    
+ [no]
    ~ disobey++
    /perform serious
    Well, duh! #16
    You're not supposed to know. #17
    /perform excited
    That's the fun part! #18
    
+ [timeout]
    /perform cocks_head_to_left
    Hello-o #19
    Anybody home? #20
    -> new_guest
    
+ [distracted] -> Yuki_PayAttention -> new_guest

- (house_rules)
/perform idle_yuki
You are a guest of Threshold House. #21
/perform thoughtful
Well, we call it a house, but it's not really one... #22

/perform serious

All guests must follow the rules #23
or they will not be allowed to stay. #24

- (learn_rules)
/perform cocks_head_to_left
Would you like to know what the rules are? #25

+ [yes]
    ~ obey++
    /perform excited
    Oh goody! I love telling people the rules! #26

+ [no]
    ~ disobey++
    /perform pouty
    Don't be a pouty baby. #27
    /perform angry
    You have to learn the rules or you can't be a good {player_name}! #28

+ [timeout]
    /perform sighs
    You aren't listening very well. #29
    -> learn_rules
    
+ [distracted] -> Yuki_PayAttention -> learn_rules
    
- (two_rules)

/perform nod
The rules are simple! #30

/perform two_fingers
And there are only two of them! #31

/perform one_finger

Answer questions when you're asked. #32

/perform two_fingers

Follow directions when they're given. #33

/perform open_arms_shrug

Disobey and be punished. #34

/perform clasp_hands
Obey and be rewarded. #35

- (easy_rules)
/perform idle_yuki
Easy, right? #36

+ [yes]
    /perform bounces
    ~ obey++
    I know! Our Goddess is so kind! #37

+ [no]
    /perform cocks_head_to_left
    ~ disobey++
    Oh! Don't worry. You'll learn fast! #38

+ [timeout]
    /perform confused
    Are you still listening? #39
    -> easy_rules
    
+ [distracted] -> Yuki_PayAttention -> easy_rules

//insert puppy pose test

- (puppy_beg)
/perform behold
Let's practice! #45

/perform idle_yuki
I really love puppies. #169
Petting them. Kissing them. Feeding them. Walking them. #170

Would you like to be my puppy? #171

+ [yes]
    /perform excited
    Oh my goodness! I'm just so excited! #172
    -> pet_beg

+ [no]
    /perform pouty
    What? But I'm so nice to my pets. #173
    You're not doing a good job of following instructions. #174

- Just try it once for me, okay? #175

+ [yes]
    /perform clasp_hands
    I promise this will be worth it! #176

+ [no]
    /perform cocks_head_to_left
    Well, that's really a shame! #177
    Maybe I'm just too nice for you. #178
    I hope that's not true! #179
    -> good_yuki

//make it to where the yuki ending gives them another chance to be a puppy or sets a variable on them that they don't like puppy play

- (pet_beg)
/perform serious
First, puppy has to beg for my attention. #180

- (pet_beg_tries)
/perform point_down
Down on your knees and raise your hands to beg. #181

+ [pose:puppy_beg]
+ { pet_beg_tries < 2 } [timeout] -> pet_beg
+ { pet_beg_tries >= 2 } [timeout] ->

- -> Yuki_GoodJob ->
/perform bounces
Oh, that's so perfect!  #182
You did so well following instructions. #183

/perform idle_yuki
Stand back up now, {player_name}. #184

- (stand_straight2)
- (stand_straight2_tries)

+ [pose:raised_chin]
+ { stand_straight2_tries < 2 } [timeout] -> stand_straight2
+ { stand_straight2_tries >= 2 } [timeout] -> Yuki_Interested -> stand_straight2
// + [no] -> CheckIfInterested -> stand_straight

/perform excited
I can't wait until I can play with my new puppy! #185

- /perform clasp_hands
You are a cute one! I really hope you do well here! #40
Giving rewards is so much more fun than punishing. #41

- (good_yuki)
/perform serious
Do you like being a good {player_name}? #42

+ [yes]
    ~ obey++
    /perform bounces
    Yay! We'll have so much fun together! #43

+ [no]
    ~ disobey++
    /perform pouty
    Oh darn. I really hope you'll change your mind. #44

+ [timeout]
    /perform lean_forward
    Are you still there? #186
    -> good_yuki

+ [distracted] -> Yuki_PayAttention -> good_yuki

- (nega_interruption)
/scene entry.intro3

/character yuki
/lookat nega

/character nega head_toss
/lookat yuki

Oh please! You're always such a good girl, Yuki, #46
/perform amused_nega
never having any fun. #47

/perform confused
Don't you want to live a little? #48

/perform behold
Bend the rules once in a while and really let loose? #49
/perform idle_nega

/character yuki foot_stomp
No, thank you! #50

//character nega approach_yuki
/character nega lean_forward
//use whisper voice
I know you do.  #51
I know your deepest, darkest secrets. #52

/perform sassy
Remember? #53

+ [continue]
 
- /character yuki unhappy
You just stop that right now, Nega-Yuki! #54

/perform clasp_hands
I want my own room someday.  #55

/perform angry
There's no way I'm breaking the rules! #56

/perform excited
I really want the Goddess to be pleased with me. #57
/perform idle_yuki

- (test_lookat)

/character nega head_toss
Ugh.. You're such a goody two-shoes. #58

/lookat none
/perform bored_nega
You'd have so much more fun if you bent the rules now and then. #59

- (new_one)

/character Yuki
/lookat player

/character nega
/lookat player

/perform curious
So. You're the new one, huh? #60

-(nega_ask_again)

+ [yes]
    ~ disobey++
    /perform inspect
    Not too bad. You have potential. #61
    
    /perform amused
    Though, I'm not too picky. #62

+ [no]
    ~ obey++
    /perform laugh
    Not new, huh? #63
    
    /perform curious
    This one is more confused than most. #64

+ [timeout]
    /perform confused_nega
    Are you stupid, or something? #65
    
    /perform lean_forward
    I asked you if you're new here. #66
    -> nega_ask_again
    
- /perform pleased
I bet Yuki's been having so much fun #67
explaining the rules to you  #68

/perform sassy
and telling you how to be a good {player_name}, #69
hasn't she? #70

+ [yes]
    ~ obey++
    /lookat none
    /perform bored_nega
    Boring. #71
    
+ [no]
    ~ disobey++
    /perform pleased
    A little liar.  #72
    We're going to get along just fine. #73

+ [timeout]

- /perform head_toss
/lookat player
Don't let her fool you.  #74

/perform amused_nega
She's not as sweet and innocent as she lets on. #75

//insert teaching player to get slapped
/perform idle_nega
I bet you like it rough. #187
Let's find out how rough, shall we? #188

// + [yes]

// + [no]
//     /perform head_toss
//     What a pathetic whimp. #189

- (nega_slap)
/character nega unhappy
I'm going to show you both how slaves ought to be trained. #190
Pain is such an effective tool. #191

Time to see how you can take a slap. #192
When I swing my hand, you better fucking jerk your head. #193
Ready? #194

+ [yes]
    ~ obey++
    That's right, bitch. #195
    You'll take this like a good little cunt. #196
    -> try_slap

+ [no]
    ~ disobey++
    /perform annoyed
    No? #197
    Then you're not going to like me very much. #198

//change nega ending so if the player says no to this, she reminds them that visiting her means the hard stuff and they have to take a slap
- /perform idle_nega
Obedient pain sluts get special rewards around here. #199

Do you want to change your mind and take a slap? #200

+ [yes]
    ~ obey++
    /perform licks_lips
    Ahhhh. Sweet music to my ears! #201

+ [no]
    ~ disobey++
    /perform head_toss
    Pity. You have no idea what you're missing. #202
    -> going_on

//set variable on player that they don't want pain and didn't get slapped

//need to add slap training

- (try_slap)
/perform slap

+ [react-success]
     Not too bad. You have potential. #61
    // -> Nega_GoodJob ->
+ [react-fail]
    That was pathetic. Try again. #203
    -> try_slap

- (going_on)
/perform confused
Have you figured out what's going on here yet? #76

+ [yes]
    ~ disobey++
    /perform curious
    Hmmm. You might just be smarter than you look, {player_name}. #77
    
    /lookat none
    /perform bored
    We'll find out soon enough. #78
   
    /perform sassy
    /lookat player
    Although, honestly, the dumb ones make better fuck toys. #79

+ [no]
    ~ obey++
    /perform rolls_eyes
    Typical. <i>She</i> always wants them to learn the hard way. #80
    
    /perform fake_annoyed
    /lookat yuki
    I think I should tell {player_him}. #81
    /perform idle_nega
    
    /character yuki frustrated
    /lookat nega
    Don't you dare! #82
    The Goddess will be so angry with you! #83
    /perform idle_yuki
    
    /character nega
    /lookat none
    /character nega bored_nega
    Oh fine, party pooper! #84
    
    
    /perform pleased
    /lookat player
    I guess I'll just make {player_him} my fuck toy instead. #85
    
+ [timeout]
    /perform confused
    Are you stupid, or something? #86
    
    /perform unhappy
    I asked you if you're new here? #87
    -> going_on
    

- (caress_question)
/character nega caress_breasts
/lookat player

I bet that {isBoy:cock|cunt} of yours works just fine. #88

/perform sassy
Let's find out. Shall we? #89

- (genitals)

+ [yes]
    ~ disobey++
    /perform pleased
    Ooo. I'm going to like this one. {isBoy: He|She} likes to be naughty. #90

+ (caress_skipped) [no]
    ~ obey++
    /perform angry
    Oh great. Another spoil-sport, just like Yuki. #91
    -> skip_caress

+ [timeout]
    /perform fake_annoyed
    Should we find out how well your {isBoy:dick|pussy} works? #92
    -> genitals
    
+ [distracted] -> Nega_PayAttention -> genitals

- /character yuki flustered
Behave, Nega!  #93
/perform idle_yuki

/character nega caress_genitals

That's it... #94
Let me feel your {isBoy:cock swell|pussy drip}. #95

Mmmm. Just as I thought. #96
You want to be a little fuck toy, don't you? #97

- (caress_genitals)
+ [yes]
    ~ disobey++
    What a naughty {player_name}. #98
    I feel how {isBoy:hard|wet} you are. #99
    Do you want to be my fuck toy? #100
    
    ++ [yes]
        ~ disobey++
        /perform head_toss
        Too bad. I only use fuck toys that have something to give me. #101
        
        /perform taps_wrist
        And you can't give me what I want yet. #102
        
    ++ [no]
        ~ obey++
        /perform sassy
        No? You don't like me? #103
        Awww. But I'm way more fun that that uptight bitch. #104
        /perform head_toss
    
    ++ [timeout]
        /perform unhappy
        I must be boring you. #105
    
+ [no] 
    ~ obey++
    /perform head_toss
    What a prude. You'll change your mind soon enough. #106
    They always do. #107

+ [timeout]
    /perform curious
    Well, do you want to be used and abused? #108
    -> caress_genitals

- (skip_caress)
/perform inspect
The Mistresses will have so much fun #109
tearing up that ass of yours. #110

/perform lean_forward
//nega whispers
<i>Tick tock little mouse.</i> #111
<i>You might want to run.</i> #112
/perform amused_nega
<i>You're trapped in this house.</i> #113
<i>And we own your cum.</i> #114
/perform laugh
/perform idle_nega

+ [wait]

- /lookat yuki
/character yuki foot_stomp
/lookat nega
That's enough, Nega! #115
/perform unhappy
You are interfering too much! #140
/perform one_finger
I'll be reporting you to the Headmistress straight away. #117
/perform idle_yuki

{obey - disobey > 0:
    - -> Yuki_finish
    - else: -> Nega_finish
}


= Yuki_finish

/character nega chuckle
/lookat player
I'll see you again soon, little mouse. #118

/perform head_toss
Come find me when you've earned a reward. #119

/scene entry.intro4yuki


- /character yuki angry
Oh gee. She's so awful! #120
Don't you pay any attention to her. #121

/perform flustered
She's going to get in big trouble one of these days! #122

- {caress_skipped: -> rules}

/perform pouty
Your poor {isBoy:penis|pussy} must be aching after that. #123

+ [wait:2]

- (rules)
/perform curious
Now... where was I? #124
/perform one_finger
Right! The rules! #125

/perform serious
Now, look at your wrists. #204
See that black mark? #205
You already have a punishment for sneaking into the Goddess's bed chambers. #126
/perform unhappy
Naughty {player_name}. #127
The Headmistress will take care of that! #128

/perform nod
Make sure to come back to me when you're finished. #129

/perform excited
See that red stone on your other wrist? #206
It looks like the Goddess also gave you a reward. #130
You're so lucky to catch Her interest! #131

/perform clasp_hands
But I can't reward you if you don't take your punishment first. #132

/perform bounces    
Okay! That's all I have for now. #133

/perform serious
Hurry up!  #134
The Headmistress doesn't like to be kept waiting. #135
  
/perform flustered
And stay away from that awful Nega-Yuki! #136
She's a bad seed! #137

/perform idle_yuki
You'll get a punishment if you get caught coming out of her room. #138

~ headmistress_hub_status = unlocked
-> entry_intro2


= Nega_finish

/scene entry.intro4nega

/character nega laugh

+ [wait]

- /perform head_toss
Run off, little Yuki,  #142
/perform amused_nega
and tell the Headmistress all about  #143
how naughty I've been. #144

/perform angry
She thought she could shove down #145
all her dirty thoughts  #146
by casting me aside... #147

/perform behold
But here I am! #148

- {caress_skipped: -> skip_caress2}

/perform caress_breasts
Now we can really have some fun! #149

/perform pleased
Shall I rub your poor aching {isBoy: cock|cunt} again #150
while I do? #151

+ [yes]
    /perform caress_genitals
    Mmmm. That's it.  #152
    We're going to find out just how naughty  #153
    you like to be. #154

+ [no]
    /perform frown
    Don't tell me I've made a mistake in  #155
    paying attention to you. #156

- (skip_caress2)
/perform dance_tease
Be a good {player_name}, do what you're told, and  #157
/perform sassy
that little {isBoy: cock|cunt} of yours is going to #158
have so much fun when you come see me. #159
    
/perform lean_forward
But, if you decide to be a little {isBoy: prick|bitch}  #160
and not visit me... #161
/perform open_arms_shrug
Well, I won't take kindly to being ignored. #162

/perform laugh

+[wait]

- /perform inspect
The best part is you can't collect any rewards  #163
until you've paid off all your punishments. #164

- 
/lookat none 
/perform bored

I'm bored with you.  #165

/perform head_toss
/lookat player
Time to get a taste of your first punishment. #166

/perform confused
Better get your ass to the Headmistress. #167

/perform idle_nega
She doesn't like to be kept waiting. #168

~ headmistress_hub_status = unlocked
//need to know how player gets to next room
-> entry_intro2


