=== nega_reward_dice ===
-> select_gender_check ->

~ setScene(reward_scene, Nega)

// /scene attic.Ready
// /music nega
// /ambient howling-wind

- (give_reward)



{cycle:

    - /perform do_hand_out angry 60
    Hand it over. #01

    - /perform do_hand_out excited 25
    Gimme! #07

    - /perform do_hand_out happy 40
    Oooo. Give it here.  #08

    - /perform idle_unhappy_stomp angry 55
    Now, give it to me. #12
      /perform do_hand_out angry 55

    - /perform talk_lean_forward amused 65
    Time to pay up! #13
      /perform do_hand_out amused 65
}

+ [pose:present_wrist_reward]
+ [timeout] -> Nega_BadJob -> give_reward
+ [no] -> Nega_CheckIfInterested -> give_reward
+ [distracted] -> Nega_PayAttention -> give_reward

- /perform do_swipe_hand_fast excited 25
~ removeReward()


{cycle:

    - /perform do_fist_pump happy 65
    Fuck yeah! #02

    - /perform react_excited aroused 45
    Hot damn! #14

    - /perform tease_stroke_motion amused 55
    Mrow! #15

    - -> Nega_GoodJob ->
}

{shuffle:

    - /perform do_rub_hands_together excited 65
    You're my bitch now. #03

    //- /perform idle_sassy happy 45
    //Thanks for the treat, fuckup!
    //Needs proper tagging

    //- /perform react_point_at_player_angry aroused 65
    //Your ass is mine!
    //Needs proper tagging

    //- /perform talk_lean_forward amused 55
    //You're going to get what you deserve.
    //Needs proper tagging

    //- /perform do_shake_finger_stern happy 35
    //You're in for it now!
    //Needs proper tagging

    //- /perform do_rub_hands_together excited 45
    //This'll be fun! For me at least!
    //Needs proper tagging
}

/perform talk_one_finger amused 65
Let's see what you're going to get #04

/perform idle_tease aroused 45
for this pretty little gift. #05

/perform squat excited 35
Get down here. #09

+ [pose:kneel]


- (test_dice)
/perform squat_dice_give amused 35
Time to play a little game. #10
/wait 1

- (hold)
/perform squat_dice_shake amused 45
Take the dice and give them a good shake. #11


+ [hold:dice!]
+ [timeout:5] -> Nega_BadJob.Squatting -> hold

- (roll)
/perform squat excited 25
Go ahead. Roll them bones. #06

+ [roll!] -> rolled
+ [timeout] -> Nega_BadJob.Squatting -> roll

// 'Roll' event will force the script into this thread.
= rolled

~ temp result1 = getNegaDie1()
~ temp result2 = getNegaDie2()
~ temp result3 = getNegaDie3()
~ temp result = result1 + result2 + result3

/notify title="Dice Roll Results" delay=10 text="<> #note04
~ rollToText(result1)
<> + <>
~ rollToText(result2)
<> + <>
~ rollToText(result3)
<>"

{
    - inTutorial && result > 0: -> intro_reward
    - inTutorial: -> intro_change_dice

    - result == 3: -> result_reward_hard
    - result == 2: -> result_reward_light
    - result == 1: -> result_reward_tease

    - result1 == 0 && result2 == 0 && result3 == 0: -> result_all_blank
    - result == 0: -> result_no_reward

    - result == -1: -> result_punishment_tease
    - result == -2: -> result_punishment_light
    - result == -3: -> result_punishment_hard
}

- (result_reward_hard)
/lookat dice
/perform squat_forward
Oooo, three cups! You are a lucky little shit. #18
That does <i>not</i> happen often. #19

/lookat player
/perform squat
I suppose I have to give you something very special, then, don't I? #20

-> check_consent ->

{shuffle:
    - -> nega_reward_forcedsex
}


- (result_reward_light)

// This can only be two cups and a blank.

/lookat dice
/perform squat_forward
Two cups and a blank, hmm? Not bad. #21

-> check_consent ->

{shuffle:
    - -> nega_reward_light
}


- (result_reward_tease)

// This can either be 2 cups and a skull or 1 cup and two blanks.

{ result1 == -1 || result2 == -1 || result3 == -1:
    /lookat dice
    /perform squat_forward
    Two cups and a skull, hmm? #22
    /perform squat
    Skulls cancel cups, I'm afraid, so that's one cup for you. #23
- else:
    /lookat dice
    /perform squat_forward
    Hmm... one cup. #24
}

/lookat player
/perform
Not bad. But not great. #25
I suppose you deserve a little something for that. #26

-> check_consent ->

{shuffle:
    - -> nega_reward_tease
}


- (result_all_blank)

/lookat dice
/perform squat_forward
Wow, that is by far the best bad luck I've ever seen. #27
/lookat player
You get nothing, of course, but for a roll like that... #28

/perform squat
{ negaLoreEmpty():
    Perhaps I'll make an exception. #29
    -> check_consent ->
    -> result_punishment_tease_no_intro
- else:
    Perhaps I'll let you in on a little secret. #30
    -> nega_lore
}

- (result_no_reward)

TODO: Write 'no reward' dialog here.

/lookat dice
/perform squat_forward
Well look at that. Skull and cup cancel each other out. #31

/lookat player
Nothing for you today. #16
-> Nega_Insult ->

{shuffle:
    - And you thought you were going to get lucky! #17
    - Better luck next time, bitch. #32
    - Come back when you're a little richer. #63
}

/perform idle_looming

-> exit


- (result_punishment_tease)

{ result1 == 1 || result2 == 1 || result3 == 1:
    /lookat dice
    /perform squat_forward
    Two skulls and a cup, hmm? #33
    Cups cancel skulls, alas, so only one little skull for you. #34
- else:
    /lookat dice
    /perform squat_forward
    Hmm... one skull. #35
}

/lookat player
/perform squat
I suppose I can't go too hard on you, #36
/perform squat_forward
but I bet I can still make you squirm. #37

-> check_consent ->

- (result_punishment_tease_no_intro)
{shuffle:
   - -> nega_punishment_tease
}


- (result_punishment_light)

/lookat dice
/perform squat_forward
Ooo, two skulls. #38
That means your ass is mine. #39
-> check_consent ->

{shuffle:
     - -> nega_punishment_boot_lick
     - -> nega_punishment_camera_shoot
}


- (result_punishment_hard)

/lookat dice
/perform squat_forward
Mmmm, look at that. #40
Three skulls. I'm going to enjoy <i>this</i>. #41

-> check_consent ->

{shuffle:
    - -> nega_punishment_genital_torture
}

= intro_change_dice

/perform squat_forward amused 65
Aaaw, look at that. No fun. #42

/lookat player
/perform squat aroused 70
Since it's your first time,  #43
perhaps I'll make it rain for you. #44
Just this once. #45

/action force_dice
TODO: Remove this and actually force the dice in-game.
-> nega_reward_forcedsex


= intro_reward

/lookat dice
/perform squat_forward
Ooo. Looky here. #46
Someone just got lucky! #47

/lookat player
/perform squat
Oh goody! This is gonna be so much fun! #48
You're gonna love getting rewards from me. #49

-> nega_reward_forcedsex


= check_consent
//these may need adjusting - you wouldn't say I'm going to use your hard if you only get a light tease
{shuffle:
    - It's not too late to back out, {isBoy: worm|slit}. #50
    - I'm going to use you hard. #51
    - You can still change your mind. #52
    - I wonder if you can handle me.  #53
}

{shuffle:
    - Are you ready? #54
    - Shall I use you? #55
    - Ready for what's next? #56
    - Are you prepared to continue? #57
}

+ [yes]
    Delish! #58
    ->->

+ [no]
    -> exit

= come_again

- {shuffle:

    -/perform idle_skeptical amused 45
    Come visit me again, cunt. #59

    -/perform talk_lean_forward skeptical 50
    Come back when you have another reward to spend. #60

    -/perform do_inspect aroused 55
    I'll let you come see me again. #61

    -/perform idle_sassy happy 25
    You know where to find me. #62
}

-> exit
