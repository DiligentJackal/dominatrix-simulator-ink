=== game_intro ===

~ tutorial_step = at_game_intro
~ setScene(intro_scene, None)

/scene EntryDark.TitleScreen
/music entry

+ [wait:6] -> verify_age


= verify_age
/scene EntryDark.VerifyAge

+ [yes] -> instructions
+ [no] -> game_outro


= instructions

/scene EntryDark.Instructions
/character DevilGirl
/music entry

/perform do_walk_up amused 50
/wait 4
/perform idle amused 50

Hello. #01

- (again)

Would you like to play a little game? #02

+ [yes] -> lets_play
+ [continue]

- (nod_info)

/perform react_yes happy 50
Nod your head for "yes." #03

+ [yes] -> lets_play
+ [continue]

- /perform react_no
Shake your head for "no." #04

+ [yes] -> lets_play
+ [continue]

- /perform idle

+ [yes] -> lets_play
+ [no]
    TODO: add in dialogue - I'm sure you'd enjoy my little game. Let's try this again.
    -> again
+ [timeout] -> nod_info


= lets_play

/perform talk_behold happy 50
Of course you would. #05
I think you're going to like this little game I've created. #06

/perform talk_gesture amused 50
Just do what you're told and you'll be fine. #07

/perform idle_skeptical happy 50
If you get stuck, I've given you a journal to guide you. #08
/perform talk_question skeptical 50
Give it a try. #09

/perform idle_skeptical happy 50
{inVR: 
    Use the 'X' or 'menu' buttons on your controller. #10
- else: 
    Just hit 'E' on your keyboard. #11
}

+ [use-journal]

- /action close_journal
/perform do_arm_raise happy
Perfect! #12

/perform talk_thoughtful amused 50
Now... #13
If you want the most authentic experience, #14
/perform talk_lean_forward amused
strip naked and begin with no clothes on. #15

/perform idle_tease aroused
Wouldn't you just love to do that for me? #16
I promise you won't regret it. #17
And, I promise to watch while you do. #18

/perform idle_skeptical amused
Don't worry. I'll wait for you. #19

+ [wait 2]

- (check_ready)
/emotion amused 50
Are you ready? #20

+ [yes] -> ready
+[no]
    I'll wait. #23
    <b>Nod</b> your head when you're ready #24
    or come back later. #25
    -> check_ready
+ [timeout] 
    Don't forget to <b>nod</b> your head for "yes". #26
    -> check_ready        


= ready

/perform react_thankful happy 50
Wonderful. #21
/perform do_stand_to_right
Have fun! #22
/perform idle_stand_to_right
/wait 1

+ [move]
    -> select_gender -> post_intro_deviation_check


=== game_intro_web ===

~ player_name = "none"

<blockquote><i>Two nude statues kneel before you with outstretched hands, one male and one female. Which would you choose?</i></blockquote>

- (options)


+ {player_name != "boy"}[Touch the male statue] You touch the male statue.
    <blockquote><i>The male statue disappears and you now have hands with thick masculine fingers. The missing statue's hands.</i></blockquote>
    {
        - player_name == "none": <blockquote><i>The door at the end of the hall cracks open...</i></blockquote>
        - else: <blockquote><i>The female statue reappears.</i></blockquote>
    }
    ~ setGender("m")
    -> options
+ {player_name != "girl"}[Touch the female statue] Female
    <blockquote><i>The female statue disappears and you now have hands with slender feminine fingers. The missing statue's hands.</i></blockquote>
    {
        - player_name == "none": <blockquote><i>The door at the end of the hall cracks open...</i></blockquote>
        - else: <blockquote><i>The male statue reappears.</i></blockquote>
    }
    ~ setGender("f")
    -> options

+ {player_name != "none"}[Open the door.] You open the door.

- <blockquote><i>You have just stepped into a darkened room with a storm thundering outside. Lighting flashes and you see a devilish figure locked in a sexual exchange with two other figures, moaning filling your ears. The lightning flashes again, filling your eyes with a new, this time, tortuous exchange. Again, it flashes, and you find those same figures in a new embrace - each scene wildly different from one another. Without warning, the storm abates, the lightning ceases, and before you stands a Goddess.</i></blockquote>

<blockquote><i>She looks at you for a moment and then challenges you...</i></blockquote>

+ [Introduction] -> goddess_intro

