=== headmistress_hub ===

-> select_gender_check ->

{inTutorial && tutorial_step != at_hm_intro:
    ~ tutorial_step = at_hm_bonus
    ~ hm_bonus_shortcut = true
    ~ punishments = 0
    ~ rewards = 1
}

{deviation == exhibitionism:
    ~ punishments = 0
    ~ rewards = 1
}

~ setHub(Headmistress)

/scene Theatre.Hub
/music headmistress
/character headmistress idle_clasp_hands

+ [move]

-> apology_check(-> hello, -> exit)


= hello
/wait

{ headmistress_regard >= 10: 
    I am pleased to see you, bauble. #01
- else:
    {shuffle:
        - Greetings, {isBoy: peon|moppet}. #02
        - Welcome back, flawed one. #03
        - I see you cannot stay away, trinket. #04
        - You continue to return. #05
    }
}

{ 
    - punishments > 0: -> check_punishment
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

Are you here to pay your punishment? #06
+ [yes] -> choose_punishment
+ [no]
    No? #07
    -> check_task


= check_reward

Are you here to receive your reward? #08

+ [yes] -> choose_reward
+ [no]
    No?  #09
    Abstaining from pleasure, I see. #10
    How restrained. #11
    -> check_task


= check_task

{ inTutorial: -> goodbye }
{ deviation == exhibitionism: -> goodbye }

{ punishments == 0 && rewards == 0:
    No rewards or punishments, huh? #12
    You must be here to serve me! #13
- else:
    Shall I give you a task then? #14
}

+ [yes] -> choose_task
+ [no] -> goodbye


= choose_punishment

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_punishment_spanking -> exit
}
//-> headmistress_punishment_flogging -> exit


= choose_reward
/perform talk_behold

{shuffle:
    - Then a reward you shall have. #15
    - It is so pleasing when trinkets have earned a reward. #16
    - Rewards are preferable to punishments. #17
}
  
{inTutorial: -> headmistress_reward_exhibitionism }
{deviation == exhibitionism: -> headmistress_reward_exhibitionism }

// Eventually we'll have the game randomly pick here.
{shuffle:
    - -> headmistress_reward_cunnilingus
    - -> headmistress_reward_exhibitionism
}

= choose_task

{deviation == hand_signals: -> headmistress_task_handsignals -> exit }

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_task_handsignals ->
}

- -> exit


= goodbye

Then I have nothing for you. #18
Begone. #19

-> exit


