=== headmistress_punishment_spanking ===
- (intro)

{not intro: /scene office_raised_curtains}

/character headmistress
/equip wood_paddle

For your transgressions,
a spanking is in order.

{intro:
    First, you will learn
    how to present yourself for punishment.
}

/perform point_paddle_at_desk

{intro:
    All right.
- else:
    You know the drill.
}

Onto my desk.
Take position upon
your hands and knees.

/perform idle

- (get_on_desk)
+ [move:get_on_desk]
    There we are.
+ [timeout]
    Do not waste my time.
    Up you go.
    -> get_on_desk
    
- (hands_and_knees_timeout)
Now on your hands and knees, bauble.

- (hands_and_knees_fail)
+ [pose:hands_and_knees] -> HoldPose(-> hands_and_knees_fail, -> hands_and_knees_timeout) ->

- /perform amused

Splayed fingers must be at least shoulder width apart.

{get_on_desk == 1:
    /perform look_up_and_down
    Observe the eagerness with which our chattel attempts to comply.
    This behaviour is pleasing to our Goddess.
- else:
    /perform displeased
    Note the hesitation. A sure sign of poor breeding.
    Such offends the Goddess and must be striken from the specimen.
}

+[wait]

- /perform amused

Now then.
The back must be delicately arched, 
presenting the most ideal access 
to the object of discipline: the buttocks.
Lift your backside high, {isBoy: mongrel|moppet}.

/perform paddle_desk

+[wait]

-In this way, the ass is presented for abasement.
Tell me, lump, is that as high as you can lift your hindquarters?

+[yes]
    ~obey++
    I disagree.
    Arch just a bit more.
    ++[wait]
    --Good.
    You will remain in this position without fail.

+[no]
    ~obey--
    You serve yourself and our Goddess poorly, heathen.
    Raise your ass!

+[timeout]
    What are you waiting for?
    Your back must strain with the effort of raising your ass, chattel.


-/perform satisfied
-> GoodJob ->
The nether regions must be fully in view.
Knees must be sufficiently apart to allow for clear observation.
Now tell me: Are you {isBoy:hard|wet}?

+[yes]
    Good.

+[no]
    Hmm.. Disappointing.
    Regardless,
    
- The moisture on the {isBoy: tip of the shaft|crown of the folds} 
and puckered rosebud can be admired fully in this position.

/perform circle_desk

{intro: -> punish_instruct | -> punish_continue}


- (punish_instruct)

Disobedient behavior must be corrected
immediately upon infraction.

It must be firm, swift, and leave an indelible mark
upon the body and soul.
/perform tap_paddle

+[wait]

-To receive a punishment with the utmost grace and decorum,
the subject of discpline must hold their head high and
breath smoothly and calmly through the nose.

/perform tap_paddle

+[wait]

-The muscles of the ass must remain relaxed between swats
to provide an exceptionally pleasing display when they clench
in response to the impact of my admonishments.
/perform tap_paddle

+[wait]

-Feet must remain flat with no obscene curling of the toes.
/perform tap_paddle
/character crowd murmur

+[wait]

- But enough of this pretense.
We aren't here for just you.
We have our entire class to enlighten!

/character crowd jeers

+[wait]

- Let us show our audience
the fine display you have for us.

/character headmistress arm_raise
/action raise_curtains
/character crowd cheers


- (punish_continue)

/character headmistress
What say you, {isBoy: toad|strumpet}?

{cycle:
    - Will our esteemed guests be enthralled by
        {isBoy:your turgid bouncing cock?|the glistening of your cunt?}
    - Do you feel their eyes on you?
    - Are you throbbing in anticipation?
    - Do you want to touch yourself?
    - Does pain turn you on?
}

+ [yes]
    ~obey++
    A glutton for exhibition.
    Most pleasing.

+ [no]
    Ah, well.
    Your lackluster genitals shall be addressed another time.
    We still have our punishment to mete out.

- /perform walk_to_front_of_desk
And finally, we come to the heart of the matter.
Do you accept that you must be punished?

- (ask_punish_ready)

+[yes]
    ~ obey++
    /perform arm_raise
    Then we shall begin.
    /character crowd cheers

+[no]
    ~ obey--
    /chracter crowd boos
    /character headmistress suspicious
    Foolish, little slave.
    
    
    It will be over quickly and your compliance will
    please the Goddess.
    Shall we continue with your punishment?

    ++[yes]
        ~ obey++
        Excellent choice. Let us begin.

    ++[no]
        ~ obey--
        /character crowd angry
        Make haste from my sight!
        You have squandered my time
        and my attentions.
        I will not suffer fools.
        You've just earned yet another punishment.
        ~ punishments++
        ~ headmistress_apologize = true
        Now leave.
        /character headmistress point_to_door
        -> exit

+[timeout] 
    You have had enough time to think.
    Your punishment must begin.
    Do you agree?
    -> ask_punish_ready

- +[wait]

- {intro: -> spank_instructions}

Before we begin,
would you like to practice how you must perform first?
~ headmistress_difficulty = 1

+[yes]
    -> spank_instructions
+[no]
    An expert, are we? We will see about that.
    -> swat_continue

- (spank_instructions)
With each swat, you will show me clearly
just how much you feel it.
When you hear the slap of my paddle
and feel its sting upon your ass
I want to see that empty head of yours jerk up and back
into a proud and upright position.

+[wait]

- And I am always pleased
with that exquisite sharp inhale of breath.
Let us practice.

+[wait]

~temp spanks = 0
~temp spanks_failed = 0

- (try_spank)
/character headmistress paddle_ass

+ [reaction:spanked]
    // This lets us count the number of successful attempts.
    ~spanks++
    // Once there have been 3 good spanks, we continue.
    {spanks < 3:
        -> GoodJob ->
        -> try_spank
    - else:
        Just so.
        -> swat_continue
    }
+ [reaction:failed]
    // This lets us count the number of failed attempts.
    ~spanks_failed++
    
    // We will continue if they fail 3 times.
    { spanks_failed < 3:
        Not good enough. Try again.
        -> try_spank
    - else:
        That will do for now. We will work on that in the future.
    }
    
    ~ headmistress_difficulty = 0

- (swat_continue)

Now then. Let us begin!

/character crowd cheers

+ [wait]

- Welcome esteemed friends!
As always, purrs, moans, and words of encouragement
are permitted during the punishment lesson.

/character crowd jeers

+ [wait]

/character headmistress walk_to_back_of_desk

- All care must be taken to assure
no lasting harm is inflicted...
unless permanency is desired, of course.

/character crowd appreciation

+ [wait]

- Our subject today has earned a spanking.
I will use a paddle made of mahogany.
An excellent, hard wood ideal for leaving
bright red welts on the flesh of disobedient slaves.
Would you not agree?

/character crowd coos 

+ [wait]

- Listen to that excitement, {player_name}.
Your ass will provide a fine demonstration today.

Are they not fortunate to have one such as you to observe?

+[yes]
    ~obey++
    Perform well and your punishment marker will be removed.

+[no]
    ~obey--
    It is a shame you do not value the learning of others.

- Prepare yourself, {player_name}.

- (spank)
/character headmistress paddle_ass

+ [reaction:spanked]
    -- (spank_count)
    -> GoodSpank ->
    {spank_count < 10:-> spank}
+ [reaction:failed] -> BadSpank -> spank

- (finish)

And done.
You are free to go.
Consider one demerit paid in full.

~ punishments--

/character crowd claps

+ [wait]

// If the player has never seen nega-yuki,
// send them off to be tempted by her directly.
{not nega_intro: -> nega_intro | -> exit }

= BadSpank
/character headmistress displeased
Terrible. We'll have to repeat that one.
->->

= GoodSpank

{shuffle:
    - A fine welt is forming.
    - Music to my ears.
    - Perhaps I should be counting.
    - A red bottom is a pleasing bottom.
    - I do so love instructing slaves in proper behavior.
    - I never tire of this.
    - I suspect that one hurt.
    -
    -
    -
    -
}

{shuffle:
    - Notice how the {isBoy:shaft bounces|nether lips clench}.
    - The light is catching the dewy sheen on the skin most magnificently.
    - Observe the {isBoy: tip|slit} begin to glisten.
    -
    -
    -
}

->->

= GoodJob

// rewrite these for Headmistress
// /perform caress_thigh

{ shuffle:
    - There's a good {player_name}.
    - What a good little {player_name}.
    - That's my {player_name}.
    - Aren't you the sweetest little thing.
    - Very nice.
    - That will do just fine.
    - Quite satisfying.
    - Mmmm. I like that.
    - Someone is being a good {player_name}.
    - Well done.
    - That's very pleasing.
}

->->

= PayAttention

//rewrite these for Headmistress
/perform amused

{ cycle:
    - Look at me when I'm speaking to you, {player_name}. #125
    - I expect your eyes on me at all times, #126
    unless I tell you otherwise. #127
    - Did I give you permission to look elsewhere? #128
    - Didn't anyone ever teach you to look at people #129
    when they're speaking to you? #130
    - Pay attention to me when you're in my presence. #131
    - You're trying my patience. Pay attention. #132
    - Eyes on me, {player_name}. #133
}

+ [pay-attention]
+ [timeout] -> PayAttention

/perform thoughtful

- { cycle:
    - Where was I? Oh yes... #134
    - That's better. Let's continue. #135
    - Good {player_name}. #136
    - Let's try that again. #137
    - As I was saying... #138
    - Now... Let's start over. #139
}

->->

= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform unhappy

Are you too good to take a punishment, {player_name}? 
Obey instructions.
- (check_again)

/perform idle

+ [yes] -> check_yes
+ [no]
+ [timeout] -> check_again

- (check_terse) Well?

/perform angry

You merely drag out the invetitable with your delay.

/perform annoyed

Are you going to continue to play the reluctant, {player_name}?

+ [yes] -> check_yes
+ [no]

- /perform bored
Out of my office, heathen.
I have no time for your shenanigans.
-> out_heathen

- (check_yes) -> GoodJob ->
/perform confused
There is hope for you yet. #157

-> out_heathen

= out_heathen

/perform paddle_desk
-> exit
