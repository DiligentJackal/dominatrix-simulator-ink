=== yuki_task_puppyplay_practice ===

- (start)

VAR tries = 0
VAR Practice_Counts = 0
VAR Practice_Attempts = 0
VAR Practice_Goal = 0


// ====== Begin Debug ======

//~ passes = 4
//~ level = 4
//-> revisit_test

// ====== End Debug =====


/perform idle_yuki
{shuffle:
    - I think we should practice your poses some more, puppy. #158
    - Time to work on your puppy poses... #129
}

- (sit)
/perform point_down
<b>Sit</b>, puppy. #159

+ [pose:puppy_sit] -> Yuki_GoodJob ->
+ [timeout] -> Yuki_Timeout -> sit

// Only play this if this is our first time in practice.
- {yuki_task_puppyplay_practice == 1:
    /perform behold
    Now, if you're good, this won't take long. #150
    
    /perform clasp_hands
    And if you do well during practices, I'll give you a special treat! #151
    
    /perform lean_forward
    But if you do poorly, I'm going to punish you. #152
}

/perform one_finger
Be ready for anything you've already learned! #160

~ tries = 0

- (ready)
/perform cocks_head_to_left
{cycle:
    - Are you listening, puppy? #161
    - Do you understand? #143
}

+ [yes] -> use_paws
+ [no]
    -- (use_paws)
    ~ tries++
    /perform chuckle
    Oh my gosh silly, answer with your paws! #144
    -> ready

+ [pose:puppy_yes]
    { tries == 0:
        /perform excited
        -> Yuki_GoodJob ->
        TODO: Re-record this line as it is missing.
        // Maybe you don't need this practice after all! #148
    - else:
        /perform agreement
        -> Yuki_AcknowledgeYes ->
    }

+ [pose:puppy_no]
    -- (wrong_no)
    ~ tries++
    /perform impatient
    Silly puppy, I think you meant to raise your <i>other</i> paw for <b>"yes."</b> #145
    -> ready
+ [timeout] 
    ~ tries++
    -> Yuki_Timeout -> ready 

- /perform behold
Pay attention! #165

{cycle:
    - ~ Practice_Goal = 3
    - ~ Practice_Goal = 5
    - ~ Practice_Goal = 7
    - ~ Practice_Goal = 9
}

-(Practice_Loop)

-> test_next_pose ->

~ Practice_Counts++

{Practice_Counts < Practice_Goal: -> Practice_Loop}

{Practice_Attempts == Practice_Goal:
    ~ addReward()
    ~ yuki_puppy_difficulty = 2
}

{Practice_Attempts > Practice_Goal + 1:
    ~ yuki_puppy_difficulty = 1
}

{Practice_Attempts > Practice_Goal + 2:
    ~ yuki_puppy_difficulty = 1
    ~ addPunishment()
}

{Practice_Attempts > Practice_Goal + 2:
    -> Bad_Ending_Director ->
- else:
    -> Good_Ending_Director ->
}

->->

= test_next_pose
{shuffle:
    - -> test_pose("puppy_beg", -> prompt_beg_pose) ->->
    - -> test_pose("puppy_confused", -> prompt_confused_pose) ->->
    - -> test_pose("puppy_hungry", -> prompt_hungry_pose) ->->
    - -> test_pose("puppy_playful", -> prompt_playful_pose) ->->
    - -> test_pose("puppy_tired", -> prompt_tired_pose) ->->
    - -> test_pose("puppy_thirsty", -> prompt_thirsty_pose) ->->
    - -> test_pose("puppy_sorry", -> prompt_sorry_pose) ->->
    - -> test_pose("puppy_back", -> prompt_back_pose) ->->
    - -> test_pose("puppy_horny", -> prompt_horny_pose) ->->
}


/**
* The pose test stitch with pose name and prompt stitch parameters.
* 
* @param {string} pose The name of the pose to test. (e.g. 'puppy_beg')
* @param {divert} prompt The stitch that hase the pose's prompt VO/performances.
*/
= test_pose(pose, -> prompt)
~ tries = 0
- (retry)
-> prompt ->
~ tries++
~ Practice_Attempts++
+ [pose:{pose}] ->->
+ { tries < 2 } [timeout] -> retry



// Prompts for each pose type used by the 'test_pose' stitch above.

= prompt_beg_pose
/perform clasp_hands

{cycle:
    - Show me you're ready to play, {player_name}. <b>"Beg"</b>! #01
    - Okay! <b>"Beg"</b> to have some fun. #02
    - Time to practice your puppy poses. Get into <b>"beg"</b>! #03
    - Yay! Time for more puppy play. Can you <b>"beg"</b> for me? #04
    - We can do puppy play... if you <b>"beg"</b> first. #05
}

/perform idle_yuki
->->


= prompt_confused_pose
/perform confused

{cycle:
    - Show me the <b>"confused"</b> pose. #09
    - Are you <b>"confused"</b>? Show me! #10
}

/perform idle_yuki
->->


= prompt_hungry_pose
/perform thoughtful

{cycle:
    - Show me <b>"hungry"</b>. #13
    - Is my cute little puppy, <b>"hungry"</b>? #14
}

/perform idle_yuki
->->


= prompt_playful_pose
/perform one_finger

{cycle:
    - Let me see you be <b>"playful"</b>. #18
    - Okay! Be my <b>"playful"</b> little puppy. #19
}

/perform idle_yuki
->->

= prompt_tired_pose
/perform inspect_kneeling

{cycle:
    - Perform <b>"tired"</b>, puppy. #21
    - Are you <b>"tired"</b>, puppy? Let me see. #22
}

/perform idle_yuki
->->


= prompt_thirsty_pose
/perform lean_forward

{cycle:
    - Can you let me know you're <b>"thisty"</b>, puppy? #25
    - Hrm. Let's see <b>"thirsty"</b>, {player_name}. #26
}

/perform idle_yuki
->->

= prompt_sorry_pose
/perform pouty

{cycle:
    - Whenever you've been a bad puppy, you need to show me you're <b>"sorry"</b>. #31
    - Perform the <b>"sorry"</b> pose. #32
}

/perform idle_yuki
->->


= prompt_back_pose
/perform behold

{ cycle:
    - Sometimes, I want my puppy on {isBoy:his|her} <b>"back"</b>. #36
    - Show me how a good puppy lays on their <b>"back"</b>. #37
}

/perform idle_yuki
->->


= prompt_horny_pose
/perform squirm

{ cycle:
    - I know my naughty little puppy will get <b>"horny"</b> sometimes. You need to be able to tell me! #41
    - Perform the <b>"horny"</b> pose. #42
}

/perform idle_yuki
->->


= Good_Ending_Director

// Fill in shuffles with extra endings

{~ -> Good_Practice_1 ->| -> Good_Practice_2 ->}

->->

= Bad_Ending_Director

{~ -> Bad_Practice_1 ->| -> Bad_Practice_2 ->}

->->

= Good_Practice_1

//Masturbates player
/perform point_down
Roll onto your back like a good little puppy. #166
/perform inspect_kneeling
Hands and legs in the air, just like a real puppy. #167
/perform open_arms_shrug
Now, spread your knees. #168

+[pose:puppy_back]

- /perform excited
Time for my good puppy to be milked dry! #169

/perform lean_forward
Better tell me when you're ready to cum, puppy. #170

/perform one_finger
Just raise your right paw! #171
/perform idle_yuki

-(MasturbationCycle)

+[pose:puppy_back_yes]
+[timeout] -> YukiPlayerToCum -> MasturbationCycle


- /wait 5

-> YukiPlayerToCum ->

+[pose:puppy_back_yes]
+[timeout] 

- /wait 5

-> YukiPlayerToCum ->

+[pose:puppy_back_yes]
+[timeout] 

- /wait 5

-> YukiPlayerToCum ->

+[pose:puppy_back_yes]
+[timeout] 


- /perform squirm
Goooood puppy. #172
Time to cum for me. #173

/perform idle_yuki
Let your Mistress milk your little {isBoy:prick|cunt}. #174
/perform excited
Such a good {player_name}. #175
Sit up again, cutie. #176

+ [pose:puppy_sit]

- (puppy_well)
/perform agreement
I treat my puppies well, don't I? #177

+ [pose:puppy_yes]
    /perform serious
    That's right. #178
    I want my puppies to come back and play after all! #179

+ [pose:puppy_no]
    /perform fake_annoyed
    Your other head seems to disagree with you. #180

+ [timeout] -> Yuki_Timeout -> puppy_well

- /perform sassy
That was fun, puppy. #181
I hope this helped you remember your poses! #182

/perform lean_forward
Come back and see me soon! #183

->->

= Good_Practice_2

//Foot licking

/perform excited
Mmm, I love watching my puppies perform well! #184

/perform lean_forward
Do you want to worship my feet, puppy? #185

+[pose:puppy_yes]
    /perform bounces    
    Yippy! #186

+[pose:puppy_no]
    /perform open_arms_shrug
    That's fine. #187
    We can do something else. #188
    -> Good_Practice_1

+[timeout] -> Yuki_Timeout ->

- /perform point_down 
Come here puppy! #189
/perform present_foot

- (lick_feet)
- (lick_feet_tries)

+ [pose:kiss_feet] -> Foot_Licking_Responses ->
+ { lick_feet_tries < 2 } [timeout] -> lick_feet
+ { lick_feet_tries >= 2 } [timeout]

- /wait 5
/perform clasp_hands
Now I'm even happier with you! #190
You're the best puppy a girl could ask for. #191

/perform idle_yuki
I really hope you come see me again soon. #192
Bye bye cutie! #193

->->

= Bad_Practice_1

//Chastisement
/perform shake_finger_stern
That was a very bad session! #194
I'm so disappointed. #195

- (sorry)

/perform unhappy    
Bow your head, puppy, and show me you're sorry. #196

- (sorry_tries)

+ [pose:puppy_sorry]
+ { sorry_tries < 2 } [timeout] -> sorry
//+ { sorry_tries >= 2 } [timeout]

- (sorry_ask_again)
/perform idle_upset 
Are you sorry for your performance {player_name}? #197

+ [yes]
    /perform shake_finger_stern
    If you feel bad about it, you should have paid better attention! #198

+ [no]
    /perform pouty  
    I think somebody <i>wants</i> to be a bad dog. #199

+ [timeout] -> Yuki_Timeout -> sorry_ask_again
+ [distracted] -> Yuki_PayAttention -> sorry_ask_again


- /perform disappointed

{cycle:
    - At this rate, you'll never be properly trained! #200
    - We'll never have any real fun until you start taking this more seriously! #201
    - I expect much more out of you next time {player_name}. #202
}

/perform point_to_leave
Now shoo! Shoo! #203

->->

= Bad_Practice_2

//The butt wiggle
/perform disappointed
What a bad puppy you are. #204
You need to make it up to me. #205

/perform point_down
Turn around and show me your puppy tail. #206
Face down on the ground. #207

- (present_ass)

- (present_ass_tries)

+ [pose:present_ass]
+ { present_ass_tries < 2 } [timeout] -> present_ass

- /perform cocks_head_to_left
Well, at least you can manage that, {player_name}. #208
Now, wiggle your little puppy tail for me! #209

//How do motion tracking - Probably watch for side to side head movements?

/perform clasp_hands
Mmmm! Much better! #210

/perform open_arms_shrug
It was kind of sad watching you struggle with your commands. #211

/perform idle_yuki
Try and do better next time, puppy! #212


-> exit

= Foot_Licking_Responses

{cycle:
    - Gooood puppy! Clean my whole foot. #213
    - Mmmm, lick me just like that. #214
    - Yes puppy, yes! Your tongue feels so good on my toes. #215
    - What a good puppy cleaning my toes with your tongue. #216
}

->->
