
=== headmistress_task_handsignals_basics ===

-> intro -> exit

= intro

- (intro_again)
/perform smooth_skirt

The time has come to accelerate your performance, heathen.
To earn your place as cherished possession, you must embody attentiveness!
Do you desire to be cherished, bauble?

+ [yes]
    -> Headmistress_AcknowledgeYes ->

+ [no]
    /perform disappointed
   -> Headmistress_Unhappy ->
   I will ask you again.
    
    Do you desire to be cherished?
    
    ++ [Yes]
    -> Headmistress_AcknowledgeYes ->
    Then let us continue.
    
    ++ [No]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        I have misjudged your readiness.
        Return when you are further molded.
        -> exit

+ [timeout] -> Headmistress_Timeout -> intro_again
+ [distracted] -> Headmistress_PayAttention -> intro_again

- /perform idle
To be cherished, one must be entirely fixated upon one's Mistress.

~ tries = 0
- (come)
~ tries++

/perform arm_raise
Come hither.

+ [move]
   
+ { tries < 2 } [timeout]
    -> come
+ { tries >= 2 } [timeout]
    -> Headmistress_Interested -> come
+ [distracted] -> Headmistress_PayAttention -> come

- The time has come to train you to respond to commands without words.
Attend to my hand, peasant.

Through the delicate position of my fingers,
I signal to you a position your body must obey.

-> TestKneel(0) ->

- /perform behold
Such delight to be found in this position.

- (delight)
Do you seek to delight your Mistress, {isBoy: speck|foible}?


+ [yes] -> Headmistress_AcknowledgeYes ->
     
+ [no]
    -> Headmistress_Unhappy ->
    Think carefully about your response.
    
    ~ addDisobey()
    Do you seek to delight with your obedience?
    
    ++ [yes] -> Headmistress_AcknowledgeYes ->
    
    ++ [no]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        I have misjudged your readiness.
        Return when you are further molded.
        -> exit
        
    ++ [timeout] -> Headmistress_Timeout ->
    
    ++ [distracted] -> Headmistress_PayAttention -> 

+ [timeout] -> Headmistress_Timeout -> delight
+ [distracted] -> Headmistress_PayAttention -> delight

- /perform idle
The bond between a Mistress and her pet must be strong.
Communication does not always require words, vassal.
Simplicity. Elegance. Silence.

- (serve)
Are you prepared to serve so attentively, flawed one?
 
+ [yes] -> Headmistress_AcknowledgeYes ->
    
+ [no]
    Perhaps this is more than your simple mind can comprehend.
    You will obey the commands I teach you to follow with my hands.
   
    Will you serve in this way, slave?
    
    ++ [yes] -> Headmistress_AcknowledgeYes ->
    
    ++ [no]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        I have misjudged your readiness.
        Return when you are further molded.
        -> exit
        
    ++ [timeout] -> Headmistress_Timeout ->
    
    ++ [distracted] -> Headmistress_PayAttention -> 

+ [timeout] -> Headmistress_Timeout -> serve
+ [distracted] -> Headmistress_PayAttention -> serve

- /perform smooth_skirt
You will find this next position equally familiar.
I know my audience found it quite entertaining.

-> TestRear(0) ->

- Ahhh, the delightful orbs of your ass are in plain view.
Perfectly placed for a swat of my hand.

- (swat)
/character headmistress paddle_ass

+ [react-success] 
    
    Such a delightful response!
        
+ [react-fail]
    
    Tut. Already you forget how to please.
       
- /perform idle
Turn around and stand upright.
Patience must be offered to those who are learning, but
expect no mercy should you fail Her once your training is complete.

- (strive)
Will you strive to please Her, chattel?

+ [yes] -> Headmistress_AcknowledgeYes ->
     
    
+ [no]
    -> Headmistress_CheckIfInterested ->
    
    
    ++ [yes] -> Headmistress_AcknowledgeYes -> 
    ++ [no]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        I have misjudged your readiness.
        Return when you are further molded.
        -> exit
    ++ [timeout] -> Headmistress_Timeout -> 
    ++ [distracted] -> Headmistress_PayAttention -> 

+ [timeout] -> Headmistress_Timeout -> strive
+ [distracted] -> Headmistress_PayAttention -> strive
  

- 
/perform behold 
I shall demonstrate the final signal for you to learn today.

-> TestHumble(0) ->

- Trinkets must, at all times, 
be prepared to receive whatever a Mistress desires. 
Punishment. Reward. Or otherwise.

- (position)
This position is perfect for many things.
Wouldn't you agree, poppet?

+ [yes] 
    Your acquisence serves you well, trinket.
     
     
+ [no]
    I shall enlighten you.
    

+ [timeout] -> Headmistress_Timeout -> position
+ [distracted] -> Headmistress_PayAttention -> position

- In this position, your ass and nether regions are on full display.
Ripe for punishment and eager for reward.
It also serves to inform your Mistress that you are sorry.

+ [wait 2]

- (caress)
Can you feel the cool air caress your {isBoy: sack|slit}?

+ [yes] 
    The gentle musk of your arousal wafts toward me.
     
     
+ [no]
    We must work on your sexual excitation {isBoy: philistine|jezebel}.
    That will wait for another day.
    

+ [timeout] -> Headmistress_Timeout -> caress
+ [distracted] -> Headmistress_PayAttention -> caress

- Rise up to your feet, {player_name}.
Let us see how well you have absorbed this lesson.

- (stand_straight)

+ [pose:raised_chin]

With merely a signal and a command, 
I expect you to perform the correct basic response.

- (test_ready)
Are you ready to demonstrate your ability to follow the signals of my hand?

+ [yes] -> Headmistress_AcknowledgeYes ->
    
     
+ [no]
    
    -> Headmistress_CheckIfInterested -> test_ready
    

+ [timeout] -> Headmistress_Timeout -> test_ready
+ [distracted] -> Headmistress_PayAttention -> test_ready


- (start_test)

~ resetObeyed()


We shall see.

-> TestRandom(1) ->

Can you recall this one, chattel?

-> TestRandom(1) ->

Now to see if you can recall the final signal.

-> TestRandom(1) ->

Whenever I feel you need further practice,
expect to be tested on basic hand signals in this way.


- (turn_around)
Face me, foible.

+ [pose:raised_chin]

- {obeyed(): -> GoodEndingIntro | -> BadEndingIntro}


= TestRandom(difficulty)

{ shuffle:
    - -> TestKneel(difficulty) ->
    - -> TestRear(difficulty) ->
    - -> TestHumble(difficulty) ->
}

->->


 = GoodEndingIntro
/perform arm_raise
If you continue to perform so gracefully,
you will excel in pleasing Her.
You have earned a reward for your performance today.
~ addReward()
Stay attentive to your lessons!
~ headmistress_hub_status = unlocked

- -> exit

= BadEndingIntro
/perform behold
Your performance today lacked enthusiasm and grace.
To please Her, you must be perfect.
Perhaps a punishment marker will provide future incentive.
~ addPunishment()
~ headmistress_hub_status = unlocked

Now, leave my sight.

- -> exit


= practice 

{ headmistress_signal_basics_difficulty <= 1:
        -> practice_easy ->
    - else:
        -> practice_hard ->
}

- ->->

= practice_easy

I think you need more practice with your hand basic signals.
That will be your task for today.

-(begin)
Shall we begin?

+ [yes] -> Headmistress_AcknowledgeYes ->
    
+ [no]
    -> Headmistress_CheckIfInterested ->
    
    ++ [yes] -> Headmistress_AcknowledgeYes -> 
    ++ [no]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        You waist my time.
        -> exit
    ++ [timeout] -> Headmistress_Timeout -> 
    ++ [distracted] -> Headmistress_PayAttention -> 

+ [timeout] -> Headmistress_Timeout -> begin
+ [distracted] -> Headmistress_PayAttention -> begin
  

// todo: endurance signals practice
- Attend to my hand, {isBoy: buffoon|dove}.

-> TestRandom(1) ->

{ disobey == 0:
    Such perfection will make you a star performer. 
    Keep it up!
- else:
    Not bad, but not perfect. 
    Keep improving and I'll have to show you off to the class one day.
}

- Turn to me.
+ [pose:raised_chin]

- Can you recall this one, chattel?

-> TestRandom(1) ->


{ disobey == 0:
    Your obedience is impressive, bauble.
- else:
    Not bad, but not perfect. 
    Keep improving and I'll have to show you off to the class one day.
}


- Now to see if you can recall the final signal.

Position yourself to attend to my hand.

+[pose:raised_chin]

- -> TestRandom(1) ->


{ disobey == 0:
    ~ headmistress_signal_basics_difficulty++
    Flawless. You serve Her well today.
    So well that I will make it this task harder next time.
- else:
    Not perfect. I think we'll have to practice again.
    But if you are flawless next time,
    I just might have to show you off to the class.
}

- (turn_around)
Stand at attention before me, miscreant.

+ [pose:raised_chin]

- {obeyed(): -> GoodEndingEasy | -> BadEndingEasy}

- ->-> 

 = GoodEndingEasy
/perform arm_raise
The beginning of excellence, bauble.
You please with your service.
You shall be rewarded!
~ addReward()
Your passion for obedience shall be stoked in future lessons.
Depart.
~ headmistress_hub_status = unlocked

- -> exit

= BadEndingEasy
/perform behold
The shape of your service is still unmolded.
I expect more diligence in the future.
Another punishment has been earned.
~ addPunishment()
~ headmistress_hub_status = unlocked
Return again to practice further.
For now, you are excused.

- -> exit

= practice_hard

Your response to basic hand signals has been most impressive.
So impressive, 
that you will proudly demonstrate them to an audience.

/perform arm_raise
/ambient crowd
~ Headmistress_RaiseCurtains()
They do so love an excellent performance.
Fail not to perform, flawed one.

- (eyes)
Can you feel all eyes upon you?

+ [yes] -> Headmistress_AcknowledgeYes ->
    
+ [no]
    Ahh, but indeed they are.

+ [timeout] -> Headmistress_Timeout -> eyes
+ [distracted] -> Headmistress_PayAttention -> eyes

- (excite)
Does it excite you to be the focus of so much attention?

+ [yes] -> Headmistress_AcknowledgeYes ->
    Your soul brims with passion.
    So much delicious sustenance to provide.
    
+ [no]
    A pity to be so shy.
    Do not let your nerves get the best of you.
    A trembling performance of perfection will still feed.

+ [timeout] -> Headmistress_Timeout -> excite
+ [distracted] -> Headmistress_PayAttention -> excite


- For today, you will be challenged on your memory 
of basic hand signals.

Without voice, I shall indicate which position for you to take,
and you will obey without assistance.

- (understand)
Do you understand, {isBoy: tribute|token}?

+ [yes] -> Headmistress_AcknowledgeYes ->
    
+ [no]
    -> Headmistress_CheckIfInterested -> understand
    
    ++ [yes] -> Headmistress_AcknowledgeYes -> 
    ++ [no]
        /perform open_arms_shrug
        -> Headmistress_Unhappy ->
        You waist my time.
        -> exit
    ++ [timeout] -> Headmistress_Timeout -> 
    ++ [distracted] -> Headmistress_PayAttention -> 

+ [timeout] -> Headmistress_Timeout -> understand
+ [distracted] -> Headmistress_PayAttention -> understand

// todo: endurance signals practice w/ audience and w/o pose assist
- We shall begin. 
/character crowd oos
/character headmistress behold
Attend!

-> TestRandom(2) ->

{ disobey == 0:
    I sense an artist within you.
- else:
    I am disappointed, stain. 
}

-> TestRandom(2) ->

{ disobey == 0:
    Dare I witness a moment of perfection?
- else:
    I can barely tolerate such clumsiness.
    You are close to earning a punishment.
}

-> TestRandom(2) ->

{ disobey == 0:
    I shudder with the headiness of such perfection!
    - else:
    You shamefully disappoint your audience.
    /character crowd boos
    /character headmistress
    I can see I have put too much faith in your abilities.
    Next time, I will guide you as one would a child.
    ~ headmistress_signal_basics_difficulty = 1
}

- (turn_around)
/character headmistress arm_raise
Rise and face your audience.

+ [pose:raised_chin]

- {obeyed(): -> GoodEndingHard | -> BadEndingHard}

- ->->

 = GoodEndingHard
//earn favor with HM
/character crowd cheers_loud
/character headmistress arm_raise
I am awed to behold such exquisite grace.
What a delicious feast to devour.
You were born to serve, little one.
~ addReward()
//what do players earn when they max out learning?
~ headmistress_hub_status = unlocked
You have my permission to depart.

- -> exit

= BadEndingHard
//lose favor with HM
/character crowd angry
/character headmistress behold
Your cheeks should be flush with shame, stain.
Your audience is left ravenous and unsatisfied.
We shall resume practice and try this again when you are ready.
Sadly, I must punish your imperfection.
~ addPunishment()
~ headmistress_hub_status = unlocked

Go and contemplate your failure.

- -> exit


// difficulty:
// - 0 = intro
// - 1 = basic test
// - 2 = advanced test

= TestKneel(difficulty)

~ temp say_full_explanation = difficulty == 0
~ temp say_short_explanation = difficulty == 0
~ temp hide_pose_assist = difficulty >= 2
~ temp perfect = true

~ tries = 0
- (retry)
~ tries++

/perform signal_kneel

{ say_short_explanation:
    This position is <b>kneel</b>.
}

{ say_full_explanation:
    You should be quite familiar with it.
    Down onto your knees, chattel.
    Hands resting  upon your thighs.
}

+ [pose:kneel{hide_pose_assist: no_assist}]
    /perform clasp_hands
    {perfect:
        ~ addObey()
        -> Headmistress_GoodJob ->
    - else:
        Much better.
    }
+ {tries == 1} [timeout] 
    /perform clasp_hands
    -> Headmistress_BadJob -> 
 
    ~ perfect = false
    ~ addDisobey()
    ~ say_short_explanation = true
    ~ say_full_explanation = false
    ~ hide_pose_assist = false
    -> retry
+ {tries == 2} [timeout]
    ~ addDisobey()
    ~ say_full_explanation = true
    /perform clasp_hands
    -> Headmistress_BadJob -> retry
+ {tries >= 3} [timeout] 
    ~ addDisobey()
    -> Headmistress_CheckIfInterested -> retry

- ->->

= TestRear(difficulty)

~ temp say_full_explanation = difficulty == 0
~ temp say_short_explanation = difficulty == 0
~ temp hide_pose_assist = difficulty >= 2
~ temp perfect = true

~ tries = 0
- (retry)
~ tries++

/perform signal_rear


{ say_short_explanation:
    The title is <b>rear<b>.
}

{ say_full_explanation:
    Turn around, bend forward, 
    and present your derriere for inspection.
}

+ [pose:rear{hide_pose_assist: no_assist}]
    /perform clasp_hands
    {perfect:
        -> Headmistress_GoodJob ->
        ~ addObey()
    - else:
        I expect more effort.
    }
+ {tries == 1} [timeout] 
    /perform clasp_hands
    -> Headmistress_BadJob ->
    ~ perfect = false
    ~ addDisobey()
    ~ say_short_explanation = true
    ~ say_full_explanation = false
    ~ hide_pose_assist = false
    -> retry
+ {tries == 2} [timeout]
    ~ addDisobey()
    ~ say_full_explanation = true
    /perform clasp_hands
    -> Headmistress_BadJob -> retry
+ {tries >= 3} [timeout]
    ~ addDisobey()
    -> Headmistress_CheckIfInterested -> retry

- ->->

= TestHumble(difficulty)

~ temp say_full_explanation = difficulty == 0
~ temp say_short_explanation = difficulty == 0
~ temp hide_pose_assist = difficulty >= 2
~ temp perfect = true

~ tries = 0
- (retry)
~ tries++

/perform signal_humble

{ say_short_explanation:
    It is named <b>humble</b>.
}

{ say_full_explanation:
    Bow forward to abase yourself before me.
    Arms stretched out before you in submission.
}

+ [pose:humble{hide_pose_assist: no_assist}]
    /perform clasp_hands
    {perfect:
        ~ addObey()
        -> Headmistress_GoodJob ->
    - else:
        Took you long enough.
    }
+ {tries == 1} [timeout] 
    /perform clasp_hands
    -> Headmistress_BadJob ->
    ~ perfect = false
    ~ addDisobey()
    ~ say_short_explanation = true
    ~ say_full_explanation = false
    ~ hide_pose_assist = false
    -> retry
+ {tries == 2} [timeout]
    ~ addDisobey()
    ~ say_full_explanation = true
    /perform clasp_hands
    -> Headmistress_BadJob -> retry
+ {tries >= 3} [timeout]
    ~ addDisobey()
    -> Headmistress_CheckIfInterested -> retry

- ->->

