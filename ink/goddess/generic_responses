=== Goddess_GoodJob ===

{ shuffle:

    - /perform idle happy 50
    There's a good {player_name}. #114

    - /perform idle_tease aroused 65
    What a good little {player_name}. #115

    - /perform talk_lean_forward amused 55
    That's my {player_name}. #116

    - /perform idle_clasp_hands happy 50
    Aren't you the sweetest little thing? #117

    - /perform talk_consider happy 50
    Very nice. #118

    - /perform react_thankful happy 45
    That will do just fine. #119

    - /perform do_inspect happy 40
    Quite satisfying. #120

    - /perform idle_sassy aroused 65
    Mmmm. I like that. #121

    - /perform react_thankful happy 50
    Someone is being a good {player_name}. #122

    - /perform talk_consider happy 50
    Well done. #123

    - /perform idle_tease aroused 40
    That's very pleasing. #124

    - /perform do_inspect aroused 60
    I am pleased. #61
}

->->

=== Goddess_AcknowledgeYes ===

{shuffle:
    - How delightful. #01
    - Very well. #02
    - So be it. #03
    - Alright then. #04
    - Excellent. #05
}

->->

=== Goddess_Unhappy ===

{shuffle:
- How disappointing. #01
- Really? #02
- I see. #03
}

->->

=== Goddess_Timeout ===

{ shuffle:
    - /perform idle_clasp_hands angry 45
    I asked you a question. #11

    - /perform talk_emphatic_one_hand angry 55
    Hurry up. I'm waiting. #12

    - /perform talk_lean_forward skeptical 35
    I don't like it when people make me wait. #13

    - /perform idle_skeptical angry 65
    What makes you think you can ignore me? Answer me. #14
 }
 
->->

=== Goddess_PayAttention ===


{ cycle:
    - /perform idle_skeptical angry 80
    Look at me when I'm speaking to you, {player_name}. #125
    /wait 1

    - /perform talk_annoyed angry 80
    I expect your eyes on me at all times, #126
    unless I tell you otherwise. #127
    /wait 1

    - /perform idle_fake_annoyed angry 80
    Did I give you permission to look elsewhere? #128
    /wait 1

    - /perform talk_behold angry 80
    Didn't anyone ever teach you to look at people #129
    when they're speaking to you? #130
    /wait 1

    - /perform talk_one_finger angry 80
    Pay attention to me when you're in my presence. #131
    /wait 1

    - /perform idle_skeptical angry 80
    You're trying my patience. Pay attention. #132
    /wait 1

    - /perform idle skeptical 80
    Eyes on me, {player_name}. #133
    /wait 1
}

// Added in waits, as the lines felt rushed when transitioning

+ [pay-attention]
+ [timeout] -> Goddess_PayAttention

// TODO: Make # of cycle elements equal or change to cycle/shuffle
- { cycle:

    - /perform talk_consider happy 60
    Where was I? Oh yes... #134
    /wait 1

    - /perform react_scoffs amused 60
    That's better. Let's continue. #135
    /wait 1

    - /perform idle_clasp_hands happy 40
    Good {player_name}. #136
    /wait 1

    - /perform talk_annoyed amused 30
    Let's try that again. #137
    /wait 1

    - /perform idle_unhappy amused 30
    As I was saying... #138
    /wait 1

    - /perform idle_sassy amused 30
    Now... Let's start over. #139
    /wait 1
}

->->


=== Goddess_BadJob ===

/emotion angry 50

// { shuffle:
//     - You dissapoint.
//     - Perhaps you do not belong here, after all.
//     - I tire of your presence.
//     - Try harder, little one.
//     - That's not quite right.
//     - Tiresome.
//     - I expect more.
// }

->->

=== Goddess_CheckIfInterested ===

{checked_if_interested: -> check_terse}

~checked_if_interested = true

/perform idle_unhappy disgusted

These are the sorts of tasks demanded of you here. #148
- (check_again)

/perform talk_behold skeptical

Are you willing to simply do as I say like a good {player_name}? #149

/perform idle

+ [yes] -> IsInterested
+ [no]
+ [timeout] -> check_again

- (check_terse)

/emotion skeptical

Really? #150

/emotion angry 50

We will need to be rid of you if you can't perform. #151

Your willingness to serve is, after all, why you are here. #152

/perform talk_annoyed neutral

So I'll ask one last time: #153

/perform talk_lean_forward angry 50
Are you willing to perform for your goddess? #154

+ [yes] -> IsInterested
+ [no] -> NotInterested


= IsInterested

// -> Goddess_GoodJob ->
/perform react_shrug_long skeptical
There is hope for you yet. #157

->->


= NotInterested

/perform talk_annoyed disgusted
Then away with you. #155
Perhaps another time when you are ready to learn your place. #156

-> Goddess_ByeForNow -> game_outro


=== Goddess_ByeForNow ===

/perform do_snap_fingers

/scene bedroombroken.pissed


+ [wait 1.5]

- (again)
/character goddess idle_looming_huge angry 50

Bye for now, {player_name}. #113
+ [wait:5]
    // {debug: -> again | ->->}
    ->->





