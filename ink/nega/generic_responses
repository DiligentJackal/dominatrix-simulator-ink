   
=== Nega_GoodJob ===
+ [continue]
{shuffle:
    - That's it, little bitch. #01
    - Just like that. #02
    - Maybe you aren't a waste of time. #03
    - Not bad. #04
    - Huh. Maybe I will get to have a little fun after all. #05
    - You're making my pussy wet. #06
    - Toying with you is so much fun! #07
    - Good little {isBoy: meatbag|slit}. #08
    - I'm glad you came to see me. #09
    - Oh, yeah. #10
    - Mmmmm. #11
    - So hot! #12
    - Good fucktoy. #13
    - Nice! #14
    - What a slut! #15
    - I'm impressed, slut. #16
}


-
->->


=== Nega_BadJob ===
/emotion angry 75
{shuffle:
    - Stupid {isBoy:  prick|cunt}. #01
    - You're fucking up! #02
    - You better entertain me, {isBoy: worm|whore}. #03
    - Try harder! #04
    - You're doing it wrong. #05
    - Useless. #06
    - Pathetic. #07
    - Ugggh. #08
    - I'm not happy. #09
    - You're boring me. #10
    - How lame. #11
    - Unworthy slut! #12
    - Grrrr. #13
    - Waste of time. #14
    - I don't know why I bother. #15
}
    
->->

=== Nega_Unhappy ===
/emotion disgusted
//use this for responses from player she doesn't like - verbal

{shuffle:
    - Fucking liar. #01
    - Bullshit. #02
    - Stupid bitch! #03
}


->->

=== Nega_Insult ===
/emotion disgusted
{shuffle:
    - You're so goddamn pitiful. #01
    - Fucking stupid {isBoy: prick|cunt}. #02
    - Jeezus you're so predictable. #03
    - You're so fucking slutty. #04
    - You'd make a better cum bucket. #05
    - Your {hasPenis: tiny cock|gaping cunt} is boring. #06
    - Such a fucking whore. #07
}
    
->->

=== Nega_Apologize ===

-(repeat)
Apologize for being a fuck up. #01
Kiss the ground! #02

+ [pose:kiss_feet]
+ [timeout] -> Nega_BadJob -> repeat

- Keep going until I tell you to stop. #03

+ [pose:kiss_feet]

- That's it, little bitch. #04
Show me how sorry you are. #05

+ [pose:kiss_feet]

- Don't fucking lie to me again. #06
Now get back up on your knees. #07

+ [pose:kneel]

->->

//response to timeouts to questions
=== Nega_Timeout ===
/emotion disgusted 75
{cycle:
    - ...the fuck? I'm talking to you!  #01
    - Don't ignore me!  #02
    - Fucking pay attention! #03
    - Am I talking to a goddamn sack of potatoes? #04
    - You little {isBoy: prick|cunt}. You're wasting my time. #05
    - Answer me, {isBoy: maggot|slut}.  #06
    - Are your ears still attached to your fucking head? #07
    - Get your shit together and pay attention, {isBoy:dog|bitch}.   #08
    - Wake the fuck up! #09
    - You fucking falling asleep on me? #10
    - I'm not a patient Mistress. #11
}

->->

//response to player distraction
=== Nega_PayAttention ===
/emotion angry 75
{ cycle:
    - Eyes on me, pretty {player_name}. #01
    - I'm fucking talking to you! #02
    - Is your brain goddamn mush? Over here. #03
    - Look at me, bitch. #04
    - Were you raised in a fucking barn? Pay attention! #05
    - Get your head in the goddamn game. #06
    - Looking for a place to hide, slut? It won't work. #07
    - Now, pay attention! #08
}

+ [pay-attention]
    { cycle:
        - Don't fucking let it happen again. #09
        - What the fuck was I saying? #10
        - Goddamn rude little whore. #11
        - Do it again and I might think you don't like me. #12
        - I fucking hate being interrupted. #13
        - Damn right, you'll pay attention. #14
    }
    ->->
    
+ [timeout] -> Nega_PayAttention


=== Nega_CheckIfInterested ===

/perform idle_unhappy
What the fuck is wrong with you!? #01

Are you going to behave and give me what I want? #02

+ [yes] 
    That's right, {isBoy: maggot|whore}. #03
    ->->

+ [no]
    Then fuck off. #04

    {reward_just_removed:
        No refunds, you little bitch. #05
        {inTutorial: -> nega_intro.spiteful_interrupted}
    }

    {inTutorial: -> exit }
    
=== Nega_prepare_masturbate ===

{shuffle:
    - {hasPenis: Grab your little cock and start stroking|Reach down and start rubbing your pussy}. #01
    - {hasPenis: Get fucking hard for me!|Start rubbing that fucking pussy!} #02
    - I want that {hasPenis: tiny little dick| gaping pussy} of yours all worked up. #03
    - You're going to rub one out for me, pathetic {isBoy: worm|{isGirl: stain|wretch}}. #04
}
->->

=== Nega_Masturbation_Comments ===

{shuffle:
    - Keep going. #06
    - {hasPenis: Your dick's not hard enough.|Your pussy's not dripping enough.} #17
    - Harder. Faster. #08
    - I bet your arm is getting tired. Too bad! #09
    - More. More. More! #10
    - I enjoy watching you be a little whore for my attention. #14
    - That's it, little meatbag. #15
    - {hasPenis: What a fucking slut getting <i>my</i> cock ready for me to use.|Such a slut getting <i>my</i> pussy so wet!} #23
    - Wallow in it, {isBoy: worm|cunt}. #18
    - Work those fingers. #03
    - Enjoy the view, slut! #16
    - Being so close must be such a turn on for you, whore. #01
    - Only a lucky few get to see something so wonderful. Be grateful! #02
    --> break01 ->
    --> break02 ->
    --> break03 ->
    --> Nega_Objectify->
    --> Nega_Objectify->
}

->->

= break01

Do you like the idea of getting ready for me, {isBoy: worm|skank}? #11

+[yes]
    ~ addObey()
    -> Nega_GoodJob ->
    Good. Keep going. #12

+[no]
    ~ addDisobey()
    -> Nega_BadJob ->
    Do it anyways! #13

- ->->

= break02
Do you want to cum for me? #19

+ [yes]
    No fucking way! #20
    I do the cumming around here! #21

+ [no]
    -> Nega_BadJob ->
    Well, you'll stroke until your dick falls off if that's what I want! #22

- ->->

= break03
Does that feel good whore? #04

+ [yes]
    Thirsty little {isBoy: boy|{isGirl: girl|pet}}. #05


+ [no]
    -> Nega_BadJob ->
    Too fucking bad. I hope it hurts! #07
    
- ->->    

=== Nega_Aroused ===

{shuffle:
    - Ahhhh. Yesss... I'm so fucking wet. #04
    - I can smell my own pussy, fuck toy. #01
    - Your little performance is making me ooze. #02
    - Damn. My cunt is throbbing watching you. #03
    - I can't wait to slide my fingers into my hot cunt. #05
    - I'm going to cum so hard later. #06
}

->->

=== Nega_Objectify === 
+[continue]
/emotion disgusted 75

{shuffle:
    - That's all you're good for, cunt, being used. #19
    - I enjoy watching you be a little whore for my attention. #21
    - You're just a filthy cum bucket to be used and abused. #01
    - What a {hasPenis: tiny little pecker|gaping hole} you have. #02
    - Good thing this House keeps you fucking firm. #03
    - Filthy bag of fuck holes. #04
    - Oooo... look at the little sex doll. #05
    - Such a fucking cum dump.  #06
}
    
->->

=== Nega_Give_Reward ===
//This isn't being used anywhere.
{shuffle:
    - I'm pleased with you! #01
    - You've been good! #02
    - I had a lot of fun with you! #03
    - You weren't as much of a fuckup as I thought you'd be! #04
    - Surprisingly, you satisfied me. #05
    - I hope you're feeling good. I am. #06
    - I enjoyed myself. #07
}
    
{shuffle:
    - Perhaps you deserve a small reward. #08
    - I think I'll do something nice for you. #09
    - I'm going to give you a little treat. #10
    - I want to give you a little something, fuck {player_name}. #11
    - It amuses me to reward you for a reward. #12
    - Time to show my appreciation. #13
}

{ shuffle:
    - -> reward_spit
    - -> reward_ass
}

= reward_spit
/perform talk_lean_forward
Open your mouth, slut! #14

+ [react-success]

TODO: create new animation
//perform spit_in_mouth
+ [wait 3]
- I'm sure you loved that, my disgusting little {isBoy: pig|cunt}! #15

Didn't you? #16

+[yes]
    Haha! Yes! #17
    
+[no]
    Poor little baby. #20
    So abused. #21
    Waaa. #22
    
-   

->->

//decide how to use this or what anims should be used
= reward_ass
//perform reverse_cowgirl_ass_raised_wiggle
//long animation - appx 10 seconds
Being so close must be such a turn on for you, whore. #18
Don't get used to it! #19

->->


