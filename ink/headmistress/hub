=== headmistress_hub ===

-> select_gender_check ->

{inTutorial && tutorial_step != at_hm_intro:
    ~ tutorial_step = at_hm_bonus
    ~ hm_bonus_shortcut = true
    ~ punishments = 0
    ~ rewards = 1
}

~ setHub(Headmistress)

/scene LectureHall.Hub
/music headmistress
/character headmistress clasp_hands

+ [move]

-> apology_check(-> hello, -> exit)


= hello

{ headmistress_regard >= 10: 
    I am pleased to see you, bauble.
- else:
    {shuffle:
        - Greetings, {isBoy: peon|moppet}.
        - Welcome back, flawed one.
        - I see you cannot stay away, trinket.
        - You continue to return.
    }
}

{ 
    - punishments > 0: -> check_punishment
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

Are you here to pay your punishment?
+ [yes] -> choose_punishment
+ [no]
    No?
    -> check_task


= check_reward

Are you here to receive your reward?

+ [yes] -> choose_reward
+ [no]
    No? 
    Abstaining from pleasure, I see.
    How restrained.
    -> check_task


= check_task

{ inTutorial: -> goodbye }

{ punishments == 0 && rewards == 0:
    No rewards or punishments, huh?
    You must be here to serve me!
- else:
    Shall I give you a task then?
}

+ [yes] -> choose_task
+ [no] -> goodbye


= choose_punishment

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_punishment_spanking -> exit
}
//-> headmistress_punishment_flogging -> exit


= choose_reward
/perform behold

{shuffle:
    - Then a reward you shall have.
    - It is so pleasing when trinkets have earned a reward.
    - Rewards are preferable to punishments.
}
  
{inTutorial: -> headmistress_reward_exhibitionism }

// Eventually we'll have the game randomly pick here.
{shuffle:
    - -> headmistress_reward_cunnilingus
    - -> headmistress_reward_exhibitionism
}

= choose_task

{deviation == hand_signals: -> headmistress_task_handsignals -> exit }

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_task_handsignals ->
}

- -> exit


= goodbye

Then I have nothing for you.
Begone.

-> exit


