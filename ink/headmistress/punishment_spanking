VAR headmistress_spanking_target_count = 10
VAR headmistress_spanking_implement = "paddle"
VAR headmistress_spanking_count = 0
VAR headmistress_spanking_break_count = 0
VAR headmistress_spanking_mistakes = 0

=== headmistress_punishment_spanking ===

/scene LectureHall.Punishment_Spanking

~ intro = headmistress_punishment_spanking == 1

{shuffle:
- ~ headmistress_spanking_target_count = 10
- ~ headmistress_spanking_target_count = 15
- ~ headmistress_spanking_target_count = 20
}

{shuffle:
- ~headmistress_spanking_implement = "paddle"
- ~headmistress_spanking_implement = "hand"
- ~headmistress_spanking_implement = "crop"
}

~ headmistress_spanking_break_count = 0
~ headmistress_spanking_count = 0
~ headmistress_spanking_mistakes = 0

{not intro: /action raise_curtains}

/character headmistress
/equip {headmistress_spanking_implement}

For your transgressions, #01
a spanking is in order. #02

//Disobedient behavior must be corrected #37
//immediately upon infraction. #38

//It must be firm, swift, and leave an indelible mark #39
//upon the body and soul. #40
/perform tap_paddle

//{intro:
//    First, you will learn #03
//    how to present yourself for punishment. #04
//}

///perform point_paddle_at_desk

//{intro:
//     Punishments are administered with the slave perched upon my desk. #05
//     Mount with nimble haste. #06
   
// - else:
//     You know what is expected. #07
//     Alight forthwith! #08
// }

/perform idle
{intro: 
    -> hands_and_knees_timeout
}
- (get_on_desk)

+ [move:get_on_desk]
    Pleasingly executed. #09
+ [timeout]
    -- (get_on_desk_timed_out) #10
    Our guests are waiting. #11
    Tut. Tut. #12
    -> get_on_desk
-
{not get_on_desk_timed_out:
    ~obey++
    /perform look_up_and_down
    Observe the eagerness with which our chattel attempts to comply. #13
    This behaviour pleases Her. #14
- else:
    ~disobey++
    /perform displeased
    Note the hesitation. A sure sign of poor breeding. #15
    Such offends the Goddess and must be striken from the specimen. #16
}

- (hands_and_knees_timeout)
Now. Onto your hands and knees, bauble. #17

- (hands_and_knees_fail)
+ [pose:hands_and_knees] -> HoldPose(-> hands_and_knees_fail, -> hands_and_knees_timeout) ->

- /perform amused
//In this position, #18
//splayed fingers must be at least shoulder width apart. #19

//Now then. #20
The back must be delicately arched, #21
presenting the most ideal access #22
to the object of discipline: the buttocks. #23
Lift your backside high, {isBoy: mongrel|moppet}. #24

/perform paddle_desk

//-In this way, the ass is presented for abasement. #25
-Tell me, lump, is that as high as you can lift your hindquarters? #26

+[yes]
    ~obey++
    I disagree. #27
    Arch just a bit more. #28
    ++[wait]
    --Just so. #29
    You will remain in this position without fail. #30

+[no]
    ~disobey++
    You serve yourself and our Goddess poorly, heathen. #31
    Raise your ass! #32

+[timeout]
    What are you waiting for? #33
    Your back must strain with the effort of raising your derrier, chattel. #34


-/perform satisfied
-> Headmistress_GoodJob ->
//
//

{intro: -> punish_intro | -> punish_continue}

= punish_intro

//-To receive a punishment with the utmost grace and decorum, #41
//the subject of discpline must hold their head high #42
//and breath smoothly and calmly through the nose. #43

///perform tap_paddle

-The muscles of the ass must remain relaxed between swats. #44
This provides an exceptionally pleasing display when they clench #45
in response to the impact of my admonishments. #46
/perform tap_paddle

///character crowd murmur

- /character headmistress
But enough of this pretense. #48
We aren't here just for you. #49
We have our entire class to enlighten! #50

/character crowd jeers

- /character headmistress
//Let us show our audience #51
//the fine display you have for us. #52

/perform arm_raise
/action raise_curtains
/character crowd cheers

-> punish_continue

= punish_continue

/character headmistress circle_desk

- Welcome esteemed guests! #53
Our subject today has earned a spanking. #54

{headmistress_spanking_implement == "paddle":
    I will use a paddle made of mahogany. #55
    An excellent, hard wood ideal for leaving #56
    bright red welts on the flesh of disobedient slaves. #57
}

{headmistress_spanking_implement == "hand":
    My own hand will do today. #58
    I want to relish #59
    the crisp strike of my hand #60
    and the quivering sound of flesh meeting flesh. #61
}

{headmistress_spanking_implement == "crop":
    For today's punishment, I have selected #62
    a slender and rigid crop. #63
    The pattern of stinging pain that will burst forth #64
    from the skin is exotic to behold! #65
}

//Would you not agree? #66

/character crowd coos 

- /character headmistress
Listen to that excitement, {player_name}. #67
Your ass will provide a fine demonstration today. #68

Are they not fortunate to have one such as you to observe? #69

+[yes]
    ~obey++
    Perform well and your punishment marker will be removed. #70

+[no]
    ~disobey++
    It is a shame you do not value the learning of others. #71
    
-Friends and students! #72
As always, purrs, moans, and words of encouragement #73
are permitted during the punishment lesson. #74

/character crowd jeers
/character headmistress
    
//Consistently ensure the subject of punishment #75
//assumes the most apt position for spanking. #76
//One that allows for full admiration #77
//of the {isBoy: tip of the shaft|crown of the folds} and puckered rosebud. #78

What say you, {isBoy: toad|strumpet}? #79

{cycle:
    - Will our esteemed guests be enthralled #80
         by {isBoy:your turgid bouncing cock?|the glistening of your cunt?} #81
    - Do you feel their eyes on you? #82
    - Are you throbbing in anticipation? #83
    - Do you want to touch yourself? #84
    - Does pain turn you on? #85
}

+ [yes]
    ~ obey++
    So eager for your debasement. #86
    Most pleasing. #87

+ [no]
    ~ disobey++
    A disappointing response for our guests. #88
    Your lackluster genitals shall be addressed another time. #89
    //We still have your punishment to mete out. #90

- /perform walk_to_front_of_desk
//And finally, we come to the heart of the matter. #91
Do you accept that you must be punished? #92

- (ask_punish_ready)

+[yes]
    ~ obey++
    /perform arm_raise
    Your acceptance is most becoming. #93
    /character crowd cheers
    /character headmistress

+[no]
    ~ disobey++
    /character crowd boos
    /character headmistress suspicious
    Foolish, little slave. #94
    
    It will be over quickly and your compliance will please the Goddess. #95
    Shall we continue with your punishment? #96

    ++[yes]
        ~ obey++
        Magnificent. Let us begin. #97

    ++[no]
        ~ disobey++
        /character crowd angry
        /character headmistress
        Make haste from my sight! #98
        You have squandered my time and my attentions. #99
        I will not suffer fools. #100
        You've just earned yet another punishment. #101
        ~ punishments++
        ~ headmistress_apologize = true
        Now leave. #102
        /perform point_to_door
        -> exit

+[timeout] 
    You have had enough time to think. #103
    Your punishment must begin. #104
    Do you agree? #105
    -> ask_punish_ready

- {intro: -> spank_instructions}

Because I am generous, I will offer you a chance to practice first. #106
Would you welcome that, {isBoy: mongrel|moppet}? #107
~ headmistress_difficulty = 1

+[yes]
    -> spank_instructions
+[no]
    An expert, are we? We will see about that. #108
    -> spank_continue


= spank_instructions

//With each swat, you will show me clearly #109
//just how much you feel it. #110
When you hear the slap of my {headmistress_spanking_implement} #111
and feel its sting upon your ass, #112
I want to see that empty head of yours jerk up and back #113
into a proud and upright position. #114

+[wait]

- /character headmistress lean_forward
//And I am always pleased #115
//with the exquisite sharp inhale of breath #116
//that comes from each blow. #117
Time to rehearse. #118

/scene LectureHall.Punishment_Spanking_Ready
+[wait]

~temp spanks = 0

- (try_spank)
/character headmistress paddle_ass

+ [react-success]
    // This lets us count the number of successful attempts.
    ~spanks++
    
    // Once there have been 3 good spanks, we continue.
    {spanks < 3:
        -> Headmistress_GoodJob ->
        -> try_spank
    - else:
        Just so. #119
        -> spank_continue
    }
+ [react-fail]
    // This lets us count the number of failed attempts.
    ~headmistress_spanking_mistakes++

    // We will continue if they fail 3 times.
    { headmistress_spanking_mistakes < 3:
        Not good enough. Try again. #120
        -> try_spank
    - else:
        ~disobey++
        That will do for now. #121
        We will work on that in the future. #122
    }
    
    ~ headmistress_difficulty = 0

- 

{ headmistress_spanking_mistakes > 0:
    ~disobey++
- else:
    ~obey++
}

// Reset spanking mistakes to track them in the next sections.
~ headmistress_spanking_mistakes = 0

-> spank_continue


= spank_continue

It is time! #123
/character crowd cheers

+ [wait]

- /character headmistress walk_to_back_of_desk

/character crowd appreciation

/character headmistress
Prepare yourself for {headmistress_spanking_target_count} swats, {player_name}. #127

- (spank)
/character headmistress paddle_ass

+ [react-success]
    ~ headmistress_spanking_count++
    ~ headmistress_spanking_break_count++
    -> GoodSpank ->
    {headmistress_spanking_break_count >= 5:
    
        // Check mistakes for obey or disobey adjustment
        {headmistress_spanking_mistakes > 0:
            ~ disobey++
        - else:
            ~ obey++
        }
        
        // Reset the mistake counter.
        ~headmistress_spanking_mistakes = 0
        
        -> SpankBreak ->
    }
    {headmistress_spanking_count < headmistress_spanking_target_count: -> spank}
+ [react-fail] -> BadSpank -> spank

- Ahhhh. Now, don't you feel better, {player_name}? #128
    +[yes]
    ~ obey++
    That is because you desire #129
    to be an obedient little toy. #130
    
    +[no]
    ~disobey--
    Come now, trinket, you comported yourself quite well. #131
    You should be proud of your performance. #132
    
- /perform circle_player
Your ass has blossomed into an objet d'art. #133

- {obeyed(): -> GoodEnding | -> BadEnding}


= GoodEnding
In fact, your performance has been so deliciously obedient #134
I wonder if you would like to perform further for our audience? #135

    +[yes]
    Ahhhh. What a crowd pleaser! #136
    /character crowd cheers
    /character headmistress
    -> Headmistress_player_masturbate ->
    
    +[no]
    A shame you do not have more in you, #137
    but you have done well. #138
    You may descend and return to Mistress Yuki-Onna with my compliments. #139
    /character crowd cheers
    /character headmistress
    -> FinalEnding
    
- -> FinalEnding


= BadEnding
You may have finished enduring your punishment, #140
but your overall performance was quite unsatisfactory. #141
Perhaps you would rather play with yourself for your new fans? #142

    +[yes]
    Too bad. Then you should have thought of that sooner. #143
    /character crowd boos
    /character headmistress
    -> FinalEnding
    
    +[no] 
    Too bad. That's exactly what you're going to do. #144
    /character crowd cheers
    /character headmistress
    -> Headmistress_player_masturbate

= FinalEnding

/character headmistress
    
Consider one demerit paid in full. #145
I give you leave to depart. #146

~ punishments--

/character crowd claps

+ [wait]

// If the player has never seen nega-yuki,
// send them off to be tempted by her directly.
- {not nega_intro: -> nega_intro | -> exit }


// Tunnels


= BadSpank
~ headmistress_spanking_mistakes++
/character headmistress displeased
{shuffle:
    - Terrible. We'll have to repeat that one. #147
    - Tsk. Tsk. You can do better. #148
    - Lift that head, cretin. #149
    - Head up or the next one will land harder. #150
    - Are you even trying? #151
    - Your lack of grace is stunning. #152
    - Where is your pride? Try harder. #153
    - Keep those toes pressed flat on the desk. #154
    - Squirming is not allowed. #155
    - I expect a better performance. #156
    - Your audience is disappointed. #157
    - /character crowd hisses 
    - /character crowd gasps
    - /character headmistress clucks
    -
    -
    -
    -
    
    }
->->

= GoodSpank

{shuffle:
    - A fine welt is forming. #158
    - Music to my ears. #159
    - Perhaps I should be counting. #160
    - A red bottom is a pleasing bottom. #161
    - I do so love instructing slaves in proper behavior. #162
    - I never tire of this. #163
    - I suspect that one hurt. #164
    - /character crowd ooos
    - /character crowd gasps
    - /character headmistress mmms
    - Brilliant! No doubt my guests are impressed by your display. #165
    - Have you been keeping track? #166
    - Notice the beads of sweat forming on the brow. #167
    - Remember to breathe, {isBoy: runt|poppet}. #168
    -
    -
    -
}

{shuffle:
    - Notice how the {isBoy:shaft bounces|nether lips clench}. #169
    - The light is catching the dewy sheen on the skin most magnificently. #170
    - Observe the {isBoy: tip|slit} begin to glisten. #171
    - Oooo. Look at how the anus puckers. #172
    - No doubt several of my students are stimulating themselves right now. #173
    - Some of you are squirming with remembrance right now. #174
    - The nether regions must be fully in view. #35
    - Knees must be sufficiently apart to allow for clear observation. #36
    - All care must be taken to assure #124
    no lasting harm is inflicted... #125
    unless permanency is desired, of course. #126
    - Feet must remain flat with no obscene curling of the toes. #47
    -
    -
    -
    -
    
}

->->


= SpankBreak
~ headmistress_spanking_break_count = 0

{ shuffle:
    - -> break01 -> 
    - -> break02 ->
    - -> break03 ->
}

->->

= break01
Every now and then, the object of correction #175
must be given a moment to compose themself. #176
However, this time should not be squandered with mere rest. #177

Reach down and stroke your {isBoy: shaft|folds}. #178
Punishments must be associated with sensual excitation. #179

Does it feel good to touch yourself, {isBoy: toad|strumpet}? #180

+[yes]
    ~ obey++
    As it should. #181
    You may continue until I instruct you otherwise. #182

+[no]
    ~ disobey++
    What a shame. Do it anyways. #183

-
+[wait 10]
- -> Headmistress_masturbation_comments->

+[wait 10]
- -> Headmistress_masturbation_comments->

+[wait 10]
- -> Headmistress_punishment_resumes->

->->

= break02
Remember to intersperse strikes with caresses. #184
The inflamed flesh is delightfully sensitive #185
and should be titillated as much as possible. #186

The tantalizing drag of fingers on welted skin #187
regularly sends shivers through the subject. #188

Would you enjoy that, {player_name}? #189

+[yes]
    ~ obey++
    Sorry, shivers are reserved for reward spankings only. #190

+[no]
    ~ disobey++
    Good. Then you will be pleased to know that #191
    such kind treatment is reserved for reward spankings only! #192

- -> Headmistress_punishment_resumes ->

->->

= break03
Perhaps our little foible here needs a carrot #193
dangled in front of {isBoy: him|her}. #194
Behaving with decorum and obedience #195
does not simply stave off punishment! #196
Great rewards are bestowed upon the most worthy. #197

/perform circle_player

What do you think, {isBoy: runt|poppet}? #198
Shall I show you the kind of treatment #199
good boys and girls earn? #200

+[yes]
    ~ disobey++
    /character headmistress caress_breasts
    Do you imagine they are soft, {player_name}? #201
    
    ++[yes]
        ~ obey++
        Mmmm. Very soft indeed. #202
    
    ++[no]
        ~ disobey++
        /character headmistress laugh
        Do you imagine they are coarse and uneven, #203
        just like your hindquarters? #204

        +++[yes]
            ~ disobey++
            A brazen response. #205
            Another punishment has been added to your band. #206
            ~ punishments++

        //player should either receive another punishment stone or an additional 5 swats

        +++[no]
            ~ disobey++
            As I surmised. #207
            You are a baffoon who does not know to think before acting. #209

+[no]
    ~ obey++
    /character headmistress pleased
    My my. A surprisingly chaste response #210
    from one whose {isBoy: shaft|mound} is throbbing so fully. #212
    But a delightful one. #213
    You have not earned a reward, #214
    so why bestow a taste upon you. #215

- -> Headmistress_punishment_resumes->

->->


= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform unhappy

Are you too good to take a punishment, {player_name}? #216
Obey instructions. #217
- (check_again)

/perform idle

+ [yes] -> check_yes
+ [no]
+ [timeout] -> check_again

- (check_terse) Well? #218

/perform angry

You merely drag out the invetitable with your delay. #219

/perform annoyed

Are you going to continue to play the reluctant {player_name}? #220

+ [yes] -> check_yes
+ [no]

- /perform bored
Out of my office, heathen. #221
I have no time for your shenanigans. #222
-> out_heathen

- (check_yes) -> Headmistress_GoodJob ->
/perform confused
There is hope for you yet. #223

-> out_heathen

= out_heathen

/perform paddle_desk
-> exit
