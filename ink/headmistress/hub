=== headmistress_hub ===

-> select_gender_check ->

{inTutorial && tutorial_step != at_hm_intro:
    ~ tutorial_step = at_hm_bonus
    ~ hm_bonus_shortcut = true
    ~ punishments = 0
    ~ rewards = 1
}

{deviation == exhibitionism:
    ~ punishments = 0
    ~ rewards = 1
}

~ setHub(Headmistress)

/scene Theatre.Hub
/music headmistress
/ambient none
/character headmistress idle_clasp_hands amused 45

+ [move]

-> apology_check(-> hello, -> exit)


= hello
/wait

{ headmistress_regard >= 10:
    /perform headmistress react_thankful happy 55
    I am pleased to see you, bauble. #01
- else:
    {shuffle:
        - /perform talk_behold amused 45
        Greetings, {isBoy: peon|moppet}. #02

        - /perform talk_one_hand skeptical 75
        Welcome back, flawed one. #03

        -/perform idle_skeptical excited 40
        I see you cannot stay away, trinket. #04

        -/perform idle_bored amused 55
        You continue to return. #05
    }
}

{
    - punishments > 0: -> check_punishment
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

/character headmistress talk_lean_forward skeptical 60
Are you here to pay your punishment? #06
+ [yes] -> choose_punishment
+ [no]
    No? #07
    -> check_task


= check_reward
/character headmistress idle_pleased amused 50
Are you here to receive your reward? #08

+ [yes] -> choose_reward
+ [no]
    /perform react_shrug_short scared 66
    No?  #09

    /perform talk_either_or disgusted 54
    Abstaining from pleasure, I see. #10

    /perform do_inspect amused 40
    How restrained. #11
    -> check_task


= check_task

{ inTutorial && deviation == tutorial: -> goodbye }
{ deviation == exhibitionism: -> goodbye }

{ punishments == 0 && rewards == 0:
    /perform do_shake_finger amused 55
    No rewards or punishments, huh? #12

    /perform do_arm_raise excited 60
    You must be here to serve me! #13
- else:
    /perform idle_pleased amused 40
    Shall I give you a task then? #14
}
/perform idle_pleased amused 

+ [yes]
    -> Headmistress_AcknowledgeYes ->
    -> choose_task

+ [no] -> goodbye


= choose_punishment

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_punishment_spanking -> exit
}
//-> headmistress_punishment_flogging -> exit


= choose_reward
/character headmistress emotion happy 65

{shuffle:
    - /perform react_thankful aroused 70
    Then a reward you shall have. #15

    -/perform do_smooth_skirt happy 45
    It is so pleasing when trinkets have earned a reward. #16

    -/perform react_head_toss_casual skeptical 55
    Rewards are preferable to punishments. #17
}

{inTutorial: -> headmistress_reward_exhibitionism }
{deviation == exhibitionism: -> headmistress_reward_exhibitionism }

// Eventually we'll have the game randomly pick here.
{shuffle:
    // - -> headmistress_reward_cunnilingus
    - -> headmistress_reward_exhibitionism
}

= choose_task

{deviation == hand_signals: -> headmistress_task_handsignals -> exit }

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_task_handsignals ->
}

- -> exit


= goodbye
/character headmistress react_resigned sad 70
Then I have nothing for you. #18

/perform do_you_leave disgusted 65
Begone. #19
/wait 1

-> exit
