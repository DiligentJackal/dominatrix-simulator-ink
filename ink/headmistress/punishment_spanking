VAR headmistress_spanking_target_count = 10
VAR headmistress_spanking_implement = "paddle"
VAR headmistress_spanking_count = 0
VAR headmistress_spanking_break_count = 0
VAR headmistress_spanking_mistakes = 0

=== headmistress_punishment_spanking ===
~ intro = headmistress_punishment_spanking == 1

{shuffle:
- ~ headmistress_spanking_target_count = 10
- ~ headmistress_spanking_target_count = 15
- ~ headmistress_spanking_target_count = 20
}

{shuffle:
- ~headmistress_spanking_implement = "paddle"
- ~headmistress_spanking_implement = "hand"
- ~headmistress_spanking_implement = "crop"
}

~ headmistress_spanking_break_count = 0
~ headmistress_spanking_count = 0
~ headmistress_spanking_mistakes = 0

{not intro: /scene office_raised_curtains}

/character headmistress
/equip {headmistress_spanking_implement}

For your transgressions,
a spanking is in order.

{intro:
    First, you will learn
    how to present yourself for punishment.
}

/perform point_paddle_at_desk

{intro:
    Punishments are administered with the slave perched upon my desk.
    Mount with nimble haste. 
   
- else:
    You know what is expected.
    Alight forthwith!
}

/perform idle

- (get_on_desk)

+ [move:get_on_desk]
    Pleasingly executed.
+ [timeout]
    -- (get_on_desk_timed_out)
    Our guests are waiting.
    Tut. Tut.
    -> get_on_desk
-
{not get_on_desk_timed_out:
    ~obey++
    /perform look_up_and_down
    Observe the eagerness with which our chattel attempts to comply.
    This behaviour pleases Her.
- else:
    ~disobey++
    /perform displeased
    Note the hesitation. A sure sign of poor breeding.
    Such offends the Goddess and must be striken from the specimen.
}

- (hands_and_knees_timeout)
Now. Onto your hands and knees, bauble.

- (hands_and_knees_fail)
+ [pose:hands_and_knees] -> HoldPose(-> hands_and_knees_fail, -> hands_and_knees_timeout) ->

- /perform amused
In this position, 
splayed fingers must be at least shoulder width apart.

+[wait]

- /perform amused

Now then.
The back must be delicately arched, 
presenting the most ideal access 
to the object of discipline: the buttocks.
Lift your backside high, {isBoy: mongrel|moppet}.

/perform paddle_desk

+[wait]

-In this way, the ass is presented for abasement.
Tell me, lump, is that as high as you can lift your hindquarters?

+[yes]
    ~obey++
    I disagree.
    Arch just a bit more.
    ++[wait]
    --Just so.
    You will remain in this position without fail.

+[no]
    ~disobey++
    You serve yourself and our Goddess poorly, heathen.
    Raise your ass!

+[timeout]
    What are you waiting for?
    Your back must strain with the effort of raising your derrier, chattel.


-/perform satisfied
-> Headmistress_GoodJob ->
The nether regions must be fully in view.
Knees must be sufficiently apart to allow for clear observation.

{intro: -> punish_intro | -> punish_continue}

= punish_intro

Disobedient behavior must be corrected
immediately upon infraction.

It must be firm, swift, and leave an indelible mark
upon the body and soul.
/perform tap_paddle

+[wait]

-To receive a punishment with the utmost grace and decorum,
the subject of discpline must hold their head high and
breath smoothly and calmly through the nose.

/perform tap_paddle

+[wait]

-The muscles of the ass must remain relaxed between swats
to provide an exceptionally pleasing display when they clench
in response to the impact of my admonishments.
/perform tap_paddle

+[wait]

-Feet must remain flat with no obscene curling of the toes.
/perform tap_paddle
/character crowd murmur

+[wait]

- But enough of this pretense.
We aren't here for just you.
We have our entire class to enlighten!

/character crowd jeers

+[wait]

- Let us show our audience
the fine display you have for us.

/character headmistress arm_raise
/action raise_curtains
/character crowd cheers

-> punish_continue

= punish_continue

/perform circle_desk

+[wait]

- Welcome esteemed guests!
Our subject today has earned a spanking.

{headmistress_spanking_implement == "paddle":
    I will use a paddle made of mahogany.
    An excellent, hard wood ideal for leaving
    bright red welts on the flesh of disobedient slaves.
}

{headmistress_spanking_implement == "hand":
    TODO: bare hand description
}

{headmistress_spanking_implement == "crop":
    TODO: crop description
}

Would you not agree?

/character crowd coos 

+ [wait]

- Listen to that excitement, {player_name}.
Your ass will provide a fine demonstration today.

Are they not fortunate to have one such as you to observe?

+[yes]
    ~obey++
    Perform well and your punishment marker will be removed.

+[no]
    ~disobey++
    It is a shame you do not value the learning of others.
    
-Friends and students!
As always, purrs, moans, and words of encouragement
are permitted during the punishment lesson.

/character crowd jeers
/character headmistress
    
Consistently ensure the subject of punishment assumes the 
most apt position for spanking as it 
allows for full admiration of the 
{isBoy: tip of the shaft|crown of the folds} and puckered rosebud.

What say you, {isBoy: toad|strumpet}?

{cycle:
    - Will our esteemed guests be enthralled by
        {isBoy:your turgid bouncing cock?|the glistening of your cunt?}
    - Do you feel their eyes on you?
    - Are you throbbing in anticipation?
    - Do you want to touch yourself?
    - Does pain turn you on?
}

+ [yes]
    ~ obey++
    So eager for your debasement.
    Most pleasing.

+ [no]
    ~ disobey++
    A disappointing response for our guests.
    Your lackluster genitals shall be addressed another time.
    We still have your punishment to mete out.

- /perform walk_to_front_of_desk
And finally, we come to the heart of the matter.
Do you accept that you must be punished?

- (ask_punish_ready)

+[yes]
    ~ obey++
    /perform arm_raise
    Your acceptance is most becoming.
    /character crowd cheers

+[no]
    ~ disobey++
    /character crowd boos
    /character headmistress suspicious
    Foolish, little slave.
    
    
    It will be over quickly and your compliance will
    please the Goddess.
    Shall we continue with your punishment?

    ++[yes]
        ~ obey++
        Excellent choice. Let us begin.

    ++[no]
        ~ disobey++
        /character crowd angry
        Make haste from my sight!
        You have squandered my time
        and my attentions.
        I will not suffer fools.
        You've just earned yet another punishment.
        ~ punishments++
        ~ headmistress_apologize = true
        Now leave.
        /character headmistress point_to_door
        -> exit

+[timeout] 
    You have had enough time to think.
    Your punishment must begin.
    Do you agree?
    -> ask_punish_ready

- +[wait]

- {intro: -> spank_instructions}

Because I am generous,
I will offer you a chance to practice first.
Would you welcome that, {isBoy: mongrel|moppet}?
~ headmistress_difficulty = 1

+[yes]
    -> spank_instructions
+[no]
    An expert, are we? We will see about that.
    -> spank_continue


= spank_instructions

With each swat, you will show me clearly
just how much you feel it.
When you hear the slap of my {headmistress_spanking_implement}
and feel its sting upon your ass,
I want to see that empty head of yours jerk up and back
into a proud and upright position.

+[wait]

/character headmistress lean_forward
- And I am always pleased
with the exquisite sharp inhale of breath 
that comes from each blow.
Time to rehearse.

+[wait]

~temp spanks = 0

- (try_spank)
/character headmistress paddle_ass

+ [reaction:spanked]
    // This lets us count the number of successful attempts.
    ~spanks++
    
    // Once there have been 3 good spanks, we continue.
    {spanks < 3:
        -> Headmistress_GoodJob ->
        -> try_spank
    - else:
        Just so.
        -> spank_continue
    }
+ [reaction:failed]
    // This lets us count the number of failed attempts.
    ~headmistress_spanking_mistakes++

    // We will continue if they fail 3 times.
    { headmistress_spanking_mistakes < 3:
        Not good enough. Try again.
        -> try_spank
    - else:
        ~disobey++
        That will do for now. We will work on that in the future.
    }
    
    ~ headmistress_difficulty = 0

- 

{ headmistress_spanking_mistakes > 0:
    ~disobey++
- else:
    ~obey++
}

// Reset spanking mistakes to track them in the next sections.
~ headmistress_spanking_mistakes = 0

-> spank_continue


= spank_continue

It is time!
/character crowd cheers

+ [wait]

/character headmistress walk_to_back_of_desk

- All care must be taken to assure
no lasting harm is inflicted...
unless permanency is desired, of course.

/character crowd appreciation

- Prepare yourself for {headmistress_spanking_target_count} swats, {player_name}.

- (spank)
/character headmistress paddle_ass

+ [reaction:spanked]
    ~ headmistress_spanking_count++
    ~ headmistress_spanking_break_count++
    -> GoodSpank ->
    {headmistress_spanking_break_count >= 5:
    
        // Check mistakes for obey or disobey adjustment
        {headmistress_spanking_mistakes > 0:
            ~ disobey++
        - else:
            ~ obey++
        }
        
        // Reset the mistake counter.
        ~headmistress_spanking_mistakes = 0
        
        -> SpankBreak ->
    }
    {headmistress_spanking_count < headmistress_spanking_target_count: -> spank}
+ [reaction:failed] -> BadSpank -> spank

- Ahhhh. Now, don't you feel better, {player_name}?
    +[yes]
    ~ obey++
    That is because you desire 
    to be an obedient little toy.
    
    +[no]
    ~disobey--
    Come now, trinket, you comported yourself
    quite well.
    You should be proud of your performance.
    
/character headmistress circle_player
- Your ass has blossomed into an objet d'art.

- {obeyed(): -> GoodEnding | -> BadEnding}


= GoodEnding
In fact, your performance has been so deliciously obedient
I wonder if you would like to perform further for our audience?

    +[yes]
    Ahhhh. What a crowd pleaser!
    /character crowd cheers
    -> Headmistress_player_masturbate ->
    
    +[no]
    A shame you do not have more in you, 
    but you have done well. 
    You may descend and return to Mistress Yuki-Onna with my compliments.
    -> FinalEnding
    
- -> FinalEnding


= BadEnding
You may have finished enduring your punishment, 
but your overall performance was quite unsatisfactory.
Perhaps you would rather play with yourself 
for your new fans?

    +[yes]
    Too bad. Then you should have thought of that sooner.
    -> FinalEnding
    
    +[no] 
    Too bad. That's exactly what you're going to do.
    -> Headmistress_player_masturbate

= FinalEnding

Consider one demerit paid in full.
I give you leave to depart.

~ punishments--


/character crowd claps

+ [wait]

// If the player has never seen nega-yuki,
// send them off to be tempted by her directly.
{not nega_intro: -> nega_intro | -> exit }


// Tunnels


= BadSpank
~ headmistress_spanking_mistakes++
/character headmistress displeased
{shuffle:
    - Terrible. We'll have to repeat that one.
    - Tsk. Tsk. You can do better.
    - Lift that head, cretin.
    - Head up or the next one will land harder.
    - Are you even trying?
    - Your lack of grace is stunning.
    - Where is your pride? Try harder.
    - Keep those toes pressed flat on the desk.
    - Squirming is not allowed.
    - I expect a better performance.
    - Your audience is disappointed.
    - /character crowd hisses 
    - /character crowd gasps
    - /character headmistress clucks
    -
    -
    -
    -
    
    }
->->

= GoodSpank

{shuffle:
    - A fine welt is forming.
    - Music to my ears.
    - Perhaps I should be counting.
    - A red bottom is a pleasing bottom.
    - I do so love instructing slaves in proper behavior.
    - I never tire of this.
    - I suspect that one hurt.
    - /character crowd ooos
    - /character crowd gasps
    - /character headmistress mmms
    - Brilliant! No doubt my guests are impressed by your display.
    - Have you been keeping track?
    - Notice the beads of sweat forming on the brow.
    - Remember to breathe, {isBoy: runt|poppet}.
    -
    -
    -
}

{shuffle:
    - Notice how the {isBoy:shaft bounces|nether lips clench}.
    - The light is catching the dewy sheen on the skin most magnificently.
    - Observe the {isBoy: tip|slit} begin to glisten.
    - Oooo. Look at how the anus puckers.
    - No doubt several of my students are stimulating themselves right now.
    - Some of you are squirming with remembrance right now.
    - 
    -
    -
}

->->


= SpankBreak
~ headmistress_spanking_break_count = 0

{ shuffle:
    - -> break01 -> 
    - -> break02 ->
    - -> break03 ->
}

->->

= break01
Every now and then, the object of correction must 
be given a moment to compose themself.
However, this time should not be squandered with mere rest.

Reach down and stroke your {isBoy: shaft|folds}.
Punishments must be associated with sensual excitation.

Does it feel good to touch yourself, {isBoy: toad|strumpet}?

+[yes]
~ obey++
As it should.
You may continue until I instruct you otherwise.

+[no]
~ disobey++
What a shame. Do it anyways.

-
+[wait 10]
-> Headmistress_masturbation_comments->

-
+[wait 10]
-> Headmistress_masturbation_comments->

-
+[wait 10]
-> Headmistress_punishment_resumes->

->->

= break02
Remember to intersperse strikes with caresses.
The inflamed flesh is delightfully sensitive 
and should be titillated as much as possible.

The tantalizing drag of fingers on welted
skin regularly sends shivers through the subject.

Would you enjoy that, {player_name}?

+[yes]
    ~ obey++
    Sorry, shivers are reserved for reward spankings only.

+[no]
    ~ disobey++
    Good. Then you will be pleased to know that 
    such kind treatment is reserved for reward spankings only!

-
-> Headmistress_punishment_resumes ->

->->

= break03
Perhaps our little foible here needs a carrot
dangled in front of {isBoy: him|her}.
Behaving with decorum and obedience does not 
simply stave off punishment!
Great rewards are bestowed upon the most worthy.
/character headmistress circle_player

What do you think, {isBoy: runt|poppet}?
Shall I show you the kind of treatment
good boys and girls earn?

+[yes]
    ~ disobey++
    /character headmistress caress_breasts
    Do you imagine they are soft, {player_name}?
    
    ++[yes]
        ~ obey++
        Mmmm. Very soft indeed.
    
    ++[no]
        ~ disobey++
        /character headmistress laugh
        Do you imagine they are coarse and uneven, 
        just like your hindquarters?

        +++[yes]
            ~ disobey++
            A brazen response.
            Another punishment has been added to your band.
            ~ punishments++

        //player should either receive another punishment stone or an additional 5 swats

        +++[no]
            ~ disobey++
            As I surmised. 
            You are a baffoon who does not 
            know to think before acting.

+[no]
    ~ obey++
    /character headmistress pleased
    My my. A surprisingly chaste response
    from one whose {isBoy: shaft|mound} is throbbing
    so fully.
    But a delightful one.
    You have not earned a reward, 
    so why bestow a taste upon you.

- -> Headmistress_punishment_resumes->

->->


= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform unhappy

Are you too good to take a punishment, {player_name}? 
Obey instructions.
- (check_again)

/perform idle

+ [yes] -> check_yes
+ [no]
+ [timeout] -> check_again

- (check_terse) Well?

/perform angry

You merely drag out the invetitable with your delay.

/perform annoyed

Are you going to continue to play the reluctant, {player_name}?

+ [yes] -> check_yes
+ [no]

- /perform bored
Out of my office, heathen.
I have no time for your shenanigans.
-> out_heathen

- (check_yes) -> Headmistress_GoodJob ->
/perform confused
There is hope for you yet. #157

-> out_heathen

= out_heathen

/perform paddle_desk
-> exit
