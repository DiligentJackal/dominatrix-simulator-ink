VAR headmistress_spanking_target_count = 10
VAR headmistress_spanking_implement = "hand"
VAR headmistress_spanking_count = 0
VAR headmistress_spanking_break_count = 0
VAR headmistress_spanking_mistakes = 0


=== headmistress_punishment_spanking ===

~ setScene(punishment_scene, Headmistress)

TODO: Support shuffling swats and equipment

// {shuffle:
// - ~ headmistress_spanking_target_count = 10
// - ~ headmistress_spanking_target_count = 25
// - ~ headmistress_spanking_target_count = 35
// }

// Just use '10' swats for now.
~ headmistress_spanking_target_count = 10

// {shuffle:
// - ~headmistress_spanking_implement = "paddle"
// - ~headmistress_spanking_implement = "hand"
// - ~headmistress_spanking_implement = "crop"
// }

// Just use the hand for now.
~ headmistress_spanking_implement = "hand"

~ headmistress_spanking_break_count = 0
~ headmistress_spanking_count = 0
~ headmistress_spanking_mistakes = 0


/scene LectureHall.Punishment_Spanking
/music headmistress-punishment
/character headmistress
// /equip {headmistress_spanking_implement}
// /perform circle_desk

-> get_on_desk ->

{inTutorial: -> punish_intro | -> punish_continue}


= get_on_desk

{inTutorial: 
    Punishments are administered with the slave perched upon my desk. #05
- else:
    For your transgressions, #01
    a spanking is in order. #02
}

/character headmistress point_paddle_at_desk
Alight forthwith! #08

- (prompt)
/perform idle

+ [move:get_on_desk]
    Pleasingly executed. #09
+ [timeout]
    -- (get_on_desk_timed_out)
    Our guests are waiting.<br/>Tut. Tut. #12
    -> prompt


- /perform point_down
 Now, kneel, slave. #225

~ tries = 0
- (kneel)
~ tries++

+ [pose:kneel]
+ { tries < 2 } [timeout] -> kneel
//+ { tries >= 2 } [timeout]

-
{tries == 1:
    ~ addObey()
    /perform look_up_and_down
    Observe the eagerness with which our chattel attempts to comply. #13
    This behaviour pleases Her. #14
- else:
    ~ addDisobey()
    /perform displeased
    Note the hesitation. A sure sign of poor breeding. #15
    Such offends the Goddess and must be stricken from the specimen. #16
}

->->

= punish_intro

- /perform one_finger
The muscles of the ass must remain relaxed between swats. #44
/perform behold
This provides an exceptionally pleasing display when they clench #45
/perform serious
in response to the impact of my admonishments. #46
//perform tap_paddle


- /character headmistress smooth_skirt
But enough of this pretense. #48
/character crowd murmur
/ambient crowd
/character headmistress idle
We aren't here just for you. #49
/character headmistress serious
We have our entire class to enlighten! #50

- 
Let us show our audience #51
the fine display you have for us. #52
/character crowd cheers_loud
~ Headmistress_RaiseCurtains()

-> punish_continue

= punish_continue

/character headmistress arm_raise

Welcome esteemed guests! #53

/perform idle
Our subject today has earned a spanking. #54

/perform serious
{headmistress_spanking_implement == "paddle":
    I will use a paddle made of mahogany. #55
    /perform behold
    An excellent, hard wood ideal for leaving #56
    bright red welts on the flesh of disobedient slaves. #57
}

{headmistress_spanking_implement == "hand":
    My own hand will do today. #58
    /perform behold
    I want to relish the crisp strike of my hand #60
    and the quivering sound of flesh meeting flesh. #61
}

{headmistress_spanking_implement == "crop":
    For today's punishment, I have selected #62
    a slender and rigid crop. #63
    /perform behold
    The pattern of stinging pain that will burst forth #64
    from the skin is exotic to behold! #65
}

/character crowd oos 

- /character headmistress laugh
Listen to that excitement, {player_name}. #67
Your ass will provide a fine demonstration today. #68

/perform open_arms_shrug
Are they not fortunate to have one such as you to observe? #69

+[yes]
    ~ addObey()
    /perform serious
    Perform well and your punishment marker will be removed. #70

+[no]
    ~ addDisobey()
    /perform displeased
    It is a shame you do not value the learning of others. #71
    
- /perform behold
As always, purrs, moans, and words of encouragement #73
are permitted during the punishment lesson. #74

/character crowd oos
/character headmistress idle

What say you, {isBoy: toad|strumpet}? #79

{cycle:
    - Will our esteemed guests be enthralled #80
     by {hasPenis:your turgid bouncing cock?|the glistening of your cunt?} #81
    - Do you feel their eyes on you? #82
    - Are you throbbing in anticipation? #83
    - Do you want to touch yourself? #84
    - Does pain turn you on? #85
}

+ [yes]
    ~ addObey()
    /perform curious
    /character crowd cheers 
    /character headmistress idle
    So eager for your debasement. #86
    Most pleasing. #87

+ [no]
    ~ addDisobey()
    /perform thoughtful
    /character crowd boos
    /character headmistress
    A disappointing response for our guests. #88
    /perform serious
    Rectify this immediately, slave. #226

- /perform idle
Rub yourself until your {hasPenis: shaft is hard|slit is wet}. #227
 
+ [wait:3]

- -> Headmistress_masturbation_comments ->

+ [wait 3]

- Pray continue. #228

+ [wait:3]

- -> Headmistress_masturbation_comments ->

+ [wait:3]

- -> Headmistress_masturbation_comments ->

+ [wait 3]

- A little more, I think. #229
 
+ [wait:3]

- -> Headmistress_masturbation_comments ->

- (accept_punishment)

+ [continue]

- /character headmistress
 What say you, slave, will your {hasPenis: rod|folds} please our guests? #230

+ [yes]
    Excellent. Then let us continue. #231
      
+ [no]
    A disappointment. We shall continue anyways. #232
  
- /perform smooth_skirt

And finally, we come to the heart of the matter. #91

- (punishment_ready)

Do you accept that you must be punished? #92

- (ask_punish_ready)

+ [yes]
    ~ addObey()
    /perform arm_raise
    Your acceptance is most becoming. #93
    /character crowd cheers
    /character headmistress idle

+ [no]
    ~ addDisobey()
    /character crowd boos
    /character headmistress suspicious
    Foolish, little slave. #94
    
    It will be over quickly and your compliance will please the Goddess. #95
    /perform inspect
    Shall we continue with your punishment? #96

    ++ [yes]
        ~ addObey()
        /perform serious
        Magnificent. Let us begin. #97
TODO: This leave should not reset the game. It is too early in the tutorial. They should have to go back to Yuki and be told they can't continue until they accept a punishment with the Headmistress.
    ++ [no]
        ~ addDisobey()
        /character crowd angry_crowd
        /character headmistress unhappy
        Make haste from my sight! #98
        You have squandered my time and my attentions. #99
        I will not suffer fools. #100

        {not inTutorial:
            You've just earned yet another punishment. #101
            ~ addPunishment()
            ~ headmistress_apologize = true
        }

        Now leave. #102
        /perform point_to_leave
        -> exit

    ++ [timeout] 
        /perform pleased
        You have had enough time to think. #103
        Your punishment must begin. #104
        Do you agree? #105
        -> ask_punish_ready

+ [timeout] 
    /perform pleased
    You have had enough time to think. #103
    Your punishment must begin. #104
    Do you agree? #105
    -> ask_punish_ready


- {inTutorial: -> headmistress_train_spanking(false) -> spank_continue }

- /perform serious
Because I am generous, #234
I will offer you a chance to practice first. #106
/perform amused
Would you welcome that, {isBoy: mongrel|moppet}? #107
~ headmistress_difficulty = 1

+[yes]
    -> headmistress_train_spanking(false) ->

+[no]
    /perform nod
    An expert, are we? We will see about that. #108

- (spank_continue)

/perform curious
Back onto your hands and knees, slave. #244

+ [pose:hands_and_knees]

/perform idle
Let us begin in earnest! #245
/character crowd cheers_loud

+ [wait]

- /scene LectureHall.HeadmistressAtBack

// /character headmistress walk_to_back_of_desk

/character crowd murmur

/character headmistress idle

// 10, 25, 35
Prepare yourself for ten swats, {player_name}. #127

- (spank)
/character headmistress paddle_ass

+ [react-success]
    ~ headmistress_spanking_count++
    ~ headmistress_spanking_break_count++
    
    {headmistress_spanking_break_count %2 == 0: 
    -> Headmistress_GoodSpank ->
    }
    
    {headmistress_spanking_break_count >= 6:
    
        // Check mistakes for obey or disobey adjustment
        {headmistress_spanking_mistakes > 0:
            ~ addDisobey()
        - else:
            ~ addObey()
        }
        
        // Reset the mistake counter.
        ~headmistress_spanking_mistakes = 0
        
        -> SpankBreak ->
    }
    {headmistress_spanking_count < headmistress_spanking_target_count: -> spank}
+ [react-fail] -> Headmistress_BadSpank -> spank

- -> SpankingFinished

= SpankingFinished
/scene LectureHall.HeadmistressAtFront
/character headmistress satisfied
Ahhhh. Now, don't you feel better, {player_name}? #128

+[yes]
    /perform pleased
    ~ addObey()
    That is because you desire #129
    to be an obedient little toy. #130
    
+[no]
    /perform amused
    ~ addDisobey()
    Too bad. You should have thought of that sooner. #258
    
- /perform behold
Your ass has blossomed into an objet d'art. #133
/perform nod    
Consider one demerit paid in full. #145
~ removePunishment()
~ has_learned_spanking = true
/character crowd cheers_loud
/character headmistress

- {obeyed(): -> GoodEnding | -> BadEnding}

//Story branch: if player obeys and performs well, they are given the chance to collect their reward from the HM and masturbate for the crowd - skips the Nega Yuki reward scene and finishes with praise from the Goddess
= GoodEnding
/perform arm_raise
You comported yourself quite well. #131
What a crowd pleaser! #136
You should be proud of your performance. #132

In fact, your performance has been so deliciously obedient #134
I wonder if you would like to perform further for our audience? #135
TODO: record new VO lines
You may claim your reward now if you do.

+ [yes]
    ~ removeReward()
    /perform confused
    /character crowd claps
    /character headmistress
    Ahhhh. What a crowd pleaser! #136
    You shall masturbate for them as your reward.
    /character headmistress idle
    -> Headmistress_player_masturbate ->
    
+ [no]
    /perform serious
    A shame you do not have more in you, #137
    but you have done well. #138
    /character crowd cheers
    /character headmistress
     -> FinalEnding

- {inTutorial:
    -> surprise_interrupt
}

= BadEnding
/perform sassy
You may have finished enduring your punishment, #140
but your overall performance was quite unsatisfactory. #141
You have a long ways to go. #249

// Perhaps you would rather play with yourself for your new fans? #142

// + [yes]
//     /perform confused_nega
//     Too bad. Then you should have thought of that sooner. #143
//     /character crowd boos
//     /character headmistress

// + [no] 
//     /perform amused
    
//     Too bad. That's exactly what you're going to do. #144
//     /character crowd cheers
//     /character headmistress
//     -> Headmistress_player_masturbate -> 

- -> FinalEnding

= FinalEnding

/character headmistress
/perform idle

~ tries = 0
- (stand_straight)
~ tries++

/perform one_finger
Stand at attention! #257

+ [pose:raised_chin]
+ { tries < 2 } [timeout] -> stand_straight
+ { tries >= 2 } [timeout] -> Headmistress_Timeout -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

/perform point_down
- {obeyed(): 
        You may descend and return to Mistress Yuki-Onna with my compliments. #139
    - else: 
        I give you leave to depart. #146
}

- + [move]


// If in the tutorial, send player off to be tempted by her directly.
- {inTutorial: 
    ~ tutorial_hm_intro_complete = true
    -> nega_hub
- else:
    -> exit
}




// Tunnels


= SpankBreak

~ headmistress_spanking_break_count = 0

- /scene LectureHall.HeadmistressAtFront

- {cycle: 
    - You may rest back on your heels, slave. #251
    - Sit upright for your fans, slave. #252
    - Raise yourself up and sit upon your heels, slave. #253
    }

~ tries = 0
- (kneel_break)
~ tries++

+ [pose:kneel]
+ { tries < 2 } [timeout] -> kneel_break
//+ { tries >= 2 } [timeout]

- 

{ shuffle:
    - -> break01 -> 
    - -> break02 ->
    - -> break03 ->
    //- -> break04 ->
}

+ [continue]

- {cycle:
    - Back on your hands and knees, slave. #254
    - Resume your spanking position, slave. #255
    - Present your ass, {isBoy: runt|strumpet}. #256
}

+ [pose:hands_and_knees]

- /scene LectureHall.HeadmistressAtBack
->->


= break01

/perform thoughtful
Have you been keeping count {isBoy: toad|strumpet}?

+[yes]
 ~ addObey()
    /perform nod
    Let's see how well.
    Are we up to seven now?
    
        ++[yes]
        Tsk. Tsk.
        We are only at six. 
        Time to start over!
        I suggest you remember to count this time, foible.
        ~ headmistress_spanking_count = 0
        -> spank
        
        ++[no]
        Paying attention, are we?
        I hope you know we are only at six.
        Let us finish your puishment.
        ->->
        
+[no]
 ~ addDisobey()
    Do you hear that, my students?
    This one has no breeding or experience.
    It fails to count.
    We shall start over!
    This flaw I shall address in the future.
     ~ headmistress_spanking_count = 0
    -> Headmistress_punishment_resumes ->
    -> spank 


= break02

/perform serious
Every now and then, the object of correction #175
must be given a moment to compose themself. #176
/character crowd murmur

/character headmistress one_finger
However, this time should not be squandered with mere rest. #177

/perform idle
Reach down and stroke your {hasPenis: shaft|folds}. #178
/character crowd gasps
/character headmistress
Punishments must be associated with sensual excitation. #179

Does it feel good to touch yourself, {isBoy: toad|strumpet}? #180
//need moans or some other sexual sound during this scene
+[yes]
    ~ addObey()
    /perform nod
    As it should. #181
    You may continue until I instruct you otherwise. #182

+[no]
    ~ addDisobey()
    What a shame. Do it anyways. #183

- /character headmistress idle
+[wait:5]
- -> Headmistress_masturbation_comments ->

+[wait:5]
- -> Headmistress_masturbation_comments ->

+[wait:5]
- -> Headmistress_punishment_resumes ->

->->


= break03

/perform smooth_skirt
Rise and present your ass that my students may inspect your progress thus far.

+ [pose:present_ass]
--> Headmistress_AcknowledgeYes ->

/character crowd gasps
/character headmistress

- /perform one_finger
Remember to intersperse strikes with caresses. #184

/perform behold
The inflamed flesh is delightfully sensitive #185
and should be titillated as much as possible. #186

/perform amused
The tantalizing drag of fingers on welted skin #187
regularly sends shivers through the subject. #188

/perform idle
Would you enjoy that, {player_name}? #189

+[yes]
    /perform head_toss
    ~ addObey()
    Sorry, shivers are reserved for reward spankings only. #190

+[no]
    /perform amused
    ~ addDisobey()
    Good. Then you will be pleased to know that #191
    such kind treatment is reserved for reward spankings only! #192

- -> Headmistress_punishment_resumes ->

->->


= break04

Perhaps our little foible here needs a carrot #193
dangled in front of {isBoy: him|her}. #194
/character crowd murmur

/character headmistress satisfied
Great rewards are bestowed upon the most worthy. #197

/perform curious
What do you think, {isBoy: runt|poppet}? #198
Shall I show you the kind of treatment #199
good boys and girls earn? #200

+[yes]
    ~ addDisobey()
    /character headmistress caress_breasts_large
    Do you imagine they are soft, {player_name}? #201
    
    ++[yes]
        ~ addObey()
        /character headmistress caress_breasts_large
        Mmmm. Very soft indeed. #202
    
    ++[no]
        ~ addDisobey()
        /character headmistress laugh
        Do you imagine they are coarse and uneven, #203
        just like your hindquarters? #204

        +++[yes]
            ~ addDisobey()
            /character crowd gasps
            /character headmistress displeased
            A brazen response. #205
            Another punishment has been added to your band. #206
            ~ addPunishment()

        //player should either receive another punishment stone or an additional 5 swats

        +++[no]
            ~ addDisobey()
            /perform nod
            As I surmised. #207
            /perform confused
            You are a buffoon who does not know to think before acting. #209

+[no]
    ~ addObey()
    /character headmistress pleased
    My my. A surprisingly chaste response #210
    from one whose {hasPenis: shaft|mound} is throbbing so fully. #212
    
    /perform idle
    But a delightful one. #213
    You have not earned a reward, #214
    so why bestow a taste upon you. #215

/character headmistress
- -> Headmistress_punishment_resumes ->

->->


= CheckIfInterested

{checked_if_interested: -> check_terse}

~ checked_if_interested = true

/perform unhappy

Are you too good to take a punishment, {player_name}? #216
Obey instructions. #217
- (check_again)

/perform idle

+ [yes] -> check_yes
+ [no]
+ [timeout] -> check_again

- (check_terse) Well? #218

/perform angry
You merely drag out the inevitable with your delay. #219

/perform annoyed
Are you going to continue to play the reluctant {player_name}? #220

+ [yes] -> check_yes
+ [no]

- /perform bored
Out of my office, heathen. #221
I have no time for your shenanigans. #222
-> out_heathen

- (check_yes) -> Headmistress_GoodJob ->
/perform confused
There is hope for you yet. #223

-> out_heathen


= out_heathen

/perform paddle_desk
-> exit


= surprise_interrupt

TODO: Vibrate controllers with rumble?
/sound goddess_interruption
/ambient none
/wait 2
/perform behold

Ahhh. <i>She comes</i>.
What a treat you have earned, trinket.

/perform idle
/music goddess
/wait 1
/scene None
I will see you again, moppet.
-> goddess_monitoring_intro_headmistress
