=== nega_reward_dice ===
-> select_gender ->

~ intro = nega_reward_dice == 1

/scene attic.negaintro
/music nega
/ambient off

- (give_reward)

/character nega hand_out
Hand it over. #01

+ [pose:present_wrist_reward]
+ [timeout] -> give_reward
+ [no] -> Nega_CheckIfInterested -> give_reward

- /perform swipe_hand
~ removeReward()

/perform chuckle
Fuck ya! #02
You're my bitch now. #03

/perform squat_give_dice

+ [hold:dice]
    Let's see what you're going to get #04
    for this pretty little gift. #05
    /perform squat
- (roll) 

Go ahead. Roll them bones. #06

+ [roll]
+ [timeout] -> roll

- ~ temp result1 = getNegaDie1()
~ temp result2 = getNegaDie2()
~ temp result3 = getNegaDie3()
~ temp result = result1 + result2 + result3

Results: <> 
~ rollToText(result1)
<> + <>
~ rollToText(result2)
<> + <>
~ rollToText(result3)

{
    - intro && result > 0: -> intro_reward 
    - intro: -> intro_change_dice 
    
    - result == 3: -> result_reward_hard 
    - result == 2: -> result_reward_light 
    - result == 1: -> result_reward_tease 
    
    - result1 == 0 && result2 == 0 && result3 == 0: -> result_all_blank 
    - result == 0: -> result_no_reward 

    - result == -1: -> result_punishment_tease 
    - result == -2: -> result_punishment_light 
    - result == -3: -> result_punishment_hard 
}


- (result_reward_hard)
/lookat dice
/perform squat_lean_forward
Oooo, three cups! You are a lucky little shit. #18
That does <i>not</i> happen often. #19

/lookat player
/perform squat
I suppose I have to give you something very special, then, don't I? #20

-> check_consent ->

TODO: Randomly select from Nega's 'hard reward' scenes here.
{shuffle:
    - -> nega_reward_forcedsex
}


- (result_reward_light)

// This can only be two cups and a blank.

/lookat dice
/perform squat_lean_forward
Two cups and a blank, hmm? Not bad. #21

-> check_consent ->

TODO: Randomly select from Nega's 'light reward' scenes here.
{shuffle:
    - -> nega_reward_forcedsex
}


- (result_reward_tease)

// This can either be 2 cups and a skull or 1 cup and two blanks.

{ result1 == -1 || result2 == -1 || result3 == -1:
    /lookat dice
    /perform squat_lean_forward
    Two cups and a skull, hmm? #22
    /perform open_arms_shrug
    Skulls cancel cups, I'm afraid, so that's one cup for you. #23
- else:
    /lookat dice
    /perform squat_lean_forward
    Hmm.. one cup. #24
}

/lookat player
/perform squat
Not bad. But not great. #25
I suppose you deserve a little something for that. #26

-> check_consent ->

TODO: Randomly select from Nega's 'tease reward' scenes here.
{shuffle:
    - -> nega_reward_forcedsex
}


- (result_all_blank)

/lookat dice
/perform squat_lean_forward
Wow, that is by far the best bad luck I've ever seen. #27
/lookat player
You get nothing, of course, but for a roll like that... #28

/perform squat
{ negaLoreEmpty():
    perhaps I'll make an exception. #29
    -> check_consent ->
    -> result_punishment_tease_no_intro
- else:
    perhaps I'll let you in on a little secret. #30
    -> nega_lore
}

- (result_no_reward)

TODO: Write 'no reward' dialog here.

/lookat dice
/perform squat_lean_forward
Well look at that. Skull and cup cancel each other out. #31
/lookat player
Nothing for you today. Better luck next time, bitch. #32
/perform looming

-> exit


- (result_punishment_tease)

{ result1 == 1 || result2 == 1 || result3 == 1:
    /lookat dice
    /perform squat_lean_forward
    Two skulls and a cup, hmm? #33
    Cups cancel skulls, alas, so only one little skull for you. #34
- else:
    /lookat dice
    /perform squat_lean_forward
    Hmm.. one skull. #35
}

/lookat player
/perform squat
I suppose I can't go too hard on you, #36
/perform squat_lean_forward
but I bet I can still make you squirm. #37

-> check_consent ->

TODO: Randomly select from Nega's 'tease punishment' scenes here.

- (result_punishment_tease_no_intro)
{shuffle:
    - -> nega_punishment_trampling
}


- (result_punishment_light)

/lookat dice
/perform squat_lean_forward
Ooo, two skulls. #38
That means your ass is mine. #39
-> check_consent ->

TODO: Randomly select from Nega's 'light punishment' scenes here.
{shuffle:
    - -> nega_punishment_trampling
}


- (result_punishment_hard)

/lookat dice
/perform squat_lean_forward
Mmmm, look at that. #40
Three skulls. I'm going to enjoy <i>this</i>. #41

-> check_consent ->

TODO: Randomly select from Nega's 'hard punishment' scenes here.
{shuffle:
    - -> nega_punishment_trampling
}


- (intro_change_dice)
/lookat dice
/perform squat_lean_forward
Aaaw, look at that. No fun. #42

/lookat player
/perform squat
Since it's your first time,  #43
perhaps I'll make it rain for you. #44
Just this once. #45

/perform squat_swipe_hand
/action force_dice


- (intro_reward)
/lookat dice
/perform squat_lean_forward
Ooo. Looky here. #46
Someone just got lucky! #47

/lookat player
/perform squat
Oh goody! This is gonna be so much fun! #48
You're gonna love getting rewards from me. #49

-> nega_reward_forcedsex

= check_consent

{shuffle:
    - It's not too late to back out, {isBoy: worm|slit}. #50
    - I'm going to use you hard. #51
    - You can still change your mind. #52
    - I wonder if you can handle me.  #53
    }

{shuffle:
    - Are you ready? #54
    - Shall I use you? #55
    - Ready for what's next? #56
    - Are you prepared to continue? #57
    }

+ [yes]
    Delish! #58
    ->->

+ [no]
    -> exit

= come_again
/perform head_toss
- {cycle:
    - Come visit me again, cunt. #59
    - Come back when you have another reward to spend. #60
    - I'll let you come see me again. #61
    - You know where to find me. #62
    }

-> demo_end
