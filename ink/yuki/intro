=== yuki_intro ===
-> select_gender_check ->

~ punishments = 1
~ rewards = 1
~ tutorial_step = at_yuki_intro
~ setScene(intro_scene, Yuki)

/scene entry.intro2
/music yuki
/ambient rain
/character yuki idle_yuki neutral
// /equip feather_duster

/perform idle_pleased happy 75
Oh! There you are! #01
/perform do_bouncing_little excited 75

I've been waiting for you. #02
/perform talk_consider happy 100

I'm Mistress Yuki-Onna. #03

/perform do_smooth_skirt amused 35
It's my job to make sure all new arrivals   #04
/perform idle_pleased amused 35
are taught how to behave  #05
while you're a guest in Goddess's home. #06

/perform talk_behold excited 69

First things first. #07

~ tries = 0
- (stand_straight)
~ tries++

/perform idle_yuki happy 30
Always stand up straight with your head high  #08
/perform idle_slave aroused 75
when you present yourself to a Mistress. #09

+ [pose:raised_chin] ->
+ { tries < 2 } [timeout] -> stand_straight
+ { tries >= 2 } [timeout] -> Yuki_Interested -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

- /perform react_clapping amused 40

You're such a good {player_name}! #10

- (new_guest)

/perform talk_one_finger amused 20

Do you know why you're here? #11

/perform idle_yuki happy 50

 + [yes]
    ~ addDisobey()
    /perform react_disappointed skeptical 80

    I don't see how you'd know that already. #12
    /perform talk_consider amused 50
    Someone must have told you. #13

    /perform talk_lean_forward happy 40

    Well, let's go through the basics anyways,  #14
    to make sure you have the right information.  #15


+ [no]
    ~ addObey()
    /perform react_head_toss_casual amused 70
    Well, duh! #16
    You're not supposed to know. #17
    /perform do_snap_fingers amused 50
    That's the fun part! #18

+ [timeout]
    /perform react_shrug_long angry 75
    Hello-o #19
    Anybody home? #20
    -> new_guest

+ [distracted] -> Yuki_PayAttention -> new_guest

- (house_rules)
/perform do_arm_raise excited 75
You are a guest of Threshold House. #21
/perform talk_consider skeptical 60
Well, we call it a house, but it's not really one... #22

/perform idle_sassy skeptical 60

All guests must follow the rules #23
/perform do_shake_finger_stern aroused 60
or you will not be allowed to stay. #24
/perform idle_unhappy disgusted 50

- (learn_rules)
/perform idle_yuki excited 50
Would you like to know what the rules are? #25

+ [yes]
    ~ addObey()
    /perform idle_pleased amused 50
    Oh goody! I love telling people the rules! #26
    -> two_rules

+ [no]
    ~ addDisobey()
    /perform react_disappointed sad 50
    Don't be a pouty baby. #27
    /perform react_disappointed disgusted 50
    You have to learn the rules or you can't be a good {player_name}! #28
    -> two_rules

+ [timeout]
    /perform talk_annoyed disgusted 50
    You aren't listening very well. #29
    -> learn_rules

+ [distracted] -> Yuki_PayAttention -> learn_rules


= two_rules

/perform talk_consider aroused 50
The rules are simple! #30

/perform talk_two_fingers excited 50
And there's only two of them! #31

/perform talk_one_finger amused 30

Answer questions when you're asked. #32

/perform talk_two_fingers aroused 80

Follow directions when they're given. #33

/perform talk_emphatic_yuki scared 20

Disobey and be punished. #34

/perform do_blow_kiss happy 60
Obey and be rewarded. #35

- (easy_rules)
/perform talk_consider happy 30
Easy, right? #36

+ [yes]
    /perform do_bouncing_eager happy 50
    ~ addObey()
    I know! Our Goddess is so kind! #37

+ [no]
    /perform react_disappointed angry 20
    ~ addDisobey()
    Oh! Don't worry. You'll learn fast! #38

+ [timeout]
    /perform idle_unhappy angry 60
    Are you still listening? #39
    -> easy_rules

+ [distracted] -> Yuki_PayAttention -> easy_rules

//insert puppy pose test

- /perform talk_behold
Let's practice! #45

/perform idle_yuki happy 60
I really love puppies. #169
Petting them. Kissing them. Feeding them. Walking them. #170

/emotion happy 50
Would you like to be my puppy? #171

+ [yes]
    ~ addObey()
    /perform do_bouncing_big happy 60
    Oh my goodness! I'm just so excited! #172
    -> puppy_beg

+ [no]
    ~ addDisobey()
    /perform react_disappointed sad 60
    What? But I'm so nice to my pets. #173
    You're not doing a good job of following instructions. #174

- Just try it once for me, okay? #175

+ [yes]
    ~ addObey()
    /perform do_bouncing_little excited 60
    I promise this will be worth it! #176
    -> puppy_beg

+ [no]
    ~ addDisobey()
    /perform idle_fake_annoyed angry 50
    Well, that's really a shame! #177
    Maybe I'm just too nice for you. #178
    I hope that's not true! #179
    -> good_yuki

//make it to where the yuki ending gives them another chance to be a puppy or sets a variable on them that they don't like puppy play


= puppy_beg

~ tries = 0
- (retry)
~ tries++

/perform talk_one_finger amused 50
First, puppy has to beg for my attention. #180

/perform do_point_down excited 80
Down on your knees and raise your hands to beg. #181

+ [pose:puppy_beg]
+ { tries < 2 } [timeout] -> retry
+ { tries >= 2 } [timeout] ->

- /perform react_excited happy 50
Oh, that's so perfect!  #182
You did so well following instructions. #183

/perform idle_yuki happy 50
Stand back up now, {player_name}. #184

~ tries = 0
- (stand_straight2)
~ tries++

+ [pose:raised_chin]
+ { tries < 2 } [timeout] -> stand_straight2
+ { tries >= 2 } [timeout] -> Yuki_Interested -> stand_straight2
// + [no] -> CheckIfInterested -> stand_straight

/perform do_bouncing_little excited 50
I can't wait until I can play with my new puppy! #185

- /perform talk_consider aroused 70
You are a cute one! I really hope you do well here! #40
Giving rewards is so much more fun than punishing. #41
-> good_yuki


= good_yuki

/perform talk_question happy 70
Do you like being a good {player_name}? #42

+ [yes]
    ~ addObey()
    /perform idle_pleased excited 30
    Yay! We'll have so much fun together! #43

+ [no]
    ~ addDisobey()
    /perform react_oh_darn_yuki sad 30
    Oh darn. I really hope you'll change your mind. #44

+ [timeout]
    /perform idle_clasp_hands skeptical 70
    Are you still there? #186
    -> good_yuki

+ [distracted] -> Yuki_PayAttention -> good_yuki

- (nega_interruption)
/scene entry.intro3

/character yuki idle_unhappy
/music nega
/lookat nega

/character nega react_head_toss_casual
/lookat yuki

Oh please! You're always such a good girl, Yuki, #46
/perform idle_bored disgusted 70
never having any fun. #47

/perform react_whatever amused 80
Don't you want to live a little? #48

/perform react_head_toss_casual aroused 80
Bend the rules once in a while and really let loose? #49
/perform idle_sassy aroused 80

/character yuki react_disappointed angry 60
No, thank you! #50

/character nega whisper_to_right aroused 60
//use whisper voice
I know you do.  #51
I know your deepest, darkest secrets. #52

/perform tease_caress_breast_left aroused 100
Remember? #53

+ [continue]

- /character yuki idle_unhappy_stomp angry 60
You just stop that right now, Nega-Yuki! #54

/perform idle_pleased happy 50
I want my own room someday.  #55

/perform react_disappointed angry 60
There's no way I'm breaking the rules! #56

/perform idle_pleased happy 60
I really want the Goddess to be pleased with me. #57
/perform idle_swaying excited 60

- (test_lookat)

/character nega react_dismissive angry 60
Ugh. You're such a goody two-shoes. #58
/character yuki talk_bashful sad

/character nega tease_dance_slow amused 60
You'd have so much more fun if you bent the rules now and then. #59

- (new_one)

/character Yuki idle_unhappy angry 50
/lookat player

/character nega
/lookat player

/perform do_inspect amused 40
So. You're the new one, huh? #60

-(nega_ask_again)

+ [yes]
    ~ addObey()
    /perform react_shrug_short disgusted 40
    Not too bad. You have potential. #61

    /perform talk_consider amused 40
    Though, I'm not too picky. #62

+ [no]
    ~ addDisobey()
    /perform idle_sassy aroused 70
    Not new, huh? #63

    /perform idle_fake_annoyed happy 70
    This one is more confused than most. #64

+ [timeout]
    /perform idle_unhappy_stomp angry 70
    Are you stupid, or something? #65

    /perform talk_lean_forward disgusted 70
    I asked you if you're new here. #66
    -> nega_ask_again

- /perform talk_lean_forward amused 60
I bet Yuki's been having so much fun #67
explaining the rules to you  #68
/character yuki idle_fake_annoyed angry 50
/character nega react_scoffs aroused 60
and telling you how to be a good {player_name}, #69
hasn't she? #70
/perform idle_skeptical

+ [yes]
    ~ addObey()
    /character yuki idle_unhappy_stomp angry 
    /character nega idle_bored scared 20
    Boring. #71

+ [no]
    ~ addDisobey()
    /character yuki idle_pleased happy 50
    /character nega talk_consider aroused 60
    A little liar.  #72
    We're going to get along just fine. #73

+ [timeout] -> Nega_Timeout ->

+ [distracted] -> Nega_PayAttention ->

- /character nega idle_cocks_head_to_left aroused 60
//her eyes bug out here. is lookat player messing her eyes up?
/lookat player
Don't let her fool you.  #74

/perform react_head_toss_casual disgusted 40
She's not as sweet and innocent as she lets on. #75
/character yuki react_shake_head_disbelief skeptical

//insert teaching player to get slapped
/character nega tease_caress_breasts aroused 40

- (rough)
I bet you like it rough. #187
/character yuki idle_unhappy angry 50
Let's find out how rough, shall we? #188

/character nega idle_sassy

+ [yes]

+ [no]
    /character nega react_scoffs disgusted
    What a pathetic whimp. #189
    -> confirm_no_slap
    
+ [timeout] -> Nega_Timeout -> rough

+ [distracted] -> Nega_PayAttention -> rough

- (nega_slap)
/perform talk_one_finger aroused 40
I'm going to show you both how slaves ought to be trained. #190
Pain is such an effective tool. #191
/perform idle_skeptical aroused 40
Time to see how you can take a slap. #192

- (slap)
When I swing my hand, you better fucking jerk your head. #193
Ready? #194

+ [yes]
    ~ addDisobey()
    That's right, bitch. #195
    You'll take this like a good little cunt. #196
    -> try_slap

+ [no]
    ~ addObey()
    /perform idle_sassy disgusted 40
    No? #197
    Then you're not going to like me very much. #198
    
+ [timeout] -> Nega_Timeout -> slap

+ [distracted] -> Nega_PayAttention -> slap

//change nega ending so if the player says no to this, she reminds them that visiting her means the hard stuff and they have to take a slap
- (confirm_no_slap)
/perform idle_unhappy angry 40
Obedient pain sluts get special rewards around here. #199
- (slap_ready)
Do you want to change your mind and take a slap? #200

+ [yes]
    ~ addDisobey()
    /perform idle_sassy excited 40
    Ahhhh. Sweet music to my ears! #201
    When I swing my hand, you better fucking jerk your head. #193

+ [no]
    ~ addObey()
    /perform react_scoffs disgusted 70
    Pity. You have no idea what you're missing. #202
    -> going_on

+ [timeout] -> Nega_Timeout -> slap_ready

+ [distracted] -> Nega_PayAttention -> slap_ready

//set variable on player that they don't want pain and didn't get slapped

//need to add slap training

- (try_slap)
/perform do_slap amused 70

+ [react-success]
     Not too bad. You have potential. #61
    // -> Nega_GoodJob ->
+ [react-fail]
    That was pathetic. Try again. #203
    -> try_slap

- (going_on)
/perform idle_tease amused 50
Have you figured out what's going on here yet? #76

+ [yes]
    ~ addDisobey()
    /perform talk_consider amused 50
    Hmmm. You might just be smarter than you look, {player_name}. #77

    /perform talk_consider aroused 50
    We'll find out soon enough. #78

    /perform idle_bored skeptical 50
    /lookat player
    Although, honestly, the dumb ones make better fuck toys. #79

+ [no]
    ~ addObey()
    /perform react_disappointed disgusted 50
    Typical. <i>She</i> always wants them to learn the hard way. #80

    /perform idle_unhappy disgusted 70
    /lookat yuki
    I think I should tell {isBoy:him|her}. #81
    /perform idle_cocks_head_to_left amused 70

    /character yuki idle_unhappy_stomp angry 40
    /lookat nega
    Don't you dare! #82
    The Goddess will be so angry with you! #83
    /perform talk_one_finger disgusted 40

    /character nega react_scoffs disgusted 70
    Oh fine, party pooper! #84


    /perform talk_lean_forward aroused 60
    /lookat player
    I guess I'll just make {isBoy:him|her} my fuck toy instead. #85

+ [timeout]
    /perform talk_annoyed disgusted 70
    Are you stupid, or something? #86

    /perform talk_serious angry 70
    I asked you if you're new here? #87
    -> going_on

+ [distracted] -> Nega_PayAttention -> going_on

- (caress_question)

{hasPenis:
    /character nega tease_stroke_motion aroused 80
- else:
    /character nega idle_tease aroused 80
}

/lookat player

I bet that {hasPenis:cock|cunt} of yours works just fine. #88

/perform idle_eager amused 80
Let's find out. Shall we? #89

- (genitals)

+ [yes]
    ~ addDisobey()
    /perform tease_caress_breasts happy 50a
    Ooo. I'm going to like this one. {isBoy: He|She} likes to be naughty. #90

+ (caress_skipped) [no]
    ~ addObey()
    /perform idle_unhappy_stomp disgusted 90
    Oh great. Another spoil-sport, just like Yuki. #91
    -> skip_caress

+ [timeout]
    /perform talk_question skeptical 30
    Should we find out how well your {hasPenis:dick|pussy} works? #92
    -> genitals

+ [distracted] -> Nega_PayAttention -> genitals

- /character yuki react_disappointed angry 60
Behave, Nega!  #93
/perform do_shake_finger_stern skeptical 30

/character nega caress_genitals aroused 70

That's it... #94
Let me feel your {hasPenis:cock swell|pussy drip}. #95

Mmmm. Just as I thought. #96
You want to be a little fuck toy, don't you? #97

- (caress_genitals)
+ [yes]
    ~ addDisobey()
    What a naughty {player_name}. #98
    I feel how {hasPenis:hard|wet} you are. #99
    Do you want to be my fuck toy? #100

    ++ [yes]
        ~ addDisobey()
        /perform react_disappointed skeptical 75
        Too bad. I only use fuck toys that have something to give me. #101

        /perform do_tap_wrist angry 50
        And you can't give me what I want yet. #102

    ++ [no]
        ~ addObey()
        /perform tease_ass_caress amused 50
        No? You don't like me? #103
        Awww. But I'm way more fun than that uptight bitch. #104
        /perform do_snap_fingers amused 50

    ++ [timeout]
        /perform talk_lean_forward skeptical 70
        I must be boring you. #105

+ [no]
    ~ addObey()
    /perform react_whatever disgusted 70
    What a prude. You'll change your mind soon enough. #106
    /perform react_head_toss_casual
    They always do. #107

+ [timeout]
    /perform talk_question angry 40
    Well, do you want to be used and abused? #108
    -> caress_genitals

- (skip_caress)
/perform talk_consider happy 60
The Mistresses will have so much fun #109
tearing up that ass of yours. #110

/perform talk_lean_forward aroused 70
//nega whispers
<i>Tick tock little mouse.</i> #111
<i>You might want to run.</i> #112
/perform do_you_leave amused 75
<i>You're trapped in this house.</i> #113
/perform idle_sassy aroused 70
<i>And we own your cum.</i> #114
/perform react_laugh amused 70
Hahahaha! #207



+ [wait]
-/character nega idle_skeptical amused 70
/lookat yuki
/character yuki idle_unhappy_stomp angry 70
/lookat nega
That's enough, Nega! #115
/perform react_disappointed disgusted 45
You are interfering too much! #140
/perform talk_one_finger scared 45
I'll be reporting you to the Headmistress straight away. #117
/perform idle_unhappy_stomp angry 70

{obeyed():
    - -> Yuki_finish
    - else: -> Nega_finish
}


= Yuki_finish

/character nega talk_lean_forward skeptical 70
/lookat player
I'll see you again soon, little mouse. #118

/perform idle_bored skeptical 70
Come find me when you've earned a reward. #119

/scene entry.intro4yuki
/music yuki


- /character yuki talk_annoyed scared 30
Oh, gee. She's so awful! #120
/perform do_shake_finger amused 30
Don't you pay any attention to her. #121

/perform do_bouncing_little aroused 60
She's going to get in big trouble one of these days! #122

- {caress_skipped: -> rules}

/perform do_inspect sad 30
Your poor {hasPenis:penis|pussy} must be aching after that. #123

+ [wait:1]

- (rules)
/perform talk_consider amused 60
Now... where was I? #124
/perform talk_lean_forward amused 60
Right! The rules! #125

/perform do_tap_wrist happy 60
Now, look at your wrists. #204
See that black mark? #205
You already have a punishment for sneaking into the Goddess's bed chambers. #126
/perform react_excited amused 60
Naughty {player_name}. #127
The Headmistress will take care of that! #128

/perform talk_consider excited 60
Make sure to come back to me when you're finished. #129

/perform do_tap_wrist happy 60
See that red stone on your other wrist? #206
It looks like the Goddess also gave you a reward. #130
You're so lucky to catch Her interest! #131

/perform idle_yuki happy 60
But I can't reward you if you don't take your punishment first. #132

/perform do_smooth_skirt amused 60
Okay! That's all I have for now. #133

/perform do_you_leave angry 30
Hurry up!  #134
The Headmistress doesn't like to be kept waiting. #135

/perform talk_serious scared 30
And stay away from that awful Nega-Yuki! #136
/perform react_disappointed disgusted 80
She's a bad seed! #137

/perform idle_yuki angry 50
You'll get a punishment if you get caught coming out of her room. #138

~ tutorial_step = at_hm_intro
~ tutorial_yuki_intro_complete = true
~ tutorial_yuki_intro_yuki_ending = true
-> entry_hub


= Nega_finish

/scene entry.intro4nega

/character nega react_laugh amused 70

+ [wait]

- /perform react_scoffs amused 80
Run off, little Yuki,  #142
/perform idle_skeptical disgusted 70
and tell the Headmistress all about  #143
how naughty I've been. #144

/perform tease_ass_caress aroused 80
She thought she could shove down #145
all her dirty thoughts  #146
by casting me aside... #147

/perform talk_behold happy 60
But here I am! #148

- {caress_skipped: -> skip_caress2}

/perform tease_caress_breasts aroused 70
Now we can really have some fun! #149

- (caress_again)
/perform idle_tease excited 60
Shall I rub your poor aching {hasPenis: cock|cunt} again #150
while I do? #151

+ [yes]
    /perform caress_genitals
    Mmmm. That's it.  #152
    We're going to find out just how naughty  #153
    you like to be. #154
    ++ [wait 5]

+ [no]
    /perform idle_unhappy_stomp angry 60
    Don't tell me I've made a mistake in  #155
    paying attention to you. #156

+ [timeout] -> Nega_Timeout -> caress_again

+ [distracted] -> Nega_PayAttention -> caress_again

- (skip_caress2)

/perform tease_dance_slow aroused 60
Be a good {player_name}, do what you're told, and  #157
that little {hasPenis: cock|cunt} of yours is going to #158
have so much fun when you come see me. #159

/perform talk_threaten aroused 80
But, if you decide to be a little {isBoy: prick|bitch}  #160
and not visit me... #161
/perform idle_bored skeptical 60
Well, I won't take kindly to being ignored. #162

/perform react_laugh amused 70

+[wait]

- /perform talk_consider excited 40
The best part is you can't collect any rewards  #163
until you've paid off all your punishments. #164

/perform react_disappointed disgusted 40

I'm bored with you.  #165

/perform idle_sassy amused 40
/lookat player
Time to get a taste of your first punishment. #166

/perform idle_clasp_hands happy 40
Better get your ass to the Headmistress. #167

/perform do_you_leave aroused 40
She doesn't like to be kept waiting. #168

~ tutorial_step = at_hm_intro
~ tutorial_yuki_intro_complete = true
~ tutorial_yuki_intro_nega_ending = true
-> entry_hub
