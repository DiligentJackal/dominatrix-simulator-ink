=== goddess_intro ===
-> select_gender_check ->

~ punishments = 0
~ rewards = 0
~ tutorial_step = at_goddess_intro
~ setScene(intro_scene, Goddess)

- (flash_scenes)

- (flash_1)
/scene bedroom.flash1
/music goddess
/ambient orgy

+ [wait:3]

- (flash_2)
/scene bedroom.flash2

+ [wait:3]

- (flash_3)
/scene bedroom.flash3

+ [wait:4]

/scene none
/wait 2

- (debug_jump)

/scene bedroom.intro
/ambient rain
/character goddess sassy angry 70


- Oh... How rude. #01
~ addPunishment()
/perform bored
And I was having so much fun. #02
/emotion angry 40
Ah, well... #03

/perform lean_forward amused 60
How did you get in here? #04

Did Mistress Yuki-Onna let you in? #05

/perform idle

- (ask_entry)
+ [yes]
    /perform annoyed angry 50
    I've told that girl not to interrupt me #06
    when I'm playing with my toys. #07
+ [no]
    /perform sassy amused
    No? Another curious guest, then. #08
+ (ask_entry2)[timeout]
    /emotion angry 50
    { cycle:
        - It's a simple question. Nod your head for yes. #09
        Shake your head for no. #10
        - I asked you a question. #11
        - Hurry up. I'm waiting. #12
        - I don't like it when people make me wait. #13
        - What makes you think you can ignore me? Answer me. #14
    }
    /emotion skeptical
    { shuffle:
        - Did my Yuki let you in? #15
        - Did that naughty little maid let you in? #16
        - Did an overly enthusiastic maid bring you to me? #17
        - Yuki-Onna is disobeying me again. She let you in, didn't she? #18
    }
    -> ask_entry
+ [distracted] -> Goddess_PayAttention -> ask_entry2

- /perform behold neutral
Well, you can't stay here. #19

/perform fake_annoyed

Only the truly exceptional may stay in my presence. #20
/perform lean_forward
And you are <i>obviously</i> not exceptional. #21
/emotion skeptical
Are you? #22

/perform idle

- (ask_exceptional)
+ [yes]
   /perform laugh skeptical
   Oh how absolutely delusional you are! #23
   /perform open_arms_shrug
   You are nowhere near worthy. #24
    
+ [no]
    /perform agreement happy 50
    Now, that is a very honest answer. #25
    /perform idle
    Perhaps I will give you a chance to prove your worth. #26
    /wait
    
+ (ask_exceptional_again)[timeout]
    /perform cocks_head_to_left disgusted 50
    Do you think you're exceptional? #27
    -> ask_exceptional
    
+ [distracted] -> Goddess_PayAttention -> ask_exceptional_again

- /perform agreement aroused 50
I bet that turned you on. Didn't it?<br/>Spying on me and my precious pets. #28
  
/perform idle

- (ask_spying)
+ [yes]
  /perform amused happy 50
  Of course it did. #29
  I bet you'd like to see more of that, wouldn't you? #30
  /perform lean_forward
  Perhaps you'd like to be one some day? #31
  /perform idle

+ (spying_no)[no]
    /perform thoughtful neutral
    No? How disappointing. #33
    /perform behold
    I don't think you're going to be much fun at all. #34
    -> ask_spying_done
    
+ [timeout]
    /perform inspect
    Be honest now. Spying on me turned you on, didn't it? #35
    -> ask_spying
    
+ [distracted] -> Goddess_PayAttention -> ask_spying_again

- (ask_spying2)

+ [yes]
    /perform fake_annoyed skeptical 50
    Well too bad. You haven't earned that privilege yet. #32

+ [no] -> spying_no

+ (ask_spying_again) [timeout]
    /perform thoughtful
    Be honest now. Spying on me turned you on, didn't it? #35
    -> ask_spying2

+ [distracted] -> Goddess_PayAttention -> ask_spying_again


- (ask_spying_done) /perform sassy behold
I bet you have no idea where you are, do you? #36
 
- (ask_where)
  
/perform idle

+ [yes]
    // /perform laugh
    /perform amused skeptical
    You think you know where you are? #37
    You have no clue. #38
    
+ [no]
    // /perform agreement
    /perform amused amused 50
    Of course you don't. #39
    
+ (ask_where_again)[timeout]
    /perform angry angry 50
    Well... do you have any idea where you are? #40
    -> ask_where
    
+ [distracted] -> Goddess_PayAttention -> ask_where_again

- /perform lean_forward amused 50
You're just a tiny little mouse stuck in a maze. #41

/perform behold
You may never find your way out again. #42

/perform thoughtful neutral

This is no ordinary house, mind you. #43

/perform annoyed

Even so. You are here now. #44

/perform bored

And you won't be going anywhere for quite a while. #45

/perform thoughtful skeptical 50

That means I'll have to give you a name. #46

/perform sassy excited 50

Would you like that? #47
  
/perform idle

+[yes] 
    /perform pleased happy 50
    Well, well. I must say I'm pleased. #48
    /perform indelible
    I think we are off to a good start. #49
    /perform fake_annoyed happy 50
    Don't you? #50
    
    /perform idle
    
    ++[yes]
        /perform cocks_head_to_left amused
        Keep this up and I might just take an interest in you. #51
    
    ++[no] -> DemandApology ->
        /perform inspect skeptical 50
        Now... as for a name. #52
    
+ [no]
    /perform fake_annoyed disgusted
    Too bad. #53
    Whatever name you had before is gone. #54

+ [timeout]
    /emotion angry 50
    Cat got your tongue? #55
    Well... I'll ignore that for now. #56

- /perform thoughtful amused 50

I will name you... "{player_name}". #57

/perform lean_forward

I expect you to respond to "{player_name}" from now on. #58
//re-reord this VO line
//Until someone gives you a new name, that is.
Until you earn a new name, that is. #59
/perform unhappy skeptical
Do you understand? #60

/perform idle

+ [yes] 
    /perform sassy happy 50
    -> Goddess_GoodJob ->

+ [no]
    /perform annoyed disgusted
    Let's try this again. Your name is now "{player_name}". #62
    /perform unhappy
    Everyone who meets you will refer to you as "{player_name}". #63
    One day, if you are very <i>very</i> good, you may earn a new name. #64
    /perform behold amused
    But for now, your name is "{player_name}". #65

+ [timeout]
    /perform open_arms_shrug amused
    Well, perhaps you're a little overwhelmed. #66
    That's to be expected. #67
    /perform lean_forward aroused
    Just remember your name is "{player_name}" #68
    /perform idle
    until I decide otherwise. #69

- /wait

/perform idle neutral

I really must send you away soon. #70
/perform bored neutral
But first, let me have a good look at you. #71

+ [continue]

~ tries = 0
- (inspection)
~ tries++

/perform point_down amused
{ cycle:
    - Come here. #72
    - Come here, {player_name}. #73
    - /emotion angry 
    I <i>said</i> come here! #74
}
  
+ [move]
    /wait
    -> Goddess_GoodJob ->
+ { tries < 3 } [timeout]
    -> inspection
+ { tries >= 3 } [timeout]
    -> CheckIfInterested -> inspection
+ [distracted] -> Goddess_PayAttention -> inspection

- /perform fake_annoyed amused

I'm always curious about our new guests. #75
/perform idle neutral
I so rarely take the time to inspect them personally #76
when they first arrive. #77
/perform inspect
This is a bit of an unexpected treat. #78
/perform lean_forward aroused 50
You don't mind, do you? #79

/perform idle

+ [yes] How unfortunate\. #80
    /perform annoyed disgusted
    Honestly, I don't even know why I bother asking. #81
+ [no]
    /perform sassy aroused
    Mmm... I think someone might be enjoying this. #82
+ [timeout]
    /perform behold skeptical
    That's alright. It's not like you have a choice anyway. #83
+ [distracted] -> Goddess_PayAttention -> stand_straight

~ tries = 0
- (stand_straight)
~ tries++

/perform unhappy neutral

{ cycle:
    - Stand up straight. #84
    - Come on. Lift that head a bit for me. #85
    - Chin up, little one. Let me inspect you. #86
}
  
/perform idle

+ [pose:raised_chin]
+ { tries < 3 } [timeout] -> stand_straight
+ { tries >= 3 } [timeout] -> CheckIfInterested -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

- -> Goddess_GoodJob ->

// /perform circle_player
/perform sassy aroused 50

That's it. #87
Push your shoulders back and arch that lovely spine. #88
/perform lean_forward
Stick that pretty little ass out where I can see it. #89

///wait for:circle_player
/perform idle
Now then... #90

~tries = 0
- (kneel)
~tries++

/perform point_down neutral

{ cycle:
    - Kneel. #91
    - /emotion skeptical
    Get down on your knees. #92
    - /emotion angry 50
    You belong on your knees. Down. #93
}

+ [pose:kneel]
+ { tries < 3 } [timeout] -> kneel
+ { tries >= 3 } [timeout] -> CheckIfInterested -> kneel
// + [no] -> CheckIfInterested -> kneel

- -> Goddess_GoodJob ->

/perform pleased aroused

Mmmmm... so much delicious flesh to work with. #94

/perform amused

Strong bones and muscles. #95
/perform thoughtful
You'll need to be flexible, as well. #96
/perform fake_annoyed
Oh yes... There is much here we can work with. #97
~ addReward()
~ tries = 0

- (present_self)
~ tries++

/perform unhappy amused

Arms behind your head. #98
Thrust your chest out. Knees apart. #99

/perform idle neutral

I want to see you present yourself more fully to me. #100
  
/perform idle

+ [pose:present]
+ { tries < 2 } [timeout] -> present_self
//+ { tries >= 2 } [timeout]
+ { tries >= 2 } [timeout] -> CheckIfInterested -> present_self
// + [no] -> CheckIfInterested -> present_self

- -> Goddess_GoodJob ->

/perform fake_annoyed aroused

Look at you. So obedient. So ready. #101

/wait 2
/perform behold neutral
You may go. But first, a reward. #102
/perform thoughtful amused
You may show me how much you appreciate my instruction. #103

/perform present_foot

You may bow before me and kiss my feet. #104

~ tries = 0
- (kiss_feet)
~ tries++

/perform present_foot

Bow down, kiss, then back to your pose like a good little {player_name}. #105

+ [pose:kiss_feet]
+ { tries < 3 } [timeout] -> kiss_feet
+ { tries >= 3 } [timeout]
+ [no] No?! #106
    /perform confused
    Turning down such a reward. You are a strange one. #107

- /perform angry neutral

That's enough. #108

~ tries = 0
- (stand_up)
~ tries++

//new line with vo 
/perform idle
Come back up to your feet, {player_name}. #158

+ [pose:raised_chin]
+ { tries < 3} [timeout] -> stand_up
+ { tries >= 3} [timeout]

- /emotion amused 
You might just prove useful one day, #109

/perform bored neutral

but you'll need much more training first. #110

/performance agreement
Off to Mistress Yuki-Onna with you. #111
/perform behold
I'm sure she can show you the ropes, so to speak. #112


+ [wait]

- (video_jump)

-> ByeForNow ->

- ~ yuki_shortcut = true
~ tutorial_step = at_yuki_intro
~ tutorial_goddess_intro_complete = true

// If this is the demo with just the Goddess, end the demo.
{deviation == demo:
    -> demo_end
- else:
    -> exit
}



= DemandApology

/perform angry angry
/wait

{ cycle:
    - Don't pretend to toy with me. #140
    - I am not amused. #141
    - Such behaviour is unacceptable. #142
    - You are trying my patience, worm. #143
}

/emotion angry 50
You will apologize this instant. #144

/perform sassy

Tell me: Do you beg for my forgiveness? #145

+ [yes] -> Goddess_GoodJob ->
+ [no] -> CheckIfInterested ->

- /perform unhappy skeptical

You are forgiven. #146
Don't let it happen again. #147
->->


= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform unhappy disgusted

These are the sorts of tasks demanded of you here. #148
- (check_again)

/perform behold skeptical

Are you willing to simply do as I say like a good {player_name}? #149

/perform idle

+ [yes] -> IsInterested
+ [no]
+ [timeout] -> check_again

- (check_terse)

/emotion skeptical

Really? #150

/perform angry 50

We will need to be rid of you if you can't perform. #151

Your willingness to serve is, after all, why you are here. #152

/perform annoyed neutral

So I'll ask one last time: #153

/perform lean_forward angry 50
Are you willing to perform for your goddess? #154

+ [yes] -> IsInterested
+ [no] -> NotInterested


= IsInterested

-> Goddess_GoodJob ->
/perform confused skeptical
There is hope for you yet. #157

->->


= NotInterested

/perform annoyed disgusted
Then away with you. #155
Perhaps another time when you are ready to learn your place. #156

-> ByeForNow -> demo_end


= ByeForNow

/perform snap_fingers

/scene bedroombroken.pissed

+ [wait 1.5]

- /character goddess looming_huge angry 50

Bye for now, {player_name}. #113

+ [wait:5] ->->





